from Standard.Base import all
import Standard.Base.Data.Base_64.Base_64
import Standard.Base.Enso_Cloud.Enso_Secret.Derived_Secret_Value
import Standard.Base.Enso_Cloud.Enso_Secret.Enso_Secret_Error
import Standard.Base.Errors.Common.Forbidden_Operation
import Standard.Base.Errors.Common.Not_Found
import Standard.Base.Errors.Illegal_Argument.Illegal_Argument
import Standard.Base.Errors.Illegal_State.Illegal_State
import Standard.Base.Network.HTTP.Request.Request
import Standard.Base.Runtime.Context
from Standard.Base.Enso_Cloud.Enso_Secret import as_hideable_value

from Standard.Test import all
from Standard.Test.Execution_Context_Helpers import run_with_and_without_output


import project.Network.Enso_Cloud.Cloud_Tests_Setup.Cloud_Tests_Setup
import project.Network.Enso_Cloud.Cloud_Tests_Setup.Mock_Credentials
import project.Network.Enso_Cloud.Cloud_Tests_Setup.Temporary_Directory

polyglot java import org.enso.base.enso_cloud.EnsoSecretAccessDenied
polyglot java import org.enso.base.enso_cloud.ExternalLibrarySecretHelper

add_specs suite_builder setup:Cloud_Tests_Setup =
    suite_builder.group "Enso Cloud Secrets" pending=setup.pending group_builder->
        subdirectory_for_tests = Temporary_Directory.make "Secrets"
        group_builder.teardown subdirectory_for_tests.cleanup

        group_builder.specify "should be able to list existing secrets" <| setup.with_prepared_environment <|
            # This should work regardless of Output context setting:
            run_with_and_without_output <|
                # We cannot test much more because we do not know what secrets are already there, further tests will check more by creating and deleting secrets
                Enso_Secret.list . should_be_a Vector

        group_builder.specify "should allow to create, list and delete secrets" <| setup.with_prepared_environment <|
            name = "my_test_secret"+Temporary_Directory.timestamp_text
            my_secret = Enso_Secret.create name "my_secret_value"
            my_secret.should_succeed
            my_secret.name . should_equal name
            my_secret.id.is_empty . should_be_false

            delete_on_fail my_secret <| Test.with_retries <|
                Enso_Secret.list . should_contain my_secret

            my_secret.delete . should_succeed

            Test.with_retries <|
                Enso_Secret.list . should_not_contain my_secret

        group_builder.specify "should allow to get a secret by name or path" <| setup.with_prepared_environment <|
            name = "my-test-secret-2-"+Temporary_Directory.timestamp_text
            created_secret = Enso_Secret.create name "my_secret_value"
            created_secret.should_succeed
            Panic.with_finalizer created_secret.delete <|
                Test.with_retries <|
                    fetched_secret = Enso_Secret.get name
                    fetched_secret . should_equal created_secret

                    path_secret = Enso_Secret.get "enso://Users/"+Enso_User.current.name+"/"+name
                    path_secret . should_equal created_secret

        group_builder.specify "does not allow both parent and path in Enso_Secret.get" <| setup.with_prepared_environment <|
            Enso_Secret.get "enso://Users/foo/SOME-SECRET" parent=Enso_File.home . should_fail_with Illegal_Argument

        group_builder.specify "should fail to create a secret if it already exists" <| setup.with_prepared_environment <|
            name = "my_test_secret-3-"+Temporary_Directory.timestamp_text
            created_secret = Enso_Secret.create name "my_secret_value"
            created_secret.should_succeed
            wait_until_secret_is_propagated created_secret
            Panic.with_finalizer created_secret.delete <|
                Test.with_retries <|
                    r1 = Enso_Secret.create name "my_secret_value"
                    r1.should_fail_with Illegal_Argument
                    r1.catch.to_display_text . should_contain "already exists"

        group_builder.specify "should allow to use secrets in HTTPS request headers" pending=setup.httpbin_pending <| setup.with_prepared_environment <|
            secret1 = Enso_Secret.create "my_test_secret-6"+Temporary_Directory.timestamp_text "Yet another Mystery"
            secret1.should_succeed

            Panic.with_finalizer secret1.delete <| Test.with_retries <|
                https = setup.httpbin_secure_client
                response = https.request (Request.get (setup.httpbin_secure_uri / "get") headers=[Header.new "X-My-Secret" secret1])
                response.decode_as_json.at "headers" . at "X-My-Secret" . should_equal "Yet another Mystery"

        group_builder.specify "should allow to derive values from secrets in Header.authorization_bearer" pending=setup.httpbin_pending <| setup.with_prepared_environment <|
            secret_token = Enso_Secret.create "my_test_secret-7"+Temporary_Directory.timestamp_text "MySecretToken"
            secret_token.should_succeed

            Panic.with_finalizer secret_token.delete <| Test.with_retries <|
                https = setup.httpbin_secure_client
                response = https.request (Request.get (setup.httpbin_secure_uri / "get") headers=[Header.authorization_bearer secret_token])
                response_json = response.decode_as_json
                response_json.at "headers" . at "Authorization" . should_equal "Bearer MySecretToken"

        group_builder.specify "should allow to derive values from secrets in Header.authorization_basic" pending=setup.httpbin_pending <| setup.with_prepared_environment <|
            secret_username = Enso_Secret.create "my_test_secret-8"+Temporary_Directory.timestamp_text "MyUsername"
            secret_username.should_succeed
            Panic.with_finalizer secret_username.delete <|
                secret_password = Enso_Secret.create "my_test_secret-9"+Temporary_Directory.timestamp_text "MyP@ssword"
                secret_password.should_succeed
                Panic.with_finalizer secret_password.delete <| Test.with_retries <|
                    https = setup.httpbin_secure_client
                    response = https.request (Request.get (setup.httpbin_secure_uri / "get") headers=[Header.authorization_basic secret_username secret_password])

                    expected = "Basic " + (Base_64.encode_text "MyUsername:MyP@ssword")
                    response_json = response.decode_as_json
                    response_json.at "headers" . at "Authorization" . should_equal expected

        group_builder.specify "should allow to derive values from secrets" <| setup.with_prepared_environment <|
            secret1 = Enso_Secret.create "my_test_secret-10"+Temporary_Directory.timestamp_text "Something"
            secret1.should_succeed
            Panic.with_finalizer secret1.delete <| Test.with_retries <|
                x = Derived_Secret_Value.from "X"
                y = Derived_Secret_Value.from "Y"
                v1 = x + y
                v2 = x + (Derived_Secret_Value.from secret1)

                v1.simplify . should_equal "XY"
                # Cannot simplify if it contains secrets
                v2.simplify . should_equal v2
                v2.to_plain_text . should_fail_with Enso_Secret_Error

                v1.to_text . should_equal "XY"
                v2.to_text . should_equal "X__SECRET__"

                b1 = Derived_Secret_Value.Base_64_Encode v1
                b2 = Derived_Secret_Value.Base_64_Encode v2

                b1.simplify . should_equal "WFk="
                b2.simplify . should_equal b2

                b1.to_text . should_equal "WFk="
                b2.to_text . should_equal "base64(X__SECRET__)"

        group_builder.specify "does not allow secrets in HTTP headers" pending=setup.httpbin_pending <| setup.with_prepared_environment <|
            secret1 = Enso_Secret.create "my_test_secret-11"+Temporary_Directory.timestamp_text "Something"
            secret1.should_succeed
            Panic.with_finalizer secret1.delete <| Test.with_retries <|
                uri = setup.httpbin_uri / "get"
                r1 = uri.fetch headers=[Header.new "X-My-Secret" secret1]
                r1.should_fail_with Illegal_Argument
                r1.catch.to_display_text . should_contain "Secrets are not allowed in HTTP connections, use HTTPS instead."

        group_builder.specify "API exposing secrets to external libraries should not be accessible from unauthorized code" <| setup.with_prepared_environment <|
            secret1 = Enso_Secret.create "my_test_secret-12"+Temporary_Directory.timestamp_text "Something"
            secret1.should_succeed
            Panic.with_finalizer secret1.delete <| Test.with_retries <|
                java_repr = as_hideable_value secret1
                Test.expect_panic EnsoSecretAccessDenied <|
                    ExternalLibrarySecretHelper.resolveValue java_repr

        group_builder.specify "should allow to create and delete secrets in a sub-directory" pending=setup.real_cloud_pending <| setup.with_prepared_environment <|
            subdirectory = subdirectory_for_tests.get
            nested_secret = Enso_Secret.create "my-nested-secret-1" "NESTED_secret_value" parent=subdirectory
            nested_secret.should_succeed

            delete_on_fail nested_secret <|
                Test.with_retries <|
                    Enso_Secret.list parent=subdirectory . should_contain nested_secret
                    Enso_Secret.exists "my-nested-secret-1" parent=subdirectory . should_be_true
                    Enso_Secret.get "my-nested-secret-1" parent=subdirectory . should_equal nested_secret

                # The secret from a subdirectory should not appear in the root list:
                Enso_Secret.list . should_not_contain nested_secret
                Enso_Secret.exists "my-nested-secret-1" . should_be_false
                Enso_Secret.get "my-nested-secret-1" . should_fail_with Not_Found

            nested_secret.delete . should_succeed

            # Secret should disappear from the list after deletion:
            Test.with_retries <|
                Enso_Secret.list parent=subdirectory . should_not_contain nested_secret
                Enso_Secret.exists "my-nested-secret-1" parent=subdirectory . should_be_false
                Enso_Secret.get "my-nested-secret-1" parent=subdirectory . should_fail_with Not_Found

        group_builder.specify "should allow to use secrets from a sub-directory" pending=(setup.real_cloud_pending.if_nothing setup.httpbin_pending) <| setup.with_prepared_environment <|
            nested_secret = Enso_Secret.create "my-nested-secret-2" "NESTED_secret_value" parent=subdirectory_for_tests.get
            nested_secret.should_succeed
            Panic.with_finalizer nested_secret.delete <|
                Test.with_retries <|
                    https = setup.httpbin_secure_client
                    response = https.request (Request.get (setup.httpbin_secure_uri / "get") headers=[Header.new "X-My-Nested-Secret" nested_secret])
                    response.decode_as_json.at "headers" . at "X-My-Nested-Secret" . should_equal "NESTED_secret_value"

        group_builder.specify "should allow to update secrets within a sub-directory" pending=(setup.real_cloud_pending.if_nothing setup.httpbin_pending) <| setup.with_prepared_environment <|
            nested_secret = Enso_Secret.create "my-nested-secret-3" "Value-A" parent=subdirectory_for_tests.get
            nested_secret.should_succeed
            Panic.with_finalizer nested_secret.delete <|
                Test.with_retries <|
                    https = setup.httpbin_secure_client
                    response = https.request (Request.get (setup.httpbin_secure_uri / "get") headers=[Header.new "X-My-Nested-Secret" nested_secret])
                    response.decode_as_json.at "headers" . at "X-My-Nested-Secret" . should_equal "Value-A"

                nested_secret.update_value "Value-New-B" . should_succeed

                # Not exactly sure if retries are needed here, but for test stability preferred to keep them.
                Test.with_retries <|
                    # Flushing caches to avoid the old value getting stuck after the first retry fails due to lack of propagation yet.
                    Enso_User.flush_caches
                    https = setup.httpbin_secure_client
                    response = https.request (Request.get (setup.httpbin_secure_uri / "get") headers=[Header.new "X-My-Nested-Secret" nested_secret])
                    response.decode_as_json.at "headers" . at "X-My-Nested-Secret" . should_equal "Value-New-B"

        group_builder.specify "should NOT be able to create/update/delete secrets with disabled Output Context" <| setup.with_prepared_environment <|
            Context.Output.with_disabled <|
                Enso_Secret.create "foo" "bar" . should_fail_with Forbidden_Operation

            secret_name = "my_test_secret-13-"+Temporary_Directory.timestamp_text
            secret1 = Enso_Secret.create secret_name "Something"
            secret1.should_succeed
            Panic.with_finalizer secret1.delete <|
                Context.Output.with_disabled <|
                    secret1.update_value "Something else" . should_fail_with Forbidden_Operation
                    secret1.delete . should_fail_with Forbidden_Operation
                    Enso_Secret.create ("foo"+Random.uuid) "baz" . should_fail_with Forbidden_Operation

                    # Get should still work
                    Test.with_retries <| Enso_Secret.get secret_name . should_equal secret1

        group_builder.specify "should be able to retry fetching a secret if the token is expired" pending=setup.httpbin_pending <| setup.with_prepared_environment <|
            mock_setup = Cloud_Tests_Setup.prepare_mock_setup
            mock_setup.with_prepared_environment <|
                secret1 = Enso_Secret.create "my_test_secret-"+Temporary_Directory.timestamp_text "Something123"
                secret1.should_succeed
                Panic.with_finalizer secret1.delete <|
                    credentials = Mock_Credentials.default mock_setup.httpbin_uri . remotely_expired
                    Cloud_Tests_Setup.run_with_mock_cloud custom_credentials=credentials <|
                        count_before = mock_setup.get_expired_token_failures_count

                        https = setup.httpbin_secure_client
                        response = https.request (Request.get (setup.httpbin_secure_uri / "get") headers=[Header.new "X-My-Secret" secret1])
                        response.decode_as_json.at "headers" . at "X-My-Secret" . should_equal "Something123"

                        # We also verify that one more request has failed due to expired token, proving that a retry was made.
                        count_after = mock_setup.get_expired_token_failures_count
                        count_after . should_equal count_before+1


main filter=Nothing =
    setup = Cloud_Tests_Setup.prepare
    suite = Test.build suite_builder->
        add_specs suite_builder setup
    suite.run_with_filter filter


wait_until_secret_is_propagated secret =
    Test.with_retries <| Enso_Secret.list . should_contain secret

delete_on_fail resource ~action =
    on_failure caught_panic =
        resource.delete
        Panic.throw caught_panic
    Panic.catch Any handler=on_failure action

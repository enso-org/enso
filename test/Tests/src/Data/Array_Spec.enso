from Standard.Base import all
import Standard.Base.Errors.Common.Index_Out_Of_Bounds
import Standard.Base.Errors.Illegal_State.Illegal_State

from Standard.Test import Test, Test_Suite
import Standard.Test.Extensions

Array.method self = 0

## Returns an array with the same contents as the given vector, surely backed by
   the Enso Array primitive.
make_enso_array vector =
    enso_array = Array.new vector.length
    Array.copy vector.to_array 0 enso_array 0 vector.length
    enso_array

test_arrays array_from_vector =
    Test.specify "should allow accessing elements" <|
        arr = array_from_vector [1, 2, 3]
        arr.at 0 . should_equal 1
        arr.at 2 . should_equal 3
        arr.at -1 . should_equal 3

    Test.specify "should panic on out of bounds access" <|
        arr = array_from_vector [1, 2, 3]
        (arr.at -4) . should_fail_with Index_Out_Of_Bounds
        (arr.at 3) . should_fail_with Index_Out_Of_Bounds

spec =
    Test.group "Enso Arrays" <|
        test_arrays make_enso_array

        Test.specify "should allow for functional dispatch on a method defined in this module" <|
            arr = make_enso_array [1, 2, 3]
            arr.method . should_equal 0

        Test.specify "should handle ==" <|
            (make_enso_array [1,2,3]).should_equal (make_enso_array [1,2,3])
            (make_enso_array [1]).should_not_equal (make_enso_array [2])

        Test.specify "should propagate dataflow errors" <|
            err = Error.throw (Illegal_State.Error "Foo")
            res = Array.new err
            res . should_fail_with Illegal_State

        Test.specify "should not sort in place" <|
            arr = make_enso_array [3, 1, 2]
            new_arr = arr.sort
            arr . should_equal [3, 1, 2]
            new_arr . should_equal [1, 2, 3]

    Test.group "Compare functionality with Vector" <|
        Test.specify "compare methods" <|
            vector_methods = Meta.meta Vector . methods . sort
            array_methods_all = Meta.meta Array . methods . sort

            array_methods = array_methods_all.filter (name -> name != "sort_builtin")

            vector_methods . should_equal array_methods

    Test.group "ArrayOverBuffer" <|
        location_pending = case Platform.os of
            Platform.OS.Windows -> "This test is disabled on Windows."
            _ -> Nothing

        Test.specify "should behave like an Array" pending=location_pending <|
            array_over_buffer = (File.new (enso_project.data / "sample.txt") . read_last_bytes 10).to_array

            case array_over_buffer of
                _ : Array -> Nothing
                _ -> Test.fail "Expected ArrayOverBuffer to match on Array type"

            array_over_buffer.to_text . should_equal "[32, 106, 117, 106, 117, 98, 101, 115, 46, 10]"
            sorted = array_over_buffer.sort
            array_over_buffer.to_text . should_equal "[32, 106, 117, 106, 117, 98, 101, 115, 46, 10]"
            sorted.to_text . should_equal "[10, 32, 46, 98, 101, 106, 106, 115, 117, 117]"


main = Test_Suite.run_main spec

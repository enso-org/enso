from Standard.Base import all
from Standard.Base.Data.List import Empty_Error

import Standard.Test

spec = Test.group "List" <|
    l = Cons 1 <| Cons 2 <| Cons 3 <| Nil
    empty = Nil
    Test.specify "should have properly defined length" <|
        l.length.should_equal 3
    Test.specify "should have well defined length when empty" <|
        Nil.length.should_equal 0
    Test.specify "should allow folding the list with an arbitrary operation with .fold" <|
        sum = l.fold 0 (+)
        prod = l.fold 1 (*)
        sum.should_equal 6
        prod.should_equal 6
    Test.specify "should allow checking if an element satisfies a predicate with .exists" <|
        any_even = l.exists (x -> x % 2 == 0)
        any_eq_five = l.exists (== 5)
        any_even.should_be_true
        any_eq_five.should_be_false
    Test.specify "should allow checking if an element satisfies a predicate with .any" <|
        any_even = l.any (x -> x % 2 == 0)
        any_eq_five = l.any (== 5)
        any_even.should_be_true
        any_eq_five.should_be_false
    Test.specify "should allow checking if all elements satisfy a predicate with `.all`" <|
        all_even = l.all(x -> x % 2 == 0)
        all_less_than_four = l.all (< 4)
        all_even . should_be_false
        all_less_than_four . should_be_true
    Test.specify "should allow checking if an element is in the list with `.contains`" <|
        l.contains 4 . should_be_false
        l.contains 3 . should_be_true
        empty.contains 10 . should_be_false
    Test.specify "should allow checking if the list is empty with `.is_empty`" <|
        l.is_empty . should_be_false
        empty.is_empty . should_be_true
    Test.specify "should allow checking if the list is not empty `.not_empty`" <|
        l.not_empty . should_be_true
        empty.not_empty . should_be_false
    Test.specify "should be convertible to a vector" <|
        (Cons 3 (Cons "a" (Cons 1 Nil))).to_vector.should_equal [3, "a", 1]
    Test.specify "should allow filtering of the list using `.filter`" <|
        l.filter (> 2) . should_equal (Cons 3 Nil)
    Test.specify "should filter elements by Filter_Condition" <|
        list = [1, 2, 3, 4, 5].to_list
        list.filter (Filter_Condition.Greater than=3) . should_equal [4, 5].to_list
        list.filter (Filter_Condition.Less than=3.5) . should_equal [1, 2, 3].to_list
        list.filter (Filter_Condition.Equal to=3) . should_equal (Cons 3 Nil)
        list.filter (Filter_Condition.Equal_Or_Greater than=3) . should_equal [3, 4, 5].to_list
        list.filter (Filter_Condition.Equal_Or_Less than=(-1)) . should_equal Nil
        list.filter (Filter_Condition.Between 2 4) . should_equal [2, 3, 4].to_list

        Test.expect_panic_with (list.filter (Filter_Condition.Starts_With "a")) No_Such_Method_Error_Data
        list.filter Filter_Condition.Is_True . should_equal Nil
        list.filter Filter_Condition.Is_False . should_equal Nil
        list.filter Filter_Condition.Is_Nothing . should_equal Nil
        list.filter Filter_Condition.Not_Nothing . should_equal list

        txt = ["aaa", "bbb", "abab", "cccc", "baaa", "ś"].to_list
        txt.filter (Filter_Condition.Contains "a") . should_equal ["aaa", "abab", "baaa"].to_list
        txt.filter (Filter_Condition.Contains 's\u0301') . should_equal ["ś"].to_list
        txt.filter (Filter_Condition.Starts_With "a") . should_equal ["aaa", "abab"].to_list
        txt.filter (Filter_Condition.Ends_With "a") . should_equal ["aaa", "baaa"].to_list
        txt.filter (Filter_Condition.Less than="a") . should_equal Nil
        txt.filter (Filter_Condition.Greater than="b") . should_equal ["bbb", "cccc", "baaa", "ś"].to_list
        txt.filter (Filter_Condition.Between "b" "c") . should_equal ["bbb", "baaa"].to_list
        Test.expect_panic_with (txt.filter (Filter_Condition.Starts_With 42)) Unsupported_Argument_Types_Data

        mixed = [1, Nothing, "b"].to_list
        mixed.filter Filter_Condition.Is_Nothing . should_equal (Cons Nothing Nil)
        mixed.filter Filter_Condition.Not_Nothing . should_equal (Cons 1 (Cons "b" Nil))

        bools = [True, False, Nothing, True].to_list
        bools.filter Filter_Condition.Is_True . should_equal [True, True].to_list
        bools.filter Filter_Condition.Is_False . should_equal [False].to_list
    Test.specify "should allow mapping a function over its elements with .map" <|
        l.map +1 . head . should_equal 2
    Test.specify "should allow executing an action for each element with .each" <|
        sum = State.run Number 0 <|
            l.each el->
                s = State.get Number
                State.put Number s+el
            State.get Number
        sum.should_equal 6
    Test.specify "should allow reversing with .reverse" <|
        l.reverse.head.should_equal 3
    Test.specify "should allow dropping elements from the left with `.drop`" <|
        l.drop_start 1 . should_equal (Cons 2 (Cons 3 Nil))
        empty.drop_start 1 . should_equal Nil
    Test.specify "should allow taking elements from the left with `.take_start`" <|
        l.take_start 2 . should_equal (Cons 1 (Cons 2 Nil))
        empty.take_start 2 . should_equal Nil
    Test.specify "should allow getting the head of the list with `.head`" <|
        l.head . should_equal 1
        empty.head.should_fail_with Empty_Error
    Test.specify "should allow getting the tail of the list with `.tail`" <|
        l.tail . should_equal (Cons 2 (Cons 3 Nil))
        empty.tail.should_fail_with Empty_Error
    Test.specify "should allow getting the init of the list with `.init`" <|
        l.init . should_equal (Cons 1 (Cons 2 Nil))
        empty.init.should_fail_with Empty_Error
    Test.specify "should allow getting the last element of the list with `.last`" <|
        l.last . should_equal 3
        empty.last.should_fail_with Empty_Error
    Test.specify "should allow getting the head of the list with `.first`" <|
        l.first . should_equal 1
        empty.first.should_fail_with Empty_Error
    Test.specify "should allow getting the tail of the list with `.rest`" <|
        l.rest . should_equal (Cons 2 (Cons 3 Nil))
        empty.rest.should_fail_with Empty_Error

main = Test.Suite.run_main spec

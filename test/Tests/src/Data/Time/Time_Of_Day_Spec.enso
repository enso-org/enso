from Standard.Base import all
import Standard.Base.Errors.Common.Incomparable_Values
import Standard.Base.Errors.Common.Type_Error
import Standard.Base.Errors.Illegal_Argument.Illegal_Argument
import Standard.Base.Errors.Time_Error.Time_Error

from Standard.Test import Test, Test_Suite
import Standard.Test.Extensions

polyglot java import java.time.LocalTime
polyglot java import java.time.format.DateTimeFormatter

spec =
    specWith "Time_Of_Day" enso_time Time_Of_Day.parse
    specWith "JavaLocalTime" java_time java_parse
    if Polyglot.is_language_installed "python" then
        specWith "PythonLocalTime" python_time python_parse nanoseconds_loss_in_precision=True

specWith name create_new_time parse_time nanoseconds_loss_in_precision=False =
    Test.group name <|

        Test.specify "should create local time" <|
            time = create_new_time 1 0 0
            time . hour . should_equal 1
            time . minute . should_equal 0
            time . second . should_equal 0
            time . to_seconds . should_equal 3600

        Test.specify "should handle errors when creating a time" <|
            case create_new_time 24 0 0 . catch of
                Time_Error.Error msg ->
                    msg.to_text . contains "24" . should_not_equal -1
                result ->
                    Test.fail ("Unexpected result: " + result.to_text)

        Test.specify "should format local time using provided pattern" <|
            text = create_new_time 12 20 44 . format "HHmmss"
            text . should_equal "122044"

        Test.specify "should format using provided pattern and locale" <|
            d = create_new_time 12 20 44
            # Note that the results are all the same.
            d.format "HH:mm" . should_equal "12:20"
            d.format "HH:mm" (Locale.new "gb") . should_equal "12:20"
            d.format "HH:mm" (Locale.new "fr") . should_equal "12:20"

        Test.specify "should format local time using default pattern" <|
            text = create_new_time 12 20 44 . to_text
            text . should_equal "12:20:44"

        Test.specify "should convert to Json" <|
            time = create_new_time 1 2 3
            time.to_json.should_equal <|
                time_pairs = [["hour", 1], ["minute", 2], ["second", 3], ["nanosecond", 0]]
                JS_Object.from_pairs ([["type", "Time_Of_Day"], ["constructor", "new"]] + time_pairs) . to_text

        Test.specify "should parse default time format" <|
            text = create_new_time 12 20 44 . to_text
            time = Time_Of_Day.parse text
            time.to_seconds . should_equal 44444

        Test.specify "should parse local time" <|
            time = parse_time "10:00:00"
            time.to_seconds . should_equal 36000

        Test.specify "should throw error when parsing invalid time" <|
            case parse_time "1200" . catch of
                Time_Error.Error msg ->
                    msg . should_equal "Text '1200' could not be parsed at index 2"
                result ->
                    Test.fail ("Unexpected result: " + result.to_text)

        Test.specify "should parse custom format" <|
            time = parse_time "12:30AM" "hh:mma"
            time.to_seconds . should_equal 1800

        Test.specify "should throw error when parsing custom format" <|
            time = parse_time "12:30" "HH:mm:ss"
            case time.catch of
                Time_Error.Error msg ->
                    msg . should_equal "Text '12:30' could not be parsed at index 5"
                result ->
                    Test.fail ("Unexpected result: " + result.to_text)

        Test.specify "should convert to time" <|
            datetime = create_new_time 1 0 0 . to_date_time (Date.new 2000 12 21) Time_Zone.utc
            datetime . year . should_equal 2000
            datetime . month . should_equal 12
            datetime . day . should_equal 21
            datetime . hour . should_equal 1
            datetime . minute . should_equal 0
            datetime . second . should_equal 0
            datetime . millisecond . should_equal 0
            datetime . microsecond . should_equal 0
            datetime . nanosecond . should_equal 0
            datetime . zone . zone_id . should_equal Time_Zone.utc.zone_id

        Test.specify "date-time conversion should work with interop values" <|
            date = Date.new 2000 12 21
            time = create_new_time 12 30 45
            datetime = date.to_date_time time
            datetime.date . should_equal date
            datetime.time_of_day . should_equal time

        Test.specify "should add time-based interval" <|
            time = create_new_time 0 + (Duration.new minutes=1)
            time . to_seconds . should_equal 60

        Test.specify "should subtract time-based interval" <|
            time = create_new_time 0 - (Duration.new minutes=1)
            time . to_seconds . should_equal 86340

        Test.specify "should support mixed interval operators" <|
            time = create_new_time 0 + (Duration.new hours=1) - (Duration.new seconds=1)
            time . to_seconds . should_equal 3599

        Test.specify "should throw error when adding date-based interval" <|
            Test.expect_panic_with matcher=Type_Error <|
                create_new_time 0 + (Period.new days=1)

        Test.specify "should throw error when subtracting date-based interval" <|
            Test.expect_panic_with matcher=Type_Error <|
                create_new_time 0 - (Period.new days=1)

        Test.specify "should support addition of Time_Period" <|
            time = create_new_time 0
            time+Time_Period.Day . should_equal <| create_new_time 0
            time+Time_Period.Hour . should_equal <| create_new_time 1
            time+Time_Period.Minute . should_equal <| create_new_time 0 1
            time+Time_Period.Second . should_equal <| create_new_time 0 0 1
            time+Time_Period.Millisecond . should_equal <| create_new_time 0 0 0 10^6
            if nanoseconds_loss_in_precision.not then
                time+Time_Period.Microsecond . should_equal <| create_new_time 0 0 0 10^3
                time+Time_Period.Nanosecond . should_equal <| create_new_time 0 0 0 1

        Test.specify "should support subtraction of Time_Period" <|
            time = create_new_time 12
            time-Time_Period.Day . should_equal <| create_new_time 12
            time-Time_Period.Hour . should_equal <| create_new_time 11
            time-Time_Period.Minute . should_equal <| create_new_time 11 59
            time-Time_Period.Second . should_equal <| create_new_time 11 59 59
            second_in_nanos = 10^9
            time-Time_Period.Millisecond . should_equal <| create_new_time 11 59 59 (second_in_nanos - 10^6)
            if nanoseconds_loss_in_precision.not then
                time-Time_Period.Microsecond . should_equal <| create_new_time 11 59 59 (second_in_nanos - 10^3)
                time-Time_Period.Nanosecond . should_equal <| create_new_time 11 59 59 (second_in_nanos - 1)

        Test.specify "should support mixed addition and subtraction of Date_Period and Time_Period" <|
            time = create_new_time 0
            time+Time_Period.Hour-Time_Period.Minute . should_equal <| create_new_time 0 59
            time+Time_Period.Minute+Time_Period.Minute-Time_Period.Minute . should_equal <| create_new_time 0 1

        Test.specify "should be comparable" <|
            time_1 = parse_time "12:30:12.7102"
            time_2 = parse_time "04:00:10.0"
            (time_1 == time_2) . should_be_false
            time_1==time_1 . should_be_true
            time_1!=time_2 . should_be_true
            time_1>time_2 . should_be_true
            time_1<time_2 . should_be_false
            time_2<(Date_Time.new 1999 1 1 4 0 10) . should_fail_with Incomparable_Values

        Test.specify "should correctly determine the type of timeofday" <|
            new_timeofday = create_new_time 15 37 58
            parsed_timeofday =  parse_time "10:00:00"

            Meta.type_of new_timeofday . should_equal_type Time_Of_Day
            Meta.type_of parsed_timeofday . should_equal_type Time_Of_Day

        max_nanos = 999999999
        Test.specify "should allow to find start/end of a Time_Period containing the current time of day" <|
            d1 = create_new_time 15 37 58 123456789
            d1.start_of Time_Period.Day . should_equal (Time_Of_Day.new)
            d1.end_of Time_Period.Day . should_equal (Time_Of_Day.new 23 59 59 nanosecond=max_nanos)
            d1.start_of Time_Period.Hour . should_equal (Time_Of_Day.new 15 0 0 0)
            d1.end_of Time_Period.Hour . should_equal (Time_Of_Day.new 15 59 59 nanosecond=max_nanos)
            d1.start_of Time_Period.Minute . should_equal (Time_Of_Day.new 15 37 0 0)
            d1.end_of Time_Period.Minute . should_equal (Time_Of_Day.new 15 37 59 nanosecond=max_nanos)
            d1.start_of Time_Period.Second . should_equal (Time_Of_Day.new 15 37 58 0)
            d1.end_of Time_Period.Second . should_equal (Time_Of_Day.new 15 37 58 nanosecond=max_nanos)
            d1.start_of Time_Period.Millisecond . should_equal (Time_Of_Day.new 15 37 58 nanosecond=123000000)
            d1.end_of Time_Period.Millisecond . should_equal (Time_Of_Day.new 15 37 58 nanosecond=123999999)

            if nanoseconds_loss_in_precision.not then
                d1.start_of Time_Period.Microsecond . should_equal (Time_Of_Day.new 15 37 58 nanosecond=123456000)
                d1.end_of Time_Period.Microsecond . should_equal (Time_Of_Day.new 15 37 58 nanosecond=123456999)
                d1.start_of Time_Period.Nanosecond . should_equal d1
                d1.end_of Time_Period.Nanosecond . should_equal d1

            d2 = create_new_time 0 0 0
            d2.start_of Time_Period.Day . should_equal (Time_Of_Day.new)
            d2.end_of Time_Period.Day . should_equal (Time_Of_Day.new 23 59 59 nanosecond=max_nanos)
            d2.start_of Time_Period.Hour . should_equal (Time_Of_Day.new 0 0 0 0)
            d2.end_of Time_Period.Hour . should_equal (Time_Of_Day.new 0 59 59 nanosecond=max_nanos)
            d2.start_of Time_Period.Minute . should_equal (Time_Of_Day.new 0 0 0 0)
            d2.end_of Time_Period.Minute . should_equal (Time_Of_Day.new 0 0 59 nanosecond=max_nanos)
            d2.start_of Time_Period.Second . should_equal (Time_Of_Day.new 0 0 0 0)
            d2.end_of Time_Period.Second . should_equal (Time_Of_Day.new 0 0 0 nanosecond=max_nanos)

            d3 = create_new_time 23 59 59 max_nanos
            d3.start_of Time_Period.Day . should_equal (Time_Of_Day.new)
            d3.end_of Time_Period.Day . should_equal (Time_Of_Day.new 23 59 59 nanosecond=max_nanos)
            d3.start_of Time_Period.Hour . should_equal (Time_Of_Day.new 23 0 0 0)
            d3.end_of Time_Period.Hour . should_equal (Time_Of_Day.new 23 59 59 nanosecond=max_nanos)
            d3.start_of Time_Period.Minute . should_equal (Time_Of_Day.new 23 59 0 0)
            d3.end_of Time_Period.Minute . should_equal (Time_Of_Day.new 23 59 59 nanosecond=max_nanos)
            d3.start_of Time_Period.Second . should_equal (Time_Of_Day.new 23 59 59 0)
            d3.end_of Time_Period.Second . should_equal (Time_Of_Day.new 23 59 59 nanosecond=max_nanos)

        Test.specify "should allow extracting a date_part" <|
            d1 = create_new_time 15 37 58 123456789
            d1.date_part Time_Period.Hour . should_equal 15
            d1.date_part Time_Period.Minute . should_equal 37
            d1.date_part Time_Period.Second . should_equal 58
            d1.date_part Time_Period.Millisecond . should_equal 123
            if nanoseconds_loss_in_precision.not then
                d1.date_part Time_Period.Microsecond . should_equal 456
                d1.date_part Time_Period.Nanosecond . should_equal 789

            d1.date_part Time_Period.Day . should_fail_with Illegal_Argument
            Test.expect_panic_with (d1.date_part Date_Period.Day) Type_Error

        Test.specify "should allow computing a date_diff" <|
            t1 = create_new_time 10 15 0
            t2 = create_new_time 12 30 20

            # There is no default period:
            t1.date_diff t2 . should_be_a Function
            Test.expect_panic_with (t1.date_diff t2 Date_Period.Month) Type_Error
            t1.date_diff t2 Time_Period.Day . should_fail_with Illegal_Argument

            t1.date_diff t2 Time_Period.Hour . should_equal 2
            t1.date_diff (Time_Of_Day.new 9 15 0) Time_Period.Hour . should_equal -1

            t1.date_diff t2 Time_Period.Minute . should_equal 135
            t1.date_diff (Time_Of_Day.new 10 04 0) Time_Period.Minute . should_equal -11

            t1.date_diff t2 Time_Period.Second . should_equal 8120
            t1.date_diff (Time_Of_Day.new 10 15 12) Time_Period.Second . should_equal 12

            t1.date_diff t2 Time_Period.Millisecond . should_equal 8120*1000
            t1.date_diff (Time_Of_Day.new 10 15 12 34) Time_Period.Millisecond . should_equal 12034

            if nanoseconds_loss_in_precision.not then
                t1.date_diff t2 Time_Period.Microsecond . should_equal 8120*1000*1000
                t1.date_diff (Time_Of_Day.new 10 15 12 34 56) Time_Period.Microsecond . should_equal 12034056
                t1.date_diff t2 Time_Period.Nanosecond . should_equal 8120*1000*1000*1000
                t1.date_diff (Time_Of_Day.new 10 15 12 34 56 78) Time_Period.Nanosecond . should_equal 12034056078

        Test.specify "should allow shifting with date_add" <|
            t1 = create_new_time 23 45 0

            t1.date_add -1 Time_Period.Hour . should_equal (Time_Of_Day.new 22 45 0)
            t1.date_add 1 Time_Period.Hour . should_equal (Time_Of_Day.new 0 45 0)
            t1.date_add -1 Time_Period.Minute . should_equal (Time_Of_Day.new 23 44 0)
            t1.date_add 15 Time_Period.Minute . should_equal (Time_Of_Day.new 0 0 0)
            t1.date_add -1 Time_Period.Second . should_equal (Time_Of_Day.new 23 44 59)
            t1.date_add 5 Time_Period.Second . should_equal (Time_Of_Day.new 23 45 05)
            t1.date_add 1 Time_Period.Millisecond . should_equal (Time_Of_Day.new 23 45 0 1)
            if nanoseconds_loss_in_precision.not then
                t1.date_add 123456 Time_Period.Microsecond . should_equal (Time_Of_Day.new 23 45 00 123 456)
                t1.date_add -2 Time_Period.Nanosecond . should_equal (Time_Of_Day.new 23 44 59 999 999 998)

            # No sense to shift Time_Of_Day by days
            t1.date_add 1 Time_Period.Day . should_fail_with Illegal_Argument
            Test.expect_panic_with (t1.date_add 1 Date_Period.Month) Type_Error

            # There is no default period.
            t1.date_add 10 . should_be_a Function

            Test.expect_panic_with (t1.date_add 1.5 Time_Period.Hour) Type_Error
            Test.expect_panic_with (t1.date_add 1.0 Time_Period.Hour) Type_Error

enso_time hour minute=0 second=0 nanoOfSecond=0 =
    Time_Of_Day.new hour minute second nanosecond=nanoOfSecond

java_time hour minute=0 second=0 nanoOfSecond=0 =
    Panic.catch Any (LocalTime.of hour minute second nanoOfSecond) (err -> Error.throw (Time_Error.Error <| err.payload.getMessage))

java_parse time_text pattern=Nothing =
    Panic.catch Any handler=(err -> Error.throw (Time_Error.Error err.payload.getMessage)) <|
        if pattern.is_nothing then LocalTime.parse time_text else
            formatter = DateTimeFormatter.ofPattern pattern
            LocalTime.parse time_text (formatter.withLocale Locale.default.java_locale)

python_time hour minute=0 second=0 nanoOfSecond=0 =
    Panic.catch Any (python_time_impl hour minute second nanoOfSecond) (err -> Error.throw (Time_Error.Error <| err.payload))

python_parse time_text pattern=Nothing =
    t = Panic.catch Any handler=(err -> Error.throw (Time_Error.Error err.payload.getMessage)) <|
        if pattern.is_nothing then LocalTime.parse time_text else
            formatter = DateTimeFormatter.ofPattern pattern
            LocalTime.parse time_text (formatter.withLocale Locale.default.java_locale)
    python_time t.hour t.minute t.second t.nanosecond

main = Test_Suite.run_main spec

foreign python python_time_impl hour minute second nanoOfSecond = """
    import datetime
    t = datetime.time(hour, minute, second, int(nanoOfSecond / 1000))
    return t

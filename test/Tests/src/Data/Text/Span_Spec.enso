
from Standard.Base import all
import Standard.Test

from Standard.Base.Data.Text.Span as Span_Module import Span, Utf_16_Span

spec = Test.group "Text.Span" <|

    Test.specify "should be able to be created over a text" <|
        text = "Hello!"
        span = Span (Range 0 3) text
        span.start . should_equal 0
        span.end . should_equal 3
        span.text . should_equal text

    Test.specify "should be able to be converted to code units" <|
        text = 'ae\u{301}fz'
        (Span (Range 1 3) text).to_utf_16_span . should_equal (Utf_16_Span (Range 1 4) text)

    Test.specify "should expand to the associated grapheme clusters" <|
        text = 'a\u{301}e\u{302}o\u{303}'
        span = Utf_16_Span (Range 1 5) text
        extended = span.to_grapheme_span
        extended . should_equal (Span (Range 0 3) text)
        extended.to_utf_16_span . should_equal (Utf_16_Span (Range 0 6) text)

        Utf_16_Span (Range 0 2) text . to_grapheme_span . should_equal (Span (Range 0 1) text)
        Utf_16_Span (Range 0 1) text . to_grapheme_span . should_equal (Span (Range 0 1) text)
        Utf_16_Span (Range 0 0) text . to_grapheme_span . should_equal (Span (Range 0 0) text)
        Utf_16_Span (Range 1 1) text . to_grapheme_span . should_equal (Span (Range 0 0) text)
        Utf_16_Span (Range 2 2) text . to_grapheme_span . should_equal (Span (Range 1 1) text)

        Utf_16_Span (Range 0 4) text . to_grapheme_span . should_equal (Span (Range 0 2) text)
        Utf_16_Span (Range 0 3) text . to_grapheme_span . should_equal (Span (Range 0 2) text)
        Utf_16_Span (Range 0 2) text . to_grapheme_span . should_equal (Span (Range 0 1) text)

    Test.specify "should be able to use the conversions" <|
        text = 'ae\u{301}fz'
        Utf_16_Span.from (Span (Range 1 3) text) . should_equal (Utf_16_Span (Range 1 4) text)
        Span.from (Utf_16_Span (Range 2 4) text) . should_equal (Span (Range 1 3) text)

main = Test.Suite.run_main spec

from Standard.Base import all
import Standard.Base.Error.Encoding_Error.Encoding_Error
import Standard.Base.Error.File_Error.File_Error

from Standard.Test import Test, Test_Suite, Problems
import Standard.Test.Extensions

spec =
    sample_xxx = enso_project.data / "sample.xxx"
    sample_txt = enso_project.data / "helloworld.txt"
    windows_log = enso_project.data / "windows.log"

    Test.group "Auto_Detect" <|
        Test.specify "should raise an error when reading an unknown file" <|
            bytes = sample_xxx.read
            bytes.should_fail_with File_Error.Unsupported_Type

        Test.specify "should be able to read a text file" <|
            content = sample_txt.read
            content.should_equal "Hello World!"

    Test.group "Bytes" <|
        Test.specify "should be able to read a file as Bytes" <|
            bytes = sample_xxx.read Bytes
            bytes.should_equal [72, 101, 108, 108, 111, 32, 87, 111, 114, 108, 100, 33]

        Test.specify "should be able to read a file as Bytes by method" <|
            bytes = Data.read (sample_xxx) Bytes
            bytes.should_equal [72, 101, 108, 108, 111, 32, 87, 111, 114, 108, 100, 33]

        Test.specify "should be able to read a file as Bytes by path" <|
            path = sample_xxx.path
            bytes = Data.read path Bytes
            bytes.should_equal [72, 101, 108, 108, 111, 32, 87, 111, 114, 108, 100, 33]

    Test.group "Plain_Text" <|
        Test.specify "should be able to read a file as Text" <|
            text = sample_xxx.read Plain_Text
            text.should_equal "Hello World!"

        Test.specify "should be able to read a file as Text with Encoding" <|
            text = windows_log.read (Plain_Text Encoding.windows_1252)
            text.should_equal "Hello World! $¢¤¥"

        Test.specify "should raise a warning when invalid encoding in a Text file" <|
            action = windows_log.read (Plain_Text Encoding.ascii) on_problems=_
            tester result = result . should_equal 'Hello World! $\uFFFD\uFFFD\uFFFD'
            problems = [Encoding_Error.Error "Encoding issues at 14, 15, 16."]
            Problems.test_problem_handling action problems tester

            # Check that it defaults to warning.
            Problems.expect_only_warning Encoding_Error <|
                windows_log.read (Plain_Text Encoding.ascii)

    Test.group "JSON_File" <|
        Test.specify "should be able to read a file as Json" <|
            f1 = enso_project.data / "sample.json"
            j1 = f1.read
            j1.at "arr" . should_equal [1, 2, 3]
            j1.at "num" . should_equal 42.5
            j1.at "not" . should_equal Nothing

            f2 = enso_project.data / "sample-json.weird-extension"
            j2 = f2.read JSON_File
            j2.at 0 . at "foo" . should_equal "bar"
            j2.at 1 . should_equal 42
            j2.at 2 . should_equal Nothing
            j2.at 3 . should_equal "baz"

        Test.specify "should gracefully handle malformed files" <|
            f1 = enso_project.data / "sample-malformed.json"
            r1 = f1.read
            r1.should_fail_with File_Error.Corrupted_Format

main = Test_Suite.run_main spec

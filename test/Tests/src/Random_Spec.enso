from Standard.Base import all
import Standard.Base.Errors.Illegal_Argument.Illegal_Argument

from Standard.Test import Test, Test_Suite
import Standard.Test.Extensions

spec = Test.group "Random" <|
    ## Random number generator seeded to make the test deterministic

    Test.specify "should allow generating random booleans" <|
        rng = Random.new 0
        0.up_to 3 . map _->rng.boolean . should_equal [True, True, False]

    Test.specify "should allow generating random integers" <|
        rng = Random.new 12345
        rng.integer . should_equal 51
        rng.integer 0 10000 . should_equal 9080
        rng.integer 0 100000000000000 . should_fail_with Illegal_Argument

        random_range = 0.up_to 1000 . map _->rng.integer . compute_bulk [Statistic.Minimum, Statistic.Maximum]
        (random_range.at 0 >= 0) . should_equal True
        (random_range.at 1 <= 100) . should_equal True

    Test.specify "should allow generating random decimals" <|
        rng = Random.new 12345
        rng.decimal . should_equal 0.3618031071604718 epsilon=0.00000001
        rng.decimal . should_equal 0.932993485288541 epsilon=0.00000001

        random_range = 0.up_to 1000 . map _->rng.decimal . compute_bulk [Statistic.Minimum, Statistic.Maximum]
        (random_range.at 0 >= 0) . should_equal True
        (random_range.at 1 <= 1) . should_equal True

    Test.specify "should allow generating random gaussian decimals" <|
        rng = Random.new 12345
        rng.gaussian . should_equal -0.187808989658912 epsilon=0.00000001
        rng.gaussian . should_equal 0.5884363051154796 epsilon=0.00000001

    Test.specify "should allow to generate random indices" <|
        rng = Random.new 0
        two_out_of_three = 0.up_to 100 . map _->
            Random.random_indices 3 2 rng
        permutations = 0.up_to 100 . map _->
            Random.random_indices 3 3 rng
        permutations_2 = 0.up_to 100 . map _->
            Random.random_indices 3 100 rng
        two_out_of_three . should_contain_the_same_elements_as [[0, 1], [0, 2], [1, 2], [1, 0], [2, 0], [2, 1]]

        all_permutations = [[0, 1, 2], [0, 2, 1], [1, 0, 2], [1, 2, 0], [2, 0, 1], [2, 1, 0]]
        permutations . should_contain_the_same_elements_as all_permutations
        permutations_2 . should_contain_the_same_elements_as all_permutations

        Random.random_indices 0 0 rng . should_equal []
        Random.random_indices 0 100 rng . should_equal []
        Random.random_indices 1 1 rng . should_equal [0]
        Random.random_indices 1 100 rng . should_equal [0]
        Random.random_indices 100 0 rng . should_equal []

        two_out_of_four = 0.up_to 10000 . map seed->
            Random.random_indices 4 2 (Random.new seed)
        two_out_of_four . should_contain_the_same_elements_as <|
            [[0, 1], [0, 2], [1, 2], [1, 0], [2, 0], [2, 1], [0, 3], [1, 3], [2, 3], [3, 0], [3, 1], [3, 2]]

    Test.specify "should allow to select a random sample from a vector" <|
        rng = Random.new 0
        vector = ["A", "B", "C"]
        shuffles = 0.up_to 100 . map _->
            Random.sample vector 2 rng
        shuffles . should_contain_the_same_elements_as [["A", "B"], ["A", "C"], ["B", "A"], ["B", "C"], ["C", "A"], ["C", "B"]]

        overflow = Random.sample vector 100 rng
        overflow.length . should_equal 3
        overflow.should_contain_the_same_elements_as vector

        Random.sample ["A", "A", "A"] 2 rng . should_equal ["A", "A"]
        Random.sample ["A", "A", "A"] 0 rng . should_equal []
        Random.sample ["A", "A", "A"] 3 rng . should_equal ["A", "A", "A"]
        Random.sample ["A", "A", "A"] 100 rng . should_equal ["A", "A", "A"]

main = Test_Suite.run_main spec

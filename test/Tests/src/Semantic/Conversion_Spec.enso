from Standard.Base import all
import Standard.Base.Errors.Common.No_Such_Conversion

import project.Semantic.Conversion.Methods
import project.Semantic.Conversion.Types
import project.Semantic.Conversion_Use.Hello

from Standard.Test import Test, Test_Suite
import Standard.Test.Extensions

polyglot java import java.lang.Object

type Foo
    Value foo
type Bar
    Value bar
type Baz
    Value baz
type Quux
    Value quux
type Quaffle
type My_Error
    Value err

type Not_Foo
    Value notfoo

Foo.from (that:Bar) = Foo.Value that.bar
Foo.from (that:Baz) = Foo.Value that.baz
Foo.from (that:Text) = Foo.Value that.length
Foo.from (that:Number) first_param=0 second_param=0 third_param=0 = Foo.Value [that, first_param, second_param, third_param]
Foo.from (that:Function) = Foo.Value (that 5)
Foo.from (that:Boolean) = Foo.Value that
Foo.from (that:Array) = Foo.Value that.length

Not_Foo.from (_:Boolean) = Not_Foo.Value True
Not_Foo.from (_:Any) = Not_Foo.Value "ANY!!!"

Foo.from (_:Quaffle) = Foo.Value "quaffle"
Foo.from (_:Error) = Foo.Value "oops"

foreign js make_str x = """
   return "js string"

foreign js call_function fn arg_1 = """
    return fn(arg_1, "a string");

Number.foo self = "foo called"

spec =
    Test.group "Conversion" <|
        Test.specify "should be able to convert atoms" <|
            ((Foo.from (Baz.Value 10)).foo + (Foo.from (Bar.Value 20)).foo) . should_equal 30
            Foo.from Quaffle . foo . should_equal "quaffle"
        Test.specify "should be able to convert text" <|
            Foo.from "123" . foo . should_equal 3
        Test.specify "should be able to convert foreign text" <|
            Foo.from (make_str 4) . foo . should_equal 9
        Test.specify "should be able to convert numbers" <|
            Foo.from 4 . should_equal (Foo.Value [4, 0, 0, 0])
            Foo.from (10^100) . should_equal (Foo.Value [10^100, 0, 0, 0])
            Foo.from 4.5 . should_equal (Foo.Value [4.5, 0, 0, 0])
        Test.specify "should be able to convert dataflow errors" <|
            Foo.from (Error.throw <| My_Error.Value "i was bad") . should_equal (Foo.Value "oops")
        Test.specify "should be able to convert functions" <|
            Foo.from (e -> e) . foo . should_equal 5
        Test.specify "should be able to convert booleans" <|
            Foo.from True . foo . should_be_true
            Foo.from False . foo . should_be_false
        Test.specify "should be able to convert arrays" <|
            Foo.from [1,2,3].to_array . foo . should_equal 3
        Test.specify "should be able to convert Any" <|
            Not_Foo.from that=Quaffle . notfoo . should_equal "ANY!!!"
            Not_Foo.from 4 . notfoo . should_equal "ANY!!!"
            Not_Foo.from (e -> e) . notfoo . should_equal "ANY!!!"
            Not_Foo.from [1,2,3].to_array . notfoo . should_equal "ANY!!!"
            Not_Foo.from [1,2,3] . notfoo . should_equal "ANY!!!"
        Test.specify "apply Any conversion to foreign object" <|
            Not_Foo.from Object.new . notfoo . should_equal "ANY!!!"
        Test.specify "apply Any conversion to type" <|
            Not_Foo.from Boolean . notfoo . should_equal "ANY!!!"
        Test.specify "should call intrinsic object conversions for unimported constructors" <|
            Vector.from Methods.get_foo . should_equal ["foo"]
        Test.specify "should call extension conversions" <|
            Text.from Methods.get_bar . should_equal "bar"

        Test.specify "should fail graciously when there is no conversion" <|
            Panic.recover Any (Foo.from (Quux.Value 10)) . catch Any .to_display_text . should_equal "Could not find a conversion from `Quux.Value` to `Foo`."
        Test.specify "should fail graciously when the conversion target is invalid" <|
            Panic.recover Any (123.from (Quux.Value 10)) . catch Any .to_display_text . should_equal "123 is not a valid conversion target. Expected a type."

        Test.specify "should be callable with by-name arguments" <|
            .from self=Foo that=4 first_param=2 . should_equal (Foo.Value [4, 2, 0, 0])
        Test.specify "should support the use of multiple arguments" <|
            Foo.from that=4 second_param=1 2 . should_equal (Foo.Value [4, 2, 1, 0])

        Test.specify "should play nicely with polyglot" <|
            call_function .from Foo . should_equal (Foo.Value 8)

        Test.specify "should support the meta functions" <|
            meta_from = Meta.meta .from
            is_symbol = case meta_from of
                _ : Meta.Unresolved_Symbol -> True
                _ -> False
            is_symbol.should_be_true

            .from . to_text . should_equal "UnresolvedConversion"

            meta_from.name.should_equal "from"

            Meta.meta .foo . rename "from" . should_equal .from
            Meta.meta .foo . rename "from" Foo "hello" . should_equal (Foo.Value 5)

            meta_from.rename "foo" 123 . should_equal "foo called"
            meta_from.rename "foo" . should_equal .foo

        Test.specify "should not allow currying" <|
            Panic.recover Any (Foo.from) . catch Any .to_display_text . should_equal "Conversion currying without `that` argument is not supported."

        Test.specify "Use conversions to in Conversion_Use module" <|
            Hello.formulate [ Hello.Say "Proper", Hello.Say "Type" ] . should_equal "ProperType"
            Hello.formulate [ Foo.Value "Perform", Bar.Value "Conversion" ] . should_equal "PERFORM conversion!"

        Test.specify "Convert Foo.to Hello" <|
            hello = Foo.Value "Perform" . to Hello
            hello . msg . should_equal "PERFORM "

        Test.specify "Convert Bar.to Hello" <|
            hello = Bar.Value "Conversion" . to Hello
            hello . msg . should_equal "conversion!"

        Test.specify "Convert Bar.to Hello with other suffix" <|
            hello = Bar.Value "Conversion" . to Hello suffix="?"
            hello . msg . should_equal "conversion?"

        Test.specify "Idempotent convert Hello.to Hello" <|
            Hello.Say "Hi there!" . to Hello . msg . should_equal "Hi there!"

        Test.specify "Unknown convertion Text.to Hello" <|
            h = Panic.recover No_Such_Conversion <| "Hi there!" . to Hello
            h . should_fail_with No_Such_Conversion

        Test.specify "Use Any.to in Conversion_Use module" <|
            Hello.formulate_with_to [ Hello.Say "Proper", Hello.Say "Type" ] . should_equal "ProperType"
            Hello.formulate_with_to [ Foo.Value "Perform", Bar.Value "Conversion" ] . should_equal "PERFORM conversion!"

Hello.from (that:Foo) suffix=" " = Hello.Say <| (that.foo.to_case Case.Upper) + suffix
Hello.from (that:Bar) suffix="!" = Hello.Say <| (that.bar.to_case Case.Lower) + suffix

main = Test_Suite.run_main spec

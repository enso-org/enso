from Standard.Base import all
from Standard.Test import Test, Test_Suite
import Standard.Test.Extensions

polyglot java import java.lang.Float
polyglot java import java.lang.Integer
polyglot java import java.lang.Long
polyglot java import java.lang.String
polyglot java import java.lang.StringBuilder as Java_String_Builder
polyglot java import java.util.ArrayList
polyglot java import java.time.LocalDate
polyglot java import java.time.LocalTime

Any.test_me x = x.is_nothing

spec =
    Test.group "Java FFI" <|
        Test.specify "should call methods imported from Java" <|
            Long.sum 1 2 . should_equal 3

        Test.specify "should call constructors imported from Java" <|
            list = ArrayList.new
            list.add 432
            list.get 0 . should_equal 432
        Test.specify "should report missing method error on Java Arrays" <|
            list = ArrayList.new
            list.add 432
            Test.expect_panic_with (list.asList) No_Such_Method_Error_Data
        Test.specify "should auto-convert numeric types across the polyglot boundary" <|
            (Float.valueOf "123.3" + 5).should_equal 128.3 epsilon=0.0001
            (Integer.sum 1 2 + 3) . should_equal 6
        Test.specify "should auto-convert strings across the polyglot boundary" <|
            (String.format "%s bar %s" "baz" "quux" + " foo").should_equal "baz bar quux foo"
        Test.specify "should support Java import renaming" <|
            builder = Java_String_Builder.new
            builder.append "foo"
            builder.append "bar"
            str = builder.toString
            str.should_equal "foobar"
        Test.specify "should invoke static methods" <|
            x = Integer.valueOf 1
            x.test_me x . should_equal False

    Test.group "Java/Enso Date" <|
        Test.specify "Java date has Enso properties" <|
            april1st = LocalDate.of 2022 04 01
            april1st.year.should_equal 2022
            april1st.month.should_equal 4
            april1st.day.should_equal 1

        Test.specify "send Enso date into Java" <|
            ensodate = Date.new 2022 04 01
            javatime = LocalTime.of 10 26
            javatimedate = javatime . to_date_time ensodate
            april1st = javatimedate . date
            april1st.year.should_equal 2022
            april1st.month.should_equal 4
            april1st.day.should_equal 1

main = Test_Suite.run_main spec

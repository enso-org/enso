from Standard.Base import all

polyglot java import java.util.Random

import Standard.Test

spec = Test.group "Pattern Matches" <|
    Test.specify "should be able to match on the Boolean type" <|
        case Boolean of
            Boolean -> Nothing
            _ -> Test.fail "Expected the Boolean constructor to match."
        case True of
            Boolean -> Nothing
            _ -> Test.fail "Expected the True constructor to match."
        case False of
            Boolean -> Nothing
            _ -> Test.fail "Expected the False constructor to match."
    Test.specify "should be able to match on the Integer type" <|
        case 1 of
            Integer -> Nothing
            _ -> Test.fail "Expected an integer to match."
        case 100000000000000000000000000000000000 of
            Integer -> Nothing
            _ -> Test.fail "Expected an integer to match."
        case Integer of
            Integer -> Nothing
            _ -> Test.fail "Expected the Integer constructor to match."
    Test.specify "should be able to match on the Decimal type" <|
        case 1.7 of
            Decimal -> Nothing
            _ -> Test.fail "Expected a decimal to match."
        case Decimal of
            Decimal -> Nothing
            _ -> Test.fail "Expected the Decimal constructor to match."
    Test.specify "should be able to match on the Number type" <|
        case 1 of
            Number -> Nothing
            _ -> Test.fail "Expected a number to match."
        case 100000000000000000000000000000000000 of
            Number -> Nothing
            _ -> Test.fail "Expected a number to match."
        case 1.7 of
            Number -> Nothing
            _ -> Test.fail "Expected a number to match."
        case Number of
            Number -> Nothing
            _ -> Test.fail "Expected the Number constructor to match."
        case Integer of
            Number -> Nothing
            _ -> Test.fail "Expected the Integer constructor to match."
        case Decimal of
            Number -> Nothing
            _ -> Test.fail "Expected the Decimal constructor to match."
    Test.specify "should be able to match on the Text type" <|
        case "foo" of
            Text -> Nothing
            _ -> Test.fail "Expected a text to match."
        case Text of
            Text -> Nothing
            _ -> Test.fail "Expected the Text constructor to match."
    Test.specify "should be able to match on the Array type" <|
        case [].to_array of
            Array -> Nothing
            _ -> Test.fail "Expected an array to match."
        case Array of
            Array -> Nothing
            _ -> Test.fail "Expected the Array constructor to match."
    Test.specify "should be able to match on the Polyglot type" <|
        random_gen = Random.new
        case random_gen of
            Polyglot -> Nothing
            _ -> Test.fail "Expected a polyglot object to match."
        case Polyglot of
            Polyglot -> Nothing
            _ -> Test.fail "Expected the Polyglot constructor to match."
    Test.specify "should be able to match on the Any type" <|
        value_1 = 1.23143
        value_2 = "foo bar"
        case value_1 of
            Any -> Nothing
            _ -> Test.fail "Expected any value to match Any."
        case value_2 of
            Any -> Nothing
            _ -> Test.fail "Expected any value to match Any."
        case Polyglot of
            Any -> Nothing
            _ -> Test.fail "Expect any constructor to match Any."
        case Any of
            Any -> Nothing
            _ -> Test.fail "Expected the Any constructor to match."
    Test.specify "should be able to match on date/time values" <|
        new_date = Date.new 2020 6 1
        new_date_time = Date_Time.new 2020 6 1
        new_time = Time_Of_Day.new 11 11
        new_zone = Time_Zone.system

        case new_date of
            Date_Time.Date_Time -> Test.fail "Expected date value to match Date."
            Time_Of_Day.Time_Of_Day -> Test.fail "Expected date value to match Date."
            Time_Zone.Time_Zone -> Test.fail "Expected date value to match Date."
            Date.Date -> Nothing
            _ -> Test.fail "Expected date value to match Date."

        case new_date_time of
            Date.Date -> Test.fail "Expected datetime value to match Date_Time."
            Time_Of_Day.Time_Of_Day -> Test.fail "Expected datetime value to match Date_Time."
            Time_Zone.Time_Zone -> Test.fail "Expected datetime value to match Date_Time."
            Date_Time.Date_Time -> Nothing
            _ -> Test.fail "Expected datetime value to match Date_Time."

        case new_time of
            Date.Date -> Test.fail "Expected time value to match Time_Of_Day."
            Date_Time.Date_Time -> Test.fail "Expected time value to match Time_Of_Day."
            Time_Zone.Time_Zone -> Test.fail "Expected time value to match Time_Of_Day."
            Time_Of_Day.Time_Of_Day -> Nothing
            _ -> Test.fail "Expected time value to match Time_Of_Day."

        case new_zone of
            Date.Date -> Test.fail "Expected timezone value to match Time_Zone."
            Date_Time.Date_Time -> Test.fail "Expected timezone to match Time_Zone."
            Time_Of_Day.Time_Of_Day -> Test.fail "Expected timezone to match Time_Zone."
            Time_Zone.Time_Zone -> Nothing
            _ -> Test.fail "Expected timezone value to match Time_Zone."

    Test.specify "should be able to match on literal values" <|
        value_1 = 42
        value_2 = "foo"
        value_3 = 's\u0301'
        value_4 = '\u00E9'
        case value_1 of
            0     -> Test.fail "Expected value to match constant."
            "foo" -> Test.fail "Expected value to match constant."
            42    -> Nothing
            _     -> Test.fail "Expected value to match constant."
        case value_2 of
            "meh" -> Test.fail "Expected value to match constant."
            0     -> Test.fail "Expected value to match constant."
            "foo" -> Nothing
            _     -> Test.fail "Expected value to match constant."
        case value_3 of
            "s"   -> Test.fail "Expected value to match constant."
            "ś"   -> Nothing
            _     -> Test.fail "Expected value to match constant."
        case value_4 of
            "e"   -> Test.fail "Expected value to match constant."
            "ę"   -> Test.fail "Expected value to match constant."
            '\u0065\u{301}' -> Nothing
            _     -> Test.fail "Expected value to match constant."
    Test.specify "should be able to match on literal values nested in constructors" <|
        value_1 = Cons 42 Nil
        value_2 = Cons (Cons 42 Nil) Nil
        case value_1 of
            42                    -> Test.fail "Expected value to match constant."
            Cons (Cons 1 Nil) Nil -> Test.fail "Expected value to match constant."
            Cons 1 Nil            -> Test.fail "Expected value to match constant."
            Cons 42 Nil           -> Nothing
            _                     -> Test.fail "Expected value to match constant."
        case value_2 of
            42                    -> Test.fail "Expected value to match constant."
            Cons (Cons 1 Nil) Nil -> Test.fail "Expected value to match constant."
            Cons (Cons a Nil) Nil -> if a == 42 then Nothing else Test.fail "Expected variable to bind to 42"
            Cons 1 Nil            -> Test.fail "Expected value to match constant."
            Cons _ Nil            -> Test.fail "Expected value to match constant."
            _                     -> Test.fail "Expected value to match constant."

main = Test.Suite.run_main spec

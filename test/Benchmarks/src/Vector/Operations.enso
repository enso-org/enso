from Standard.Base import all
import Standard.Base.Runtime.State
import Standard.Base

from Standard.Test import Bench

import project.Vector.Utils

polyglot java import java.util.Random as Java_Random

## Bench Utilities ============================================================

vector_size = 1000000
iter_size = 100
num_iterations = 10

# The Benchmarks ==============================================================

collect_benches group_builder =
    random_vec = Utils.make_random_vec vector_size
    random_vec_2 = Utils.make_random_vec 100000
    random_gen = Java_Random.new 123456

    bench_measure ~act name = group_builder.specify name act

    bench_measure (Base.Vector.new vector_size i->i) "New Vector"
    bench_measure (Base.Vector.new vector_size _->42) "New Constant"
    bench_measure (Base.Vector.new vector_size _->random_gen.nextLong) "New Random"
    bench_measure (Base.Vector.fill vector_size 42) "Fill Constant"
    bench_measure (Base.Vector.fill vector_size random_gen.nextLong) "Fill Random (constant)"
    bench_measure (random_vec + [1]) "Append Single"
    bench_measure (random_vec + random_vec_2) "Append Large"
    bench_measure (random_vec.reduce (Math.max)) "Max"
    bench_measure (random_vec.reduce (+)) "Sum"
    bench_measure ((random_vec.drop (First 20)).reduce (+)) "Drop First 20 and Sum"
    bench_measure ((random_vec.drop (Last 20)).reduce (+)) "Drop Last 20 and Sum"
    bench_measure (random_vec.filter (x -> x % 3 == 1)) "Filter"
    bench_measure (random_vec.filter_with_index (i-> x-> (i+x) % 3 == 1)) "Filter With Index"
    bench_measure (random_vec.compute Statistic.Maximum) "Max (Stats)"
    bench_measure (random_vec.compute Statistic.Sum) "Sum (Stats)"
    bench_measure (random_vec.compute Statistic.Variance) "Variance (Stats)"

    bench_measure (random_vec . map (x -> x +  random_gen.nextLong) . filter (x -> x % 3 == 1)) "Map & Filter"
    bench_measure (random_vec.partition (x -> x % 3 == 1)) "Partition"
    bench_measure (random_vec.partition_with_index (i-> x-> (i+x) % 3 == 1)) "Partition With Index"

    stateful_fun x =
       s = State.get Number
       State.put s+x
    bench_measure (State.run Number 0 <| random_vec.each stateful_fun) "Each"

options = Bench.options . size iter_size . iter num_iterations

all = Bench.build builder->
    builder.group "Vector Operations" options group_builder->
        collect_benches group_builder

main = all . run_main

from Standard.Base import all
import Standard.Base

from Standard.Table import Table

import Standard.Visualization

from Standard.Test import Test
import Standard.Test.Extensions

type My_Type
    Value my_field

spec = Test.group "Serializable Visualization Identifiers" <|

    Test.specify "serializes builtin visualizations in the correct format" <|
        json_for_name n = JS_Object.from_pairs [["library", Nothing], ["name", n]] . to_text
        Visualization.Id.json.to_json . should_equal (json_for_name "JSON")
        Visualization.Id.scatter_plot.to_json . should_equal (json_for_name "Scatter Plot")
        Visualization.Id.histogram.to_json . should_equal (json_for_name "Histogram")
        Visualization.Id.heatmap.to_json . should_equal (json_for_name "Heatmap")
        Visualization.Id.table.to_json . should_equal (json_for_name "Table")
        Visualization.Id.sql_query.to_json . should_equal (json_for_name "SQL Query")
        Visualization.Id.geo_map.to_json . should_equal (json_for_name "Geo Map")
        Visualization.Id.image.to_json . should_equal (json_for_name "Image")

    Test.specify "serializes library visualizations in the correct format" <|
        expected p_name v_name =
            lib = JS_Object.from_pairs [["name", p_name]]
            JS_Object.from_pairs [["library", lib], ["name", v_name]] . to_text
        v_1 = Visualization.Id.from_module Id_Spec "My Vis"
        v_2 = Visualization.Id.from_module Base "Other Vis"
        v_1.to_json.should_equal (expected "enso_dev.Visualization_Tests" "My Vis")
        v_2.to_json.should_equal (expected "Standard.Base" "Other Vis")

    Test.specify "specifies default JSON visualization for any type" <|
        My_Type.Value 30 . default_visualization . should_equal Visualization.Id.json
        "foobar".default_visualization.should_equal Visualization.Id.json
        True.default_visualization.should_equal Visualization.Id.json

    Test.specify "specifies default Table visualization for Vector and Array type" <|
        [1,2,3].default_visualization.should_equal Visualization.Id.table
        [1,2,3].to_array.default_visualization.should_equal Visualization.Id.table


    Test.specify "should specify Table's default visualizations correctly" <|
        c_1_1 = ['x', [1, 2, 3]]
        c_1_2 = ['Y', [5.3, 56.2, 6.3]]
        t_1 = Table.new [c_1_1, c_1_2]
        t_1.default_visualization.should_equal Visualization.Id.scatter_plot

        c_2_1 = ['LaTituDe', [5,3,2]]
        c_2_2 = ['longitude', [6,7,8]]
        t_2 = Table.new [c_2_1, c_2_2]
        t_2.default_visualization.should_equal Visualization.Id.geo_map

        c_3_1 = ['latitude', [5,2,5]]
        c_3_2 = ['Y', [2,4,2]]
        c_3_3 = ['name', ["foo", "bar", "baz"]]
        t_3 = Table.new [c_3_1, c_3_2, c_3_3]
        t_3.default_visualization.should_equal Visualization.Id.table

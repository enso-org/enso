from Standard.Base import all

from Standard.Table import Table, Column

import Standard.Visualization.Histogram

from Standard.Test import Test
import Standard.Test.Extensions

import project

spec =
    expect value expected_label expected_values =
        text = Histogram.process_to_json_text value
        json = Json.parse text
        expected_data = JS_Object.from_pairs [['values', expected_values]]
        expected_json = case expected_label of
            Nothing -> JS_Object.from_pairs [['data', expected_data]]
            _       ->
                expected_x = JS_Object.from_pairs [['label', expected_label]]
                expected_axis = ['axis', JS_Object.from_pairs [['x', expected_x]]]
                JS_Object.from_pairs [['data', expected_data], expected_axis]
        json.should_equal expected_json

    Test.group "Histogram Visualization" <|
        Test.specify "plots first column if none recognized" <|
            header = ['α', 'ω']
            row_1  = [11 , 10 ]
            row_2  = [21 , 20 ]
            table  = Table.from_rows header [row_1, row_2]
            expect table 'α' [11,21]

        Test.specify "plots 'value' numeric column if present" <|
            header = ['α', 'value']
            row_1  = [11 , 10 ]
            row_2  = [21 , 20 ]
            table  = Table.from_rows header [row_1, row_2]
            expect table 'value' [10,20]

        Test.specify "is case-insensitive" <|
            header = ['α', 'Value']
            row_1  = [11 , 10 ]
            row_2  = [21 , 20 ]
            table  = Table.from_rows header [row_1, row_2]
            expect table 'Value' [10,20]

        Test.specify "plots column" <|
            column = Column.from_vector 'my_name' [1,4,6]
            expect column 'my_name' [1,4,6]

        Test.specify "plots vector" <|
            vector = [1,2,3]
            expect vector Nothing vector

        Test.specify "plots range" <|
            vector = 2.up_to 5
            expect vector Nothing [2,3,4]

from Standard.Base import all
from Standard.Table.Data.Column_Selector as Column_Selector_Module import By_Name
import Standard.Table.Data.Sort_Column_Selector
import Standard.Table.Data.Sort_Column

import project.Database.Helpers.Fake_Test_Connection
import Standard.Database.Data.Dialect
import Standard.Database.Data.Table as Table_Module
import Standard.Test
import Standard.Test.Problems

from Standard.Table.Data.Aggregate_Column import all
from Standard.Database import all
from Standard.Database.Data.Sql import Sql_Type
from Standard.Table import No_Such_Column_Error
from Standard.Table.Error as Table_Errors import No_Input_Columns_Selected, Missing_Input_Columns
from Standard.Database.Error as Database_Errors import Unsupported_Database_Operation_Error

spec =
    int = Sql_Type.integer
    bool = Sql_Type.boolean
    str = Sql_Type.varchar
    test_connection =
        table1 = ["T1", [["A", int], ["B", str], ["C", bool]]]
        table2 = ["T2", [["D", int], ["E", int], ["F", bool]]]
        table3 = ["T3", [["A", int], ["E", bool], ["F", int]]]
        tables = Map.from_vector [table1, table2, table3]
        Fake_Test_Connection.make Dialect.sqlite tables
    t1 = test_connection.access_table "T1"
    Test.group "[Codegen] JSON serialization" <|
        Test.specify "should serialize Tables and Columns to their SQL representation" <|
            q1 = t1.where (t1.at "A" == 42) . to_json
            part1 = Json.from_pairs [["sql_code", 'SELECT "T1"."A" AS "A", "T1"."B" AS "B", "T1"."C" AS "C" FROM "T1" AS "T1" WHERE ("T1"."A" = ']]
            interp = Json.from_pairs [["value", 42], ["expected_sql_type", "INTEGER"]]
            part2 = Json.from_pairs [["sql_interpolation", interp]]
            part3 = Json.from_pairs [["sql_code", ")"]]
            expected = Json.from_pairs [["query", Json.Array [part1, part2, part3]]]
            q1.should_equal expected

            q2 = t1.at "A" . to_json
            q2.should_equal (Json.from_pairs [["query", Json.Array [Json.from_pairs [["sql_code", 'SELECT "T1"."A" AS "A" FROM "T1" AS "T1"']]]]])

    Test.group "[Codegen] Basic Select" <|
        Test.specify "should select columns from a table" <|
            t1.to_sql.prepare . should_equal ['SELECT "T1"."A" AS "A", "T1"."B" AS "B", "T1"."C" AS "C" FROM "T1" AS "T1"', []]
            t2 = t1.select_columns (By_Name ["C", "B", "undefined"]) reorder=True
            t2.to_sql.prepare . should_equal ['SELECT "T1"."C" AS "C", "T1"."B" AS "B" FROM "T1" AS "T1"', []]

            foo = t1.at "A" . rename "FOO"
            foo.to_sql.prepare . should_equal ['SELECT "T1"."A" AS "FOO" FROM "T1" AS "T1"', []]

            t3 = t2.set "bar" foo
            t3.to_sql.prepare . should_equal ['SELECT "T1"."C" AS "C", "T1"."B" AS "B", "T1"."A" AS "bar" FROM "T1" AS "T1"', []]

        Test.specify "should fail if at is called for a non-existent column" <|
            t1.at "undefined" . should_fail_with No_Such_Column_Error

        Test.specify "should allow to limit the amount of returned results" <|
            t2 = t1.limit 5
            t2.to_sql.prepare . should_equal ['SELECT "T1"."A" AS "A", "T1"."B" AS "B", "T1"."C" AS "C" FROM "T1" AS "T1" LIMIT 5', []]

        Test.specify "should work correctly when there are no columns" <|
            empty = t1.select_columns (By_Name [])
            json = Json.from_pairs [["query", Nothing], ["message", "The table has no columns so a query cannot be generated."]]
            empty.to_json . should_equal json
            empty.columns.length . should_equal 0
            empty.to_sql . should_fail_with Unsupported_Database_Operation_Error

    Test.group "[Codegen] Building Expressions" <|
        Test.specify "should allow building expressions from columns and constants" <|
            a = t1.at "A"
            b = t1.at "B"
            c = t1.at "C"
            arith = (a * 2) + 1
            logic = (c || c.not) && True
            cmp = (a / a >= b) && (a - b < a)
            arith.to_sql.prepare . should_equal ['SELECT (("T1"."A" * ?) + ?) AS "A" FROM "T1" AS "T1"', [[2, int], [1, int]]]
            logic.to_sql.prepare . should_equal ['SELECT (("T1"."C" OR (NOT "T1"."C")) AND ?) AS "C" FROM "T1" AS "T1"', [[True, bool]]]
            cmp.to_sql.prepare . should_equal ['SELECT ((("T1"."A" / "T1"."A") >= "T1"."B") AND (("T1"."A" - "T1"."B") < "T1"."A")) AS "A" FROM "T1" AS "T1"', []]

        Test.specify "should support simple text operations" <|
            b = t1.at "B"
            add = b + "SUFFIX"
            add.to_sql.prepare . should_equal ['SELECT ("T1"."B" + ?) AS "B" FROM "T1" AS "T1"', [["SUFFIX", str]]]

            ends = b.ends_with "suf"
            starts = b.starts_with "pref"
            contains = b.contains "inf"
            ends.to_sql.prepare . should_equal ['SELECT ("T1"."B" LIKE (\'%\' || ?)) AS "B" FROM "T1" AS "T1"', [["suf", str]]]
            starts.to_sql.prepare . should_equal ['SELECT ("T1"."B" LIKE (? || \'%\')) AS "B" FROM "T1" AS "T1"', [["pref", str]]]
            contains.to_sql.prepare . should_equal ['SELECT instr("T1"."B", ?) > 0 AS "B" FROM "T1" AS "T1"', [["inf", str]]]

    Test.group "[Codegen] Masking Tables and Columns" <|
        Test.specify "should allow filtering table rows based on a boolean expression" <|
            t2 = t1.where (t1.at "A" == 42)
            t2.to_sql.prepare . should_equal ['SELECT "T1"."A" AS "A", "T1"."B" AS "B", "T1"."C" AS "C" FROM "T1" AS "T1" WHERE ("T1"."A" = ?)', [[42, int]]]

        Test.specify "should allow selecting column rows based on a boolean expression" <|
            c2 = (t1.at "B").where (t1.at "A" == t1.at "C")
            c2.to_sql.prepare . should_equal ['SELECT "T1"."B" AS "B" FROM "T1" AS "T1" WHERE ("T1"."A" = "T1"."C")', []]

    Test.group "[Codegen] Joining Tables" <|
        t2 = test_connection.access_table "T2"
        t3 = test_connection.access_table "T3"
        Test.specify "should allow joining tables index-on-index" <|
            r1 = t1.set_index 'A' . join (t2.set_index 'D')
            r1.to_sql.prepare . should_equal ['SELECT "T1"."B" AS "B", "T1"."C" AS "C", "T2"."E" AS "E", "T2"."F" AS "F" FROM (SELECT "T1"."B" AS "B", "T1"."C" AS "C", "T1"."A" AS "A", "T1"."A" AS "A_1" FROM "T1" AS "T1") AS "T1" LEFT JOIN (SELECT "T2"."E" AS "E", "T2"."F" AS "F", "T2"."D" AS "D" FROM "T2" AS "T2") AS "T2" ON ("T1"."A_1" = "T2"."D")', []]

        Test.specify "should allow joining tables column-on-index" <|
            r1 = t1.join (t2.set_index 'D') on='B' drop_unmatched=True
            r1.to_sql.prepare . should_equal ['SELECT "T1"."A" AS "A", "T1"."B" AS "B", "T1"."C" AS "C", "T2"."E" AS "E", "T2"."F" AS "F" FROM (SELECT "T1"."A" AS "A", "T1"."B" AS "B", "T1"."C" AS "C", "T1"."B" AS "B_1" FROM "T1" AS "T1") AS "T1" INNER JOIN (SELECT "T2"."E" AS "E", "T2"."F" AS "F", "T2"."D" AS "D" FROM "T2" AS "T2") AS "T2" ON ("T1"."B_1" = "T2"."D")', []]

        Test.specify "should append suffixes to disambiguate column names" <|
            r1 = t1.join (t3.set_index 'E') on='A'
            r1.to_sql.prepare . should_equal ['SELECT "T1"."A" AS "A_left", "T1"."B" AS "B", "T1"."C" AS "C", "T3"."A" AS "A_right", "T3"."F" AS "F" FROM (SELECT "T1"."A" AS "A", "T1"."B" AS "B", "T1"."C" AS "C", "T1"."A" AS "A_1" FROM "T1" AS "T1") AS "T1" LEFT JOIN (SELECT "T3"."A" AS "A", "T3"."F" AS "F", "T3"."E" AS "E" FROM "T3" AS "T3") AS "T3" ON ("T1"."A_1" = "T3"."E")', []]

            r2 = (t3.set_index 'E').join (t2.set_index 'F')
            r2.index.name . should_equal "E_left"
            r2.columns . map .name . should_equal ["A", "F", "D", "E_right"]

        Test.specify "should avoid duplicates when disambiguating column names" <|
            connection =
                table1 = ["T1", [["X", int], ["A", int], ["A_left", int]]]
                table2 = ["T2", [["X", int], ["A", int], ["B", int]]]
                tables = Map.from_vector [table1, table2]
                Fake_Test_Connection.make Dialect.sqlite tables
            t1 = connection.access_table "T1"
            t2 = connection.access_table "T2"
            (t1.set_index "X").join (t2.set_index "X") . should_fail_with Illegal_State_Error

        Test.specify "should ensure that name suffixes are distinct" <|
            err = (t1.set_index 'A').join (t2.set_index 'D') left_suffix='foo' right_suffix='foo'
            err . should_fail_with Illegal_State_Error

        Test.specify "should correctly handle self-joins" <|
            r1 = t1.join (t1.set_index 'A') on='B'
            r1.to_sql.prepare . should_equal ['SELECT "T1_left"."A" AS "A", "T1_left"."B" AS "B_left", "T1_left"."C" AS "C_left", "T1_right"."B" AS "B_right", "T1_right"."C" AS "C_right" FROM (SELECT "T1"."A" AS "A", "T1"."B" AS "B", "T1"."C" AS "C", "T1"."B" AS "B_1" FROM "T1" AS "T1") AS "T1_left" LEFT JOIN (SELECT "T1"."B" AS "B", "T1"."C" AS "C", "T1"."A" AS "A" FROM "T1" AS "T1") AS "T1_right" ON ("T1_left"."B_1" = "T1_right"."A")', []]

    Test.group "[Codegen] Handling Missing Values" <|
        Test.specify "fill_missing should allow to replace missing values in a column with a constant" <|
            c = t1.at "A" . fill_missing "not-applicable"
            c.to_sql.prepare . should_equal ['SELECT COALESCE("T1"."A", ?) AS "A" FROM "T1" AS "T1"', [["not-applicable", int]]]

        Test.specify "drop_missing should drop missing rows in a Column" <|
            col = t1.at "A" . drop_missing
            col.to_sql.prepare . should_equal ['SELECT "T1"."A" AS "A" FROM "T1" AS "T1" WHERE (NOT ("T1"."A" IS NULL))', []]

        Test.specify "drop_missing_rows should drop rows that contain at least one missing column in a Table" <|
            t2 = t1.drop_missing_rows
            t2.to_sql.prepare . should_equal ['SELECT "T1"."A" AS "A", "T1"."B" AS "B", "T1"."C" AS "C" FROM "T1" AS "T1" WHERE (NOT ("T1"."A" IS NULL)) AND (NOT ("T1"."B" IS NULL)) AND (NOT ("T1"."C" IS NULL))', []]

    Test.group "[Codegen] Sorting" <|
        Test.specify "should allow sorting by a single column name" <|
            r1 = t1.order_by (Sort_Column_Selector.By_Name [Sort_Column.Name "A"]) . at "B"
            r1.to_sql.prepare . should_equal ['SELECT "T1"."B" AS "B" FROM "T1" AS "T1" ORDER BY "T1"."A" ASC', []]

            r2 = t1.order_by (Sort_Column_Selector.By_Name [Sort_Column.Name "B" Sort_Direction.Descending]) . at "A"
            r2.to_sql.prepare . should_equal ['SELECT "T1"."A" AS "A" FROM "T1" AS "T1" ORDER BY "T1"."B" DESC', []]

        Test.specify 'should allow sorting by multiple column names' <|
            r1 = t1.order_by (Sort_Column_Selector.By_Name [Sort_Column.Name 'A', Sort_Column.Name 'B'])
            r1.to_sql.prepare . should_equal ['SELECT "T1"."A" AS "A", "T1"."B" AS "B", "T1"."C" AS "C" FROM "T1" AS "T1" ORDER BY "T1"."A" ASC, "T1"."B" ASC', []]

        Test.specify 'should allow sorting with specific by-column rules' <|
            r1 = t1.order_by (Sort_Column_Selector.By_Name [Sort_Column.Name "A", Sort_Column.Name "B" Sort_Direction.Descending])
            r1.to_sql.prepare . should_equal ['SELECT "T1"."A" AS "A", "T1"."B" AS "B", "T1"."C" AS "C" FROM "T1" AS "T1" ORDER BY "T1"."A" ASC, "T1"."B" DESC', []]

        Test.specify 'should return warnings and errors when passed a non-existent column' <|
            action = t1.order_by (Sort_Column_Selector.By_Name [Sort_Column.Name 'foobar']) on_problems=_
            tester table =
                table.to_sql.prepare . should_equal ['SELECT "T1"."A" AS "A", "T1"."B" AS "B", "T1"."C" AS "C" FROM "T1" AS "T1"', []]
            problems = [Missing_Input_Columns [Sort_Column.Name 'foobar'], No_Input_Columns_Selected]
            Problems.test_problem_handling action problems tester

    Test.group "Helpers" <|
        Test.specify "combine_names should combine lists of names" <|
            v1 = ["A", "B"]
            v2 = ["A", "C"]
            combined = Table_Module.combine_names v1 v2 "_1" "_2"
            combined.first . should_equal ["A_1", "B"]
            combined.second . should_equal ["A_2", "C"]

            Test.expect_panic_with (Table_Module.combine_names ["A", "A_1"] ["A"] "_1" "_2") Illegal_State_Error
        Test.specify "fresh_names should provide fresh names" <|
            used_names = ["A", "A_1"]
            preferred_names = ["A", "A", "B"]
            Table_Module.fresh_names used_names preferred_names . should_equal ["A_2", "A_3", "B"]

    Test.group "[Codegen] Aggregation" <|
        Test.specify "should allow to count rows" <|
            code = t1.aggregate [Group_By "A" "A grp", Count "counter"] . to_sql . prepare
            code . should_equal ['SELECT "T1"."A" AS "A grp", COUNT(*) AS "counter" FROM "T1" AS "T1" GROUP BY "T1"."A"', []]

        Test.specify "should allow to group by multiple fields" <|
            code = t1.aggregate [Sum "A" "sum_a", Group_By "C" Nothing, Group_By "B" "B grp"] . to_sql . prepare
            code . should_equal ['SELECT SUM("T1"."A") AS "sum_a", "T1"."C" AS "C", "T1"."B" AS "B grp" FROM "T1" AS "T1" GROUP BY "T1"."C", "T1"."B"', []]

main = Test.Suite.run_main spec

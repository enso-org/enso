from Standard.Base import all
from Standard.Table import Table
from Standard.Test import all
from Standard.Table.Errors import all
import Standard.Base.Errors.Common.Type_Error

# pretty printed xml actually makes the tests less pretty
# so all but one tests depretty the xml before comparing
depretty xmlText =
    xmlText.replace (Regex.compile "\n\s*") ""

type Data
    #   | Title                  | Author              | Price | Year | Last Borrowed
    #---+------------------------+---------------------+-------+------+------------------------------------------
    # 0 | A Tale Of Two Cities   | Charles Dickens     | 9.99  | 1859 | 1999-01-02 03:40:00Z[UTC]
    # 1 | The Great Gatsby       | F. Scott Fitzgerald | 5.99  | 1925 | 2000-02-03 04:50:00Z[UTC]
    # 2 | The Catcher In The Rye | J. D. Salinger      | 6.99  | 1951 | 2001-03-04 05:25:00Z[UTC]
    # 3 | The Grapes Of Wrath    | John Steinbeck      | 7.99  | 1939 | 2002-04-05 06:15:00+01:00[UTC]
    Value ~table

    setup =
        make_table =
            title = ["Title", ["A Tale Of Two Cities", "The Great Gatsby", "The Catcher In The Rye", "The Grapes Of Wrath"]]
            author = ["Author", ["Charles Dickens", "F. Scott Fitzgerald", "J. D. Salinger", "John Steinbeck"]]
            price = ["Price", [9.99, 5.99, 6.99, 7.99]]
            year = ["Year", [1859, 1925, 1951, 1939]]
            last_borrowed = ["Last Borrowed", [Date_Time.new 1999 1 2 3 40 (zone = Time_Zone.utc), Date_Time.new 2000 2 3 4 50 (zone = Time_Zone.utc), Date_Time.new 2001 3 4 5 25 (zone = Time_Zone.utc), Date_Time.new 2002 4 5 6 15 (zone = Time_Zone.utc)]]

            Table.new [title, author, price, year, last_borrowed]
        Data.Value make_table

add_specs suite_builder =
    suite_builder.group "to_xml" group_builder->
        data = Data.setup
        group_builder.specify "table with no rows should become empty Table element" <|
            t = data.table . take 0
            r = t.to_xml
            e = "<Table/>"
            (depretty r).should_equal (depretty e)
        group_builder.specify "if no columns are specified all fields become child elements" <|
            t = data.table . select_columns ['Title', 'Year']
            r = t.to_xml
            e = '''
                <Table>
                  <Row>
                    <Title>A Tale Of Two Cities</Title>
                    <Year>1859</Year>
                  </Row>
                  <Row>
                    <Title>The Great Gatsby</Title>
                    <Year>1925</Year>
                  </Row>
                  <Row>
                    <Title>The Catcher In The Rye</Title>
                    <Year>1951</Year>
                  </Row>
                  <Row>
                    <Title>The Grapes Of Wrath</Title>
                    <Year>1939</Year>
                  </Row>
                </Table>
            (depretty r).should_equal (depretty e)
        group_builder.specify "Single column as element" <|
            t = data.table . take 4
            r = t.to_xml ["Year"]
            e = '''
                <Table>
                  <Row>
                    <Year>1859</Year>
                  </Row>
                  <Row>
                    <Year>1925</Year>
                  </Row>
                  <Row>
                    <Year>1951</Year>
                  </Row>
                  <Row>
                    <Year>1939</Year>
                  </Row>
                </Table>
            (depretty r).should_equal (depretty e)
        group_builder.specify "Single column as attribute" <|
            t = data.table . take 4
            r = t.to_xml [] ["Year"]
            e = '''
                <Table>
                  <Row Year="1859"/>
                  <Row Year="1925"/>
                  <Row Year="1951"/>
                  <Row Year="1939"/>
                </Table>
            (depretty r).should_equal (depretty e)
        group_builder.specify "Single column as value" <|
            t = data.table . take 4
            r = t.to_xml [] [] "Year"
            e = '''
                <Table>
                  <Row>1859</Row>
                  <Row>1925</Row>
                  <Row>1951</Row>
                  <Row>1939</Row>
                </Table>
            (depretty r).should_equal (depretty e)
        group_builder.specify "Different fields can be used as attributes, elements and value" <|
            t = data.table . take 1
            r = t.to_xml ["Year"] ["Author", "Price"] "Title"
            e = '''
                <Table>
                  <Row Author="Charles Dickens" Price="9.99">
                    A Tale Of Two Cities
                    <Year>1859</Year>
                  </Row>
                </Table>
           (depretty r).should_equal (depretty e)
        group_builder.specify "Can use indexes to specify columns" <|
            t = data.table . take 1
            r = t.to_xml [3] [1, 2] 0
            e = '''
                <Table>
                  <Row Author="Charles Dickens" Price="9.99">
                    A Tale Of Two Cities
                    <Year>1859</Year>
                  </Row>
                </Table>
            (depretty r).should_equal (depretty e)
        group_builder.specify "Can use regex to specify columns for element and attribute" <|
            t = data.table . take 1
            r = t.to_xml (Regex.compile ".*e") (Regex.compile ".*a.*") "Author"
            e = '''
                <Table>
                  <Row Last_Borrowed="1999-01-02T03:40Z[UTC]" Year="1859">
                    Charles Dickens
                    <Title>A Tale Of Two Cities</Title>
                    <Price>9.99</Price>
                  </Row>
                </Table>
            (depretty r).should_equal (depretty e)
        group_builder.specify "The same fields can be used as attributes, elements and value" <|
            t = data.table . take 1
            r = t.to_xml ["Author", "Price"] ["Author", "Price"] "Author"
            e = '''
                <Table>
                  <Row Author="Charles Dickens" Price="9.99">
                    Charles Dickens
                    <Author>Charles Dickens</Author>
                    <Price>9.99</Price>
                  </Row>
                </Table>
            (depretty r).should_equal (depretty e)
        group_builder.specify "If a field is specified duplicate times as an attribute or element it is only included once" <|
            t = data.table . take 1
            r = t.to_xml ["Author", "Price", "Author"] ["Author", "Price", "Price"] "Author"
            e = '''
                <Table>
                  <Row Author="Charles Dickens" Price="9.99">
                    Charles Dickens
                    <Author>Charles Dickens</Author>
                    <Price>9.99</Price>
                  </Row>
                </Table>
           (depretty r).should_equal (depretty e)
        group_builder.specify "Root and Row tags can be set" <|
            t = data.table . take 1
            r = t.to_xml ["Year"] ["Author", "Price"] "Title" "library" "book"
            e = '''
                <library>
                  <book Author="Charles Dickens" Price="9.99">
                    A Tale Of Two Cities
                    <Year>1859</Year>
                  </book>
                </library>
            (depretty r).should_equal (depretty e)
        group_builder.specify "Will warn if not all incoming columns specified" <|
            t = data.table . take 1
            r = t.to_xml ["Year"] ["Author", "Price"] "Title"
            e = '''
                <Table>
                  <Row Author="Charles Dickens" Price="9.99">
                    A Tale Of Two Cities
                    <Year>1859</Year>
                  </Row>
                </Table>
            (depretty r).should_equal (depretty e)
            Problems.expect_warning (Unexpected_Extra_Columns.Warning ["Last Borrowed"]) r
        group_builder.specify "Will not warn if not all incoming columns specified, but warnings are ignored" <|
            t = data.table . take 1
            r = t.to_xml ["Year"] ["Author", "Price"] "Title" on_problems=Problem_Behavior.Ignore
            e = '''
                <Table>
                  <Row Author="Charles Dickens" Price="9.99">
                    A Tale Of Two Cities
                    <Year>1859</Year>
                  </Row>
                </Table>
           (depretty r).should_equal (depretty e)
            Problems.assume_no_problems r
        group_builder.specify "Will error if not all incoming columns specified and on_problems set to Report_Error" <|
            t = data.table
            r = t.to_xml ["Year"] ["Author", "Price"] "Title" on_problems=Problem_Behavior.Report_Error
            r.should_fail_with (Unexpected_Extra_Columns.Warning ["Last Borrowed"])
        group_builder.specify "Will error if configured with a element column that is not in the table" <|
            t = data.table
            r = t.to_xml ["Author", "Price", "Not In Table"] ["Year"] "Title"
            r.should_fail_with (Missing_Input_Columns.Error ["Not In Table"])
        group_builder.specify "Will error if configured with a attribute column that is not in the table" <|
            t = data.table
            r = t.to_xml ["Author", "Price"] ["Year", "Not In Table"] "Title"
            r.should_fail_with (Missing_Input_Columns.Error ["Not In Table"])
        group_builder.specify "Will error if configured with a value column that is not in the table" <|
            t = data.table
            r = t.to_xml ["Author", "Price"] ["Year"] "Not In Table"
            r.should_fail_with (No_Such_Column.Error "Not In Table")
        group_builder.specify "XML should be pretty printed" <|
            # pretty printed xml actually makes the tests less pretty
            # so all the other tests depretty the xml before comparing
            # this one makes sure it actually is pretty printed for users
            t = data.table . take 2
            r = t.to_xml ["Year"] ["Author", "Price"] "Title"
            e = '<Table>\n'
                + '    <Row Author="Charles Dickens" Price="9.99">\n'
                + '        A Tale Of Two Cities\n'
                + '        <Year>1859</Year>\n'
                + '    </Row>\n'
                + '    <Row Author="F. Scott Fitzgerald" Price="5.99">\n'
                + '        The Great Gatsby\n'
                + '        <Year>1925</Year>\n'
                + '    </Row>\n'
                + '</Table>\n'
            r . should_equal e
        group_builder.specify "Illegal xml names are cleaned up in root_name and row_name" <|
            t = data.table . take 1
            r = t.to_xml ["Year"] ["Author", "Price"] "Title" "the library" "22book"
            e = '''
                <the_library>
                  <_22book Author="Charles Dickens" Price="9.99">
                    A Tale Of Two Cities
                    <Year>1859</Year>
                  </_22book>
                </the_library>
            (depretty r).should_equal (depretty e)
        group_builder.specify "Illegal xml names are cleaned up" <|
            numeric_column_name = ["1", [10]]
            illegal_column_name = ["Bad <chars> & symbols", [1]]
            last_borrowed = ["Last Borrowed", [Date_Time.new 1999 1 2 3 40 (zone = Time_Zone.utc)]]
            #   | 1  | Bad <chars> & symbols | Last Borrowed
            #---+----+-----------------------+-------------------------------------
            # 0 | 10 | 1                     | 1999-01-02 03:40:00Z[UTC]
            t = Table.new [numeric_column_name, illegal_column_name, last_borrowed]
            r = t.to_xml ["Bad <chars> & symbols", "Last Borrowed", "1"] ["Bad <chars> & symbols", "Last Borrowed", "1"]
            e = '''
                <Table>
                  <Row Bad__chars____symbols="1" Last_Borrowed="1999-01-02T03:40Z[UTC]" _1="10">
                    <_1>10</_1>
                    <Bad__chars____symbols>1</Bad__chars____symbols>
                    <Last_Borrowed>1999-01-02T03:40Z[UTC]</Last_Borrowed>
                  </Row>
                </Table>
            (depretty r).should_equal (depretty e)
        group_builder.specify "Empty strings are empty attributes/elements. Nothing values omit the entire attribute/element" <|
            desc_column = ["desc", ["Col1 and Col2 both have values", "Col1 has value, Col2 Nothing", "Col1 empty string, Col2 has value", "Col1 and Col2 both blank string", "Col1 and Col2 both Nothing" ]]
            col1 = ["Col1", ["Value1", "Value2", "", "", Nothing]]
            col2 = ["Col2", ["Value1", Nothing, "Value2", "", Nothing]]
            #   | desc                              | Col1    | Col2
            #---+-----------------------------------+---------+---------
            # 0 | Col1 and Col2 both have values    | Value1  | Value1
            # 1 | Col1 has value, Col2 Nothing      | Value2  | Nothing
            # 2 | Col1 empty string, Col2 has value |         | Value2
            # 3 | Col1 and Col2 both blank string   |         |
            # 4 | Col1 and Col2 both Nothing        | Nothing | Nothing
            t = Table.new [desc_column, col1, col2]
            r = t.to_xml ["Col1", "Col2"] ["desc", "Col1", "Col2"] "Col1"
            e = '''
                <Table>
                  <Row Col1="Value1" Col2="Value1" desc="Col1 and Col2 both have values">
                    Value1
                    <Col1>Value1</Col1>
                    <Col2>Value1</Col2>
                  </Row>
                  <Row Col1="Value2" desc="Col1 has value, Col2 Nothing">
                    Value2
                    <Col1>Value2</Col1>
                  </Row>
                  <Row Col1="" Col2="Value2" desc="Col1 empty string, Col2 has value">
                    <Col1/>
                    <Col2>Value2</Col2>
                  </Row>
                  <Row Col1="" Col2="" desc="Col1 and Col2 both blank string">
                    <Col1/>
                    <Col2/>
                  </Row>
                  <Row desc="Col1 and Col2 both Nothing"/>
                </Table>
            (depretty r).should_equal (depretty e)
        group_builder.specify "Panic if wrong types passed in element_columns" <|
            t = data.table
            r = Panic.recover Any (t.to_xml 1.23)
            r.to_text.should_equal "(Error: (Type_Error.Error Vector | Text | Integer | Regex 1.23 '`element_columns`'))"
        group_builder.specify "Panic if wrong types passed in element_columns vector" pending='Not working' <|
            t = data.table
            r = Panic.recover Any (t.to_xml [1.23])
            r.to_text.should_equal "(Error: (Type_Error.Error Vector | Text | Integer | Regex 1.23 '`element_columns`'))"
        group_builder.specify "Panic if wrong types passed in attribute_columns" <|
            t = data.table
            r = Panic.recover Any (t.to_xml [] 1.23)
            r.to_text.should_equal "(Error: (Type_Error.Error Vector | Text | Integer | Regex 1.23 '`attribute_columns`'))"
        group_builder.specify "Panic if wrong types passed in attribute_columns vector" pending='Not working' <|
            t = data.table
            r = Panic.recover Any (t.to_xml [] [1.23])
            r.to_text.should_equal "(Error: (Type_Error.Error Vector | Text | Integer | Regex 1.23 '`attribute_columns`'))"
        group_builder.specify "Panic if wrong types passed in value_column" <|
            t = data.table
            r = Panic.recover Any (t.to_xml [] [] 1.23)
            r.to_text.should_equal "(Error: (Type_Error.Error Text | Integer | Nothing 1.23 '`value_column`'))"
        group_builder.specify "Panic if wrong types passed in root_name" <|
            t = data.table
            r = Panic.recover Any (t.to_xml [] [] "Year" 1.23)
            r.to_text.should_equal "(Error: (Type_Error.Error Text 1.23 '`root_name`'))"
        group_builder.specify "Panic if wrong types passed in row_name" <|
            t = data.table
            r = Panic.recover Any (t.to_xml [] [] "Year" "Table" 1.23)
            r.to_text.should_equal "(Error: (Type_Error.Error Text 1.23 '`row_name`'))"
        group_builder.specify "Panic if wrong types passed in on_problems" <|
            t = data.table
            r = Panic.recover Any (t.to_xml [] [] "Year" "Table" "row" 1.23)
            r.to_text.should_equal "(Error: (Type_Error.Error Problem_Behavior 1.23 '`on_problems`'))"
        group_builder.specify "works with unicode characters" <|
            unicode_column = ["unicode", ['\u00A9', "üë©‚Äçüî¨"]]
            #   | unicode
            #---+-----------
            # 0 | '\u00A9'
            # 1 | üë©‚Äçüî¨
            t = Table.new [unicode_column]
            r = t.to_xml ["unicode"] ["unicode"] "unicode"
            e = '''
                <Table>
                  <Row unicode="\u00A9">
                    \u00A9
                    <unicode>\u00A9</unicode>
                  </Row>
                  <Row unicode="üë©‚Äçüî¨">
                    üë©‚Äçüî¨
                    <unicode>üë©‚Äçüî¨</unicode>
                  </Row>
                </Table>
            (depretty r).should_equal (depretty e)
        group_builder.specify "xml or special characters get escaped" <|
            xml_column = ["xml", ["</Table>", "<tag>12</tag>", '<r><c v="1"></c></r>']]
            special_chars_column = ["special", ["<", "&>", "'"]]
            #   | xml                  | special
            #---+----------------------+---------
            # 0 | </Table>              | <
            # 1 | <tag>12</tag>        | &>
            # 2 | <r><c v="1"></c></r> | '
            t = Table.new [xml_column, special_chars_column]
            r = t.to_xml ["xml", "special"] ["xml", "special"] "xml"
            e = '''
                <Table>
                  <Row special="&lt;" xml="&lt;/Table&gt;">
                    &lt;/Table&gt;
                    <xml>&lt;/Table&gt;</xml>
                    <special>&lt;</special>
                  </Row>
                  <Row special="&amp;&gt;" xml="&lt;tag&gt;12&lt;/tag&gt;">
                    &lt;tag&gt;12&lt;/tag&gt;
                    <xml>&lt;tag&gt;12&lt;/tag&gt;</xml>
                    <special>&amp;&gt;</special>
                  </Row>
                  <Row special="'" xml="&lt;r&gt;&lt;c v=&quot;1&quot;&gt;&lt;/c&gt;&lt;/r&gt;">
                    &lt;r&gt;&lt;c v="1"&gt;&lt;/c&gt;&lt;/r&gt;
                    <xml>&lt;r&gt;&lt;c v="1"&gt;&lt;/c&gt;&lt;/r&gt;</xml>
                    <special>'</special>
                  </Row>
                </Table>
            (depretty r).should_equal (depretty e)

main filter=Nothing =
    suite = Test.build suite_builder->
        add_specs suite_builder
    suite.run_with_filter filter


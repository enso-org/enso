from Standard.Base import all
import Standard.Base.Error.Common.Index_Out_Of_Bounds
import Standard.Base.Error.Illegal_Argument.Illegal_Argument

from Standard.Table import Column
import Standard.Table.Data.Storage.Storage

import Standard.Examples

from Standard.Test import Test, Test_Suite
import Standard.Test.Extensions

polyglot java import org.enso.table.data.column.storage.Storage as Java_Storage

main = Test_Suite.run_main spec

spec = Test.group "Columns" <|
    test_column = Column.from_vector "Test" [1, 3, 5, 2, 4, 6]
    empty_column = Column.from_vector "Test" []

    Test.specify "should correctly map storage types" <|
        Storage.types.at Java_Storage.Type.LONG . should_equal Storage.Integer
        Storage.types.at Java_Storage.Type.DOUBLE . should_equal Storage.Decimal
        Storage.types.at Java_Storage.Type.STRING . should_equal Storage.Text
        Storage.types.at Java_Storage.Type.BOOL . should_equal Storage.Boolean
        Storage.types.at Java_Storage.Type.OBJECT . should_equal Storage.Any
        Storage.types.at Java_Storage.Type.DATE . should_equal Storage.Date
        Storage.types.at Java_Storage.Type.TIME_OF_DAY . should_equal Storage.Time_Of_Day
        Storage.types.at Java_Storage.Type.DATE_TIME . should_equal Storage.Date_Time

    Test.specify "should allow getting specific elements" <|
        test_column.at 0 . should_equal 1
        test_column.at 2 . should_equal 5
        test_column.at 5 . should_equal 6
        test_column.at 6 . should_fail_with Index_Out_Of_Bounds
        empty_column.at 0 . should_fail_with Index_Out_Of_Bounds

    Test.specify "should be able to take the first n elements" <|
        expected_1 = Column.from_vector "Test" [1, 3, 5]
        expected_2 = Column.from_vector "Test" [1, 3, 5, 2, 4, 6]
        expected_3 = Column.from_vector "Test" []
        test_column.take (First 3) . to_vector . should_equal expected_1.to_vector
        test_column.take (First 7) . to_vector . should_equal expected_2.to_vector
        test_column.take (First 0) . to_vector . should_equal expected_3.to_vector

    Test.specify "should be able to take the first n elements by Integer" <|
        expected_1 = Column.from_vector "Test" [1, 3, 5]
        expected_2 = Column.from_vector "Test" [1, 3, 5, 2, 4, 6]
        expected_3 = Column.from_vector "Test" []
        test_column.take 3 . to_vector . should_equal expected_1.to_vector
        test_column.take 7 . to_vector . should_equal expected_2.to_vector
        test_column.take 0 . to_vector . should_equal expected_3.to_vector

    Test.specify "should be able to take the last n elements" <|
        expected_1 = Column.from_vector "Test" [2, 4, 6]
        expected_2 = Column.from_vector "Test" [1, 3, 5, 2, 4, 6]
        expected_3 = Column.from_vector "Test" []
        test_column.take (Last 3) . to_vector . should_equal expected_1.to_vector
        test_column.take (Last 7) . to_vector . should_equal expected_2.to_vector
        test_column.take (Last 0) . to_vector . should_equal expected_3.to_vector

    Test.specify "should be able to get the first element" <|
        test_column.first . should_equal 1
        empty_column.first.should_fail_with Index_Out_Of_Bounds

    Test.specify "should be able to get the last element" <|
        test_column.last . should_equal 6
        empty_column.last.should_fail_with Index_Out_Of_Bounds

    Test.specify "should be able to be reversed" <|
        expected_1 = Column.from_vector "Test" [6, 4, 2, 5, 3, 1]
        test_column.reverse.to_vector . should_equal expected_1.to_vector
        empty_column.reverse.to_vector . should_equal empty_column.to_vector

    Test.specify "should allow to fill missing values from another column" <|
        nulled = Column.from_vector "col" [0, Nothing, 4, 5, Nothing, Nothing]
        defaults = Column.from_vector "def" [1, 2, 10, 20, Nothing, 30]
        r = nulled.fill_missing defaults
        r.to_vector . should_equal [0, 2, 4, 5, Nothing, 30]

    Test.specify "should allow to count duplicate value occurences" <|
        c_1 = Column.from_vector "c_1" [0, 1, 2, 2, 1, 0, 2]
        c_1.duplicate_count.to_vector.should_equal [0, 0, 0, 1, 1, 1, 2]

        c_2 = Column.from_vector "c_2" ["foo", "bar", "foo", "baz", "bar"]
        c_2.duplicate_count.to_vector.should_equal [0, 0, 1, 0, 1]

    Test.specify "should result in correct Storage if operation allows it" <|
        another = Column.from_vector "Test" [10, 20, 30, 40, 50, 60]
        (test_column + 1).storage_type . should_equal Storage.Integer
        (test_column - 1).storage_type . should_equal Storage.Integer
        (test_column * 2).storage_type . should_equal Storage.Integer
        (test_column * 1.5).storage_type . should_equal Storage.Decimal
        (test_column + another).storage_type . should_equal Storage.Integer

    Test.specify "should not allow invalid column names" <|
        c1 = Column.from_vector "" [1, 2, 3]
        c1.should_fail_with Illegal_Argument

        c2 = Column.from_vector Nothing [1, 2, 3]
        c2.should_fail_with Illegal_Argument

        c3 = Column.from_vector '\0' [1, 2, 3]
        c3.should_fail_with Illegal_Argument

        c4 = Column.from_vector 'foo\0bar' [1, 2, 3]
        c4.should_fail_with Illegal_Argument

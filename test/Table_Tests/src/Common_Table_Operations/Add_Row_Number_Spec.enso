from Standard.Base import all

from Standard.Table import Sort_Column
from Standard.Table.Errors import Missing_Input_Columns, Column_Indexes_Out_Of_Range, Duplicate_Output_Column_Names, Floating_Point_Equality

from Standard.Test import Test, Problems
import Standard.Test.Extensions

from project.Common_Table_Operations.Util import run_default_backend

polyglot java import java.lang.Long as Java_Long

main = run_default_backend spec

spec setup =
    prefix = setup.prefix
    table_builder = setup.table_builder
    Test.group prefix+"Table.add_row_number" <|
        Test.specify "should add a row numbering column" <|
            t = table_builder [["X", ['a', 'b', 'a', 'a', 'c']]]
            t1 = t.add_row_number
            rows = t1.rows.to_vector . map .to_vector
            rows . should_equal [['a', 1], ['b', 2], ['a', 3], ['a', 4], ['c', 5]]
            t1.at "Row" . to_vector . should_equal [1, 2, 3, 4, 5]
            t1.at "Row" . value_type . is_integer . should_be_true

        Test.specify "should rename existing column upon a name clash" <|
            t1 = table_builder [["X", ['a', 'b']], ["Y", ['c', 'd']], ["Z", [40, 20]]]
            t2 = t1.add_row_number name="Y"
            t2.column_names . should_equal ["X", "Y 1", "Z", "Y"]
            t2.at "X" . to_vector . should_equal ['a', 'b']
            t2.at "Y 1" . to_vector . should_equal ['c', 'd']
            t2.at "Z" . to_vector . should_equal [40, 20]
            t2.at "Y" . to_vector . should_equal [1, 2]
            Problems.expect_warning Duplicate_Output_Column_Names t2

            r3 = t1.add_row_number name="X" on_problems=Problem_Behavior.Report_Error
            r3.should_fail_with Duplicate_Output_Column_Names

        Test.specify "should allow customizing the starting index and step" <|
            t = table_builder [["X", ['a', 'b', 'a']]]
            t1 = t.add_row_number from=10
            t1.at "Row" . to_vector . should_equal [10, 11, 12]

            t2 = t.add_row_number step=10
            t2.at "Row" . to_vector . should_equal [1, 11, 21]

            t3 = t.add_row_number from=100 step=(-10)
            t3.at "Row" . to_vector . should_equal [100, 90, 80]

            t4 = t.add_row_number from=44 step=0
            t4.at "Row" . to_vector . should_equal [44, 44, 44]

            t5 = t.add_row_number from=(-1)
            t5.at "Row" . to_vector . should_equal [-1, 0, 1]

        Test.specify "should allow to assign row numbers separately within each group" <|
            t = table_builder [["X", ['a', 'a', 'a', 'a', 'b', 'b']], ["Y", [40, 30, 20, 40, 20, 10]]]
            t1 = t.add_row_number group_by=["X"]
            t1.at "Row" . to_vector . should_equal [1, 2, 3, 4, 1, 2]

            t2 = table_builder [["X", ['a', 'a', 'a', 'a', 'b', 'b']], ["Y", [40, 40, 20, 20, 20, 10]]]
            t3 = t2.add_row_number group_by=["X", "Y"]
            t3.at "Row" . to_vector . should_equal [1, 2, 1, 2, 1, 1]

        Test.specify "should allow to assign row numbers separately within scattered groups, preserving the row layout" <|
            v = ['a', 'b', 'a', 'b', 'b', 'b', 'c', 'a']
            t = table_builder [["X", v]]
            t1 = t.add_row_number group_by=["X"]
            # No reordering of elements:
            t1.at "X" . to_vector . should_equal v
            t1.at "Row" . to_vector . should_equal [1, 1, 2, 2, 3, 4, 1, 3]

        Test.specify "should allow to order the row numbers by some columns, keeping the row ordering intact" <|
            v = [9, 8, 7, 6, 5, 4, 100, 200]
            t = table_builder [["X", v]]
            t1 = t.add_row_number order_by=["X"]
            # No reordering of elements
            t1.at "X" . to_vector . should_equal v
            t1.at "Row" . to_vector . should_equal [6, 5, 4, 3, 2, 1, 7, 8]

            t2 = table_builder [["X", ["a", "b", "a", "a"]], ["Y", [1, 2, 3, 4]]]
            t3 = t2.add_row_number order_by=["X", (Sort_Column.Name "Y" Sort_Direction.Descending)]
            t3.at "Row" . to_vector . should_equal [3, 4, 2, 1]

        Test.specify "should allow mixing grouping with ordering and custom start and step" <|
            vx = ['a', 'b', 'a', 'a', 'a', 'b', 'c', 'c']
            vy = [9, 8, 7, 6, 5, 4, 100, 200]
            t = table_builder [["X", vx], ["Y", vy]]
            t1 = t.add_row_number group_by=["X"] order_by=["Y"] from=100 step=100

            # No reordering
            t1.at "X" . to_vector . should_equal vx
            t1.at "Y" . to_vector . should_equal vy

            t1.at "Row" . to_vector . should_equal [400, 200, 300, 200, 100, 100, 100, 200]

        Test.specify "should report floating point equality warning when grouping on float columns" <|
            t = table_builder [["X", [1.0, 1.5, 1.0, 2.5, 2.5]]]
            t1 = t.add_row_number group_by=["X"]
            Problems.expect_warning Floating_Point_Equality t1
            t1.at "Row" . to_vector . should_equal [1, 1, 2, 1, 2]

            r2 = t.add_row_number group_by=["X"] on_problems=Problem_Behavior.Report_Error
            r2.should_fail_with Floating_Point_Equality

            t3 = t.add_row_number order_by=["X"]
            Problems.assume_no_problems t3
            t3.at "Row" . to_vector . should_equal [1, 3, 2, 4, 5]

            t4 = table_builder [["X", [1, "A", 1, 24.0, 24.0, 24.0, 24]]]
            t5 = t4.add_row_number group_by=["X"]
            Problems.expect_warning Floating_Point_Equality t5
            t5.at "Row" . to_vector . should_equal [1, 1, 2, 1, 2, 3, 4]

        Test.specify "should fail if columns provided in ordering/grouping do not exist" <|
            t = table_builder [["X", [20, 30, 10]]]
            r1 = t.add_row_number group_by=["X", "Y", "Z"]
            r1.should_fail_with Missing_Input_Columns
            r1.catch.criteria . should_equal ["Y", "Z"]

            r2 = t.add_row_number order_by=["Z", "X", "Y"]
            r2.should_fail_with Missing_Input_Columns
            r2.catch.criteria . should_equal ["Z", "Y"]

            r3 = t.add_row_number group_by=[44]
            r3.should_fail_with Column_Indexes_Out_Of_Range

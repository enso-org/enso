from Standard.Base import all

from Standard.Table.Errors import all

from Standard.Test import Test, Problems
import Standard.Test.Extensions

from project.Common_Table_Operations.Util import run_default_backend

main = run_default_backend spec

spec setup =
    prefix = setup.prefix
    table_builder = setup.table_builder
    db_todo = if setup.is_database.not then Nothing else "Table.transpose is not implemented yet in Database."
    Test.group prefix+"Table.transpose" pending=db_todo <|
        Test.specify "should transpose all columns by default" <|
            t = table_builder [["Key", ["x", "y", "z"]], ["Value", [1, 2, 3]], ["Another", [10, Nothing, 20]], ["Yet Another", [Nothing, "Hello", "World"]]]
            t1 = t.transpose
            t1.row_count . should_equal 12
            t1.column_count . should_equal 2
            t1.at "Name" . to_vector . should_equal ["Key", "Value", "Another", "Yet Another", "Key", "Value", "Another", "Yet Another", "Key", "Value", "Another", "Yet Another"]
            t1.at "Value" . to_vector . should_equal ["x", 1, 10, Nothing, "y", 2, Nothing, "Hello", "z", 3, 20, "World"]

        Test.specify "should allow custom names" <|
            t = table_builder [["Key", ["x", "y", "z"]], ["Value", [1, 2, 3]], ["Another", [10, Nothing, 20]], ["Yet Another", [Nothing, "Hello", "World"]]]
            t1 = t.transpose name_field="Key" value_field="Object"
            t1.row_count . should_equal 12
            t1.column_count . should_equal 2
            t1.at "Key" . to_vector . should_equal ["Key", "Value", "Another", "Yet Another", "Key", "Value", "Another", "Yet Another", "Key", "Value", "Another", "Yet Another"]
            t1.at "Object" . to_vector . should_equal ["x", 1, 10, Nothing, "y", 2, Nothing, "Hello", "z", 3, 20, "World"]

        Test.specify "should allow id fields" <|
            t = table_builder [["Key", ["x", "y", "z"]], ["Value", [1, 2, 3]], ["Another", [10, Nothing, 20]], ["Yet Another", [Nothing, "Hello", "World"]]]
            t1 = t.transpose ["Key"]
            t1.row_count . should_equal 9
            t1.column_names . should_equal ["Key", "Name", "Value"]
            t1.at "Key" . to_vector . should_equal ["x", "x", "x", "y", "y", "y", "z", "z", "z"]
            t1.at "Name" . to_vector . should_equal ["Value", "Another", "Yet Another", "Value", "Another", "Yet Another", "Value", "Another", "Yet Another"]
            t1.at "Value" . to_vector . should_equal [1, 10, Nothing, 2, Nothing, "Hello", 3, 20, "World"]

        Test.specify "should allow single id field" <|
            t = table_builder [["Key", ["x", "y", "z"]], ["Value", [1, 2, 3]], ["Another", [10, Nothing, 20]], ["Yet Another", [Nothing, "Hello", "World"]]]
            t1 = t.transpose "Key"
            t1.row_count . should_equal 9
            t1.column_count . should_equal 3
            t1.at "Key" . to_vector . should_equal ["x", "x", "x", "y", "y", "y", "z", "z", "z"]
            t1.at "Name" . to_vector . should_equal ["Value", "Another", "Yet Another", "Value", "Another", "Yet Another", "Value", "Another", "Yet Another"]
            t1.at "Value" . to_vector . should_equal [1, 10, Nothing, 2, Nothing, "Hello", 3, 20, "World"]

        Test.specify "should allow fields selected by index" <|
            t = table_builder [["Key", ["x", "y", "z"]], ["Value", [1, 2, 3]], ["Another", [10, Nothing, 20]], ["Yet Another", [Nothing, "Hello", "World"]]]
            t1 = t.transpose [0, -1]
            t1.column_names . should_equal ["Key", "Yet Another", "Name", "Value"]
            t1.row_count . should_equal 6
            t1.at "Key" . to_vector . should_equal ["x", "x", "y", "y", "z", "z"]
            t1.at "Yet Another" . to_vector . should_equal [Nothing, Nothing, "Hello", "Hello", "World", "World"]
            t1.at "Name" . to_vector . should_equal ["Value", "Another", "Value", "Another", "Value", "Another"]
            t1.at "Value" . to_vector . should_equal [1, 10, 2, Nothing, 3, 20]

        Test.specify "should allow all current columns to become id fields, without warning" <|
            t = table_builder [["Key", ["x", "y", "z"]], ["Foo", [1, 2, 3]], ["Another", [10, Nothing, 20]], ["Yet Another", [Nothing, "Hello", "World"]]]
            t1 = t.transpose t.column_names
            t1.row_count . should_equal 3
            t1.column_names . should_equal ["Key", "Foo", "Another", "Yet Another", "Name", "Value"]
            t1.at "Key" . to_vector . should_equal ["x", "y", "z"]
            t1.at "Foo" . to_vector . should_equal [1, 2, 3]
            t1.at "Name" . to_vector . should_equal [Nothing, Nothing, Nothing]
            t1.at "Value" . to_vector . should_equal [Nothing, Nothing, Nothing]
            Problems.assume_no_problems t1

        # ToDo: Verify the warnings and error handling within transpose.
        Test.specify "should handle missing columns" <|
            t1 = table_builder [["Key", ["x", "y", "z"]], ["Value", [1, 2, 3]], ["Another", [10, Nothing, 20]]]

            err1 = t1.transpose ["Key", "Missing", "Missing_2"]
            err1.should_fail_with Missing_Input_Columns
            err1.catch.criteria . should_equal ["Missing", "Missing_2"]

            err2 = t1.transpose [0, -1, 42, -100]
            err2.should_fail_with Column_Indexes_Out_Of_Range
            err2.catch.indexes . should_equal [42, -100]

            action1 = t1.transpose ["Key", "Missing", "Missing_2"] error_on_missing_columns=False on_problems=_
            tester1 table =
                table.column_names . should_equal ["Key", "Name", "Value"]
            problems1 = [Missing_Input_Columns.Error ["Missing", "Missing_2"]]
            Problems.test_problem_handling action1 problems1 tester1

            action2 = t1.transpose [0, -1, 42, -100] error_on_missing_columns=False on_problems=_
            tester2 table =
                table.column_names . should_equal ["Key", "Another", "Name", "Value"]
            problems2 = [Column_Indexes_Out_Of_Range.Error [42, -100]]
            Problems.test_problem_handling action2 problems2 tester2

        Test.specify "should warn on column name clashes" <|
            t1 = table_builder [["X", ["x", "y", "z"]], ["Y", [1, 2, 3]], ["Z", [10, Nothing, 20]]]

            action1 = t1.transpose ["X", "Y", "Z"] name_field="Y" value_field="Z" on_problems=_
            tester1 table =
                table.column_names . should_equal ["X", "Y", "Z", "Y_1", "Z_1"]
            problems1 = [Duplicate_Output_Column_Names.Error ["Y", "Z"]]
            Problems.test_problem_handling action1 problems1 tester1

            action2 = t1.transpose ["X"] name_field="F" value_field="F" on_problems=_
            tester2 table =
                table.column_names . should_equal ["X", "F", "F_1"]
            problems2 = [Duplicate_Output_Column_Names.Error ["F"]]
            Problems.test_problem_handling action2 problems2 tester2

            # No clash with the columns that are removed by transpose.
            t2 = t1.transpose ["X"] name_field="Y" value_field="Z" on_problems=Problem_Behavior.Report_Error
            Problems.assume_no_problems t2
            t2.column_names . should_equal ["X", "Y", "Z"]

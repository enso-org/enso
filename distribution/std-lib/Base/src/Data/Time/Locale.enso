from Base import all

polyglot java import java.util.Locale as JavaLocale

# Constants
# These are chosen as the union of the top 10 countries by population, and the
# top ten countries by total (nominal, not per-capita) GDP.

## The default locale.
default : Locale
default = here.from_java JavaLocale.ROOT

## A locale representing Bangladesh.
bangladesh : Locale
bangladesh = here.from_language_tag "bn-BD"

## A locale representing Brazil.
brazil : Locale
brazil = here.from_language_tag "pt-BR"

## A locale representing Canada with language English.
canada_english : Locale
canada_english = here.from_language_tag "en-CA"

## A locale representing Canada with language French.
canada_french : Locale
canada_french = here.from_language_tag "fr-CA"

## A locale representing the PRC.
china : Locale
china = here.from_language_tag "zh-CN"

## A locale representing France.
france : Locale
france = here.from_language_tag "fr-FR"

## A locale representing Germany.
germany : Locale
germany = here.from_language_tag "de-DE"

## A locale representing India with language Hindi.
india_hindi : Locale
india_hindi = here.from_language_tag "hi-IN"

## A locale representing India with language English.
india_english : Locale
india_english = here.from_language_tag "en-IN"

## A locale representing Indonesia.
indonesia : Locale
indonesia = here.from_language_tag "id-ID"

## A locale representing Italy.
italy : Locale
italy = here.from_language_tag "it-IT"

## A locale representing Japan.
japan : Locale
japan = here.from_language_tag "jp-JP"

## A locale representing Mexico.
mexico : Locale
mexico = here.from_language_tag "es-MX"

## A locale representing Nigeria.
nigeria : Locale
nigeria = here.from_language_tag "en-NG"

## A locale representing paksitan with language Urdu.
pakistan_urdu : Locale
pakistan_urdu = here.from_language_tag "ur-PK"

## A locale representing paksitan with language English.
pakistan_english : Locale
pakistan_english = here.from_language_tag "en-PK"

## A locale representing Russia.
russia : Locale
russia = here.from_language_tag "ru-RU"

## A locale representing South Korea.
south_korea : Locale
south_korea = here.from_language_tag "ko-KR"

## A locale representing the UK.
uk : Locale
uk = here.from_language_tag "en-GB"

## A locale representing the United States.
us : Locale
us = here.from_language_tag "en-US"

## Construct a new locale.

   In addition to the basic constructors, it can take a vector containing locale
   properties.
   > Example
     A locale representing en-GB.UTF-8.
         Locale.new "en" "GB" "UTF-8"
new : Text -> Text | Nothing -> Text | Nothing -> Locale
new language country=Nothing variant=Nothing =
    country_text = if country.is_nothing then "" else country
    variant_text = if variant.is_nothing then "" else variant
    java_locale = JavaLocale.new language country_text variant_text
    here.from_java java_locale

## Returns the locale specified by the provided IETF BCP47 language tag string.

   If the specified language tag contains any ill-formed subtags, the first such
   subtag and all following subtags are ignored.

   The following conversions are performed:
   - The language code "und" is mapped to language "".
   - The language codes "he", "yi", and "id" are mapped to "iw", "ji", and "in"
     respectively.
   - The portion of a private use subtag prefixed by "lvariant", if any, is
     removed and appended to the variant field in the result locale (without
     case normalization).
   - When the languageTag argument contains an extlang subtag, the first such
     subtag is used as the language, and the primary language subtag and other
     extlang subtags are ignored.
   - Case is normalized except for variant tags, which are left unchanged.
     Language is normalized to lower case, script to title case, country to
     upper case, and extensions to lower case.
   - If, after processing, the locale would exactly match either ja_JP_JP or
     th_TH_TH with no extensions, the appropriate extensions are added.

   This implements the 'Language-Tag' production of BCP47, and so supports
   grandfathered (regular and irregular) as well as private use language tags.

   > Example
     Creating the locale en_US.
         Locale.from_language_tag "en_US"
from_language_tag : Text -> Locale | Nothing
from_language_tag tag =
    java_locale = JavaLocale.forLanguageTag tag
    here.from_java java_locale

## A type representing a locale.

   A locale consists of three parts:
   - A language, which is mandatory.
   - A country code, which is optional.
   - A variant, which is optional.
type Locale
    type Locale java_locale

    ## Gets the language from the locale.
    language : Text | Nothing
    language =
        lang = this.java_locale.getLanguage
        if lang.is_empty then Nothing else lang

    ## Gets the country from the locale.
    country : Text | Nothing
    country =
        place = this.java_locale.getCountry
        if place.is_empty then Nothing else place

    ## Gets the country from the locale.
    variant : Text | Nothing
    variant =
        var = this.java_locale.getVariant
        if var.is_empty then Nothing else var

    ## Gets a representation of the language in the locale that can be shown to
       the user.
    display_language : Text | Nothing
    display_language =
        disp = this.java_locale.getDisplayLanguage
        if disp.is_empty then Nothing else disp

    ## Gets a representation of the country in the locale that can be shown to
       the user.
    display_country : Text | Nothing
    display_country =
        disp = this.java_locale.getDisplayCountry
        if disp.is_empty then Nothing else disp

    ## Gets a representation of the variant in the locale that can be shown to
       the user.
    display_variant : Text | Nothing
    display_variant =
        disp = this.java_locale.getDisplayVariant
        if disp.is_empty then Nothing else disp

    ## Converts the locale to text.
    to_text : Text | Nothing
    to_text = this.java_locale.toLanguageTag

    ## A Locale to Json conversion
    to_json : Json.Object
    to_json =
        b = Vector.new_builder
        b.append ["type", "Locale"]
        if this.language.is_nothing.not then b.append ["language", this.language]
        if this.country.is_nothing.not then b.append ["country", this.country]
        if this.variant.is_nothing.not then b.append ["variant", this.variant]
        Json.from_pairs b.to_vector

## PRIVATE
from_java : JavaLocale -> Locale
from_java java = Locale java

## PRIVATE
javaLocaleBuilder = JavaLocale.Builder

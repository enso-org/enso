from Base import all

polyglot java import java.net.URI as Java_URI
polyglot java import java.util.Optional

## A type that can be converted to Uri.
type To_Uri

    type To_Uri to_uri

## Implement To_Uri for Text
Text.to_uri = here.parse this

type Uri_Error
    type Syntax_Error message

## PRIVATE
panic_on_error ~action =
    action . catch e-> case e of
        Syntax_Error msg -> Panic.throw (Syntax_Error msg)

## PRIVATE
empty_if_null : Text | Nothing -> Text
empty_if_null value = case value of
    Nothing -> ""
    x -> x

type Uri

    ## Represents a Uniform Resource Identifier (URI) reference.
    type Uri internal_uri

    ## Implement To_Uri
    to_uri : Uri
    to_uri = this

    ## Get scheme part of this Uri.

       > Example
         Return the "http" part of the HTTP address.
             addr = "http://user:pass@example.com/foo/bar?key=val"
             Uri.parse addr . scheme
    scheme : Text
    scheme = here.empty_if_null this.internal_uri.getScheme

    ## Get user info part of this Uri.

       > Example
         Return the "user:pass" part of the HTTP address.
             addr = "http://user:pass@example.com/foo/bar?key=val"
             Uri.parse addr . user_info
    user_info : Text
    user_info = here.empty_if_null this.internal_uri.getUserInfo

    ## Get host part of this Uri.

       > Example
         Return the "example.com" part of the HTTP address.
             addr = "http://user:pass@example.com/foo/bar?key=val"
             Uri.parse addr . host
    host : Text
    host = here.empty_if_null this.internal_uri.getHost

    ## Get authority (user info and host) part of this Uri.

       > Example
         Return the "user:pass@example.com" part of the HTTP address.
             addr = "http://user:pass@example.com/foo/bar?key=val"
             Uri.parse addr . authority
    authority : Text
    authority = here.empty_if_null this.internal_uri.getAuthority

    ## Get port part of this Uri.

       > Example
         Return the "80" part of the HTTP address.
             addr = "http://user:pass@example.com:80/foo/bar?key=val"
             Uri.parse addr . port

       > Example
         Return the empty string if the port is not specified.
             addr = "http://user:pass@example.com:80/foo/bar?key=val"
             Uri.parse addr . port
    port : Text
    port =
        port_number = this.internal_uri.getPort
        if port_number == -1 then "" else port_number.to_text

    ## Get path part of this Uri.

       > Example
         Return the "/foo/bar" part of the HTTP address.
             addr = "http://user:pass@example.com:80/foo/bar?key=val"
             Uri.parse addr . path
    path : Text
    path = here.empty_if_null this.internal_uri.getPath

    ## Get query part of this Uri.

       > Example
         Return the "key=val" part of the HTTP address.
             addr = "http://user:pass@example.com:80/foo/bar?key=val"
             Uri.parse addr . query
    query : Text
    query = here.empty_if_null this.internal_uri.getQuery

    ## Get fragment part of this Uri.

       > Example
         Return the empty fragment of the HTTP address.
             addr = "http://user:pass@example.com:80/foo/bar?key=val"
             Uri.parse addr . fragment
    fragment : Text
    fragment = here.empty_if_null this.internal_uri.getFragment

    ## Get unescaped user info part of this Uri.
    raw_user_info : Text
    raw_user_info = here.empty_if_null this.internal_uri.getRawUserInfo

    ## Get unescaped authority part of this Uri.
    raw_authority : Text
    raw_authority = here.empty_if_null this.internal_uri.getRawAuthority

    ## Get unescaped path part of this Uri.
    raw_path : Text
    raw_path = here.empty_if_null this.internal_uri.getRawPath

    ## Get unescaped query part of this Uri.
    raw_query : Text
    raw_query = here.empty_if_null this.internal_uri.getRawQuery

    ## Get unescaped fragment part of this Uri.
    raw_fragment : Text
    raw_fragment = here.empty_if_null this.internal_uri.getRawFragment

    ## Convert this Uri to text.
    to_text : Text
    to_text = this.internal_uri.toString

    ## An Uri to JSON conversion.
    to_json : Json.String
    to_json : Json.String this.to_text

    ## Check Uri equality.
    == : Uri -> Boolean
    == that = this.internal_uri.equals that.internal_uri

## Parse Uri from text.
   Return Syntax_Error when the text cannot be parsed as Uri.

   > Example
     Parse Uri text.
         Uri.parse "http://example.com"
parse : Text -> Uri ! Syntax_Error
parse text =
    Panic.recover (Uri (Java_URI.create text)) . catch e-> case e of
        Polyglot_Error ex -> Error.throw (Syntax_Error ex.getMessage)
        other -> Panic.throw other

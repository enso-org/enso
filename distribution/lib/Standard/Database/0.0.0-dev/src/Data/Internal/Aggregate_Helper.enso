from Standard.Base import all
from Standard.Base.Data.Text.Text_Ordering import Text_Ordering

from Standard.Table.Data.Aggregate_Column import all
import Standard.Database.Data.Internal.IR
from Standard.Database.Data.Sql import Sql_Type
from Standard.Database.Error import Unsupported_Database_Operation_Error

## PRIVATE
   Creates an `Internal_Column` that computes the specified statistic.
   It returns a dataflow error if the given operation is not supported.

   The provided `aggregate` is assumed to contain only already resolved columns.
   You may need to transform it with `resolve_aggregate` first.
make_aggregate_column : Table -> Aggregate_Column -> Text -> IR.Internal_Column
make_aggregate_column table aggregate new_name =
    sql_type = table.connection.dialect.resolve_target_sql_type aggregate
    expression = make_expression aggregate table.connection.dialect
    IR.Internal_Column new_name sql_type expression

## PRIVATE
   Creates an Internal Representation of the expression that computes a
   requested statistic.
make_expression : Aggregate_Column -> Dialect -> IR.Expression
make_expression aggregate dialect =
    is_non_empty_selector v = if v.is_nothing then False else v.columns.not_empty
    case aggregate of
        Group_By c _ -> c.expression
        Count _ -> IR.Operation "COUNT_ROWS" []
        Count_Distinct columns _ ignore_nothing -> if columns.is_empty then Error.throw (Illegal_Argument_Error "Count_Distinct must have at least one column.") else
            case ignore_nothing of
                True -> IR.Operation "COUNT_DISTINCT" (columns.map .expression)
                False -> IR.Operation "COUNT_DISTINCT_INCLUDE_NULL" (columns.map .expression)
        Count_Not_Nothing c _ -> IR.Operation "COUNT" [c.expression]
        Count_Nothing c _ -> IR.Operation "COUNT_IS_NULL" [c.expression]
        Count_Not_Empty c _ -> IR.Operation "COUNT_NOT_EMPTY" [c.expression]
        Count_Empty c _ -> IR.Operation "COUNT_EMPTY" [c.expression]
        Percentile p c _ -> IR.Operation "PERCENTILE" [IR.Constant Sql_Type.double p, c.expression]
        Mode c _ -> IR.Operation "MODE" [c.expression]
        First c _ ignore_nothing order_by -> case is_non_empty_selector order_by of
            False -> Error.throw (Unsupported_Database_Operation_Error "`First` aggregation requires at least one `order_by` column.")
            True ->
                order_bys = order_by.columns.map c-> dialect.prepare_order_descriptor c.column.as_internal c.direction Text_Ordering
                case ignore_nothing of
                    False -> IR.Operation "FIRST" [c.expression]+order_bys
                    True -> IR.Operation "FIRST_NOT_NULL" [c.expression]+order_bys
        Last c _ ignore_nothing order_by -> case is_non_empty_selector order_by of
            False -> Error.throw (Unsupported_Database_Operation_Error "`Last` aggregation requires at least one `order_by` column.")
            True ->
                order_bys = order_by.columns.map c-> dialect.prepare_order_descriptor c.column.as_internal c.direction Text_Ordering
                case ignore_nothing of
                    False -> IR.Operation "LAST" [c.expression]+order_bys
                    True -> IR.Operation "LAST_NOT_NULL" [c.expression]+order_bys
        Maximum c _ -> IR.Operation "MAX" [c.expression]
        Minimum c _ -> IR.Operation "MIN" [c.expression]
        Shortest c _ -> IR.Operation "SHORTEST" [c.expression]
        Longest c _ -> IR.Operation "LONGEST" [c.expression]
        Standard_Deviation c _ population -> case population of
            True -> IR.Operation "STDDEV_POP" [c.expression]
            False -> IR.Operation "STDDEV_SAMP" [c.expression]
        Concatenate c _ separator prefix suffix quote_char ->
            base_args = [c.expression, IR.Constant Sql_Type.text separator, IR.Constant Sql_Type.text prefix, IR.Constant Sql_Type.text suffix]
            case quote_char.is_empty of
                True -> IR.Operation "CONCAT" base_args
                False -> IR.Operation "CONCAT_QUOTE_IF_NEEDED" base_args+[IR.Constant Sql_Type.text quote_char]
        Sum c _ -> IR.Operation "SUM" [c.expression]
        Average c _ -> IR.Operation "AVG" [c.expression]
        Median c _ -> IR.Operation "MEDIAN" [c.expression]

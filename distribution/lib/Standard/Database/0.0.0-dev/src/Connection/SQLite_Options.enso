from Standard.Base import all

import project.Connection.Connection_Options
import project.Internal.SQLite.SQLite_Connection

from project.Connection.SQLite_Options.SQLite_Options import SQLite
from project.Connection.SQLite_Options.SQLite_Options export SQLite

type SQLite_Options
    ## Connect to a SQLite DB File or InMemory DB.

       Arguments:
       - location: Location of the SQLite database to connect to.
    SQLite (location:(In_Memory|File|Text))

    ## Build the Connection resource.

       Arguments:
       - options: Overrides for the connection properties.
    connect : Connection_Options
    connect self options =
        properties = options.merge self.jdbc_properties
        SQLite_Connection.create self.jdbc_url properties

    ## Provides the jdbc url for the connection.
    jdbc_url : Text
    jdbc_url self = case self.location of
        In_Memory -> "jdbc:sqlite::memory:"
        _ -> "jdbc:sqlite:" + ((File.new self.location).absolute.path.replace '\\' '/')

    ## Provides the properties for the connection.
    jdbc_properties : Vector
    jdbc_properties self = []

## Connect to an in-memory SQLite database.
type In_Memory

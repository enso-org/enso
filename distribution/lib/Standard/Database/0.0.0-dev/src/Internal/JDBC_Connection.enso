from Standard.Base import all
import Standard.Base.Errors.Illegal_Argument.Illegal_Argument
import Standard.Base.Errors.Illegal_State.Illegal_State
import Standard.Base.Errors.Unimplemented.Unimplemented
import Standard.Base.Runtime.Managed_Resource.Managed_Resource

import Standard.Table.Data.Table.Table as Materialized_Table
import Standard.Table.Data.Type.Value_Type.Value_Type

import project.Data.SQL_Statement.SQL_Statement
import project.Data.SQL_Type.SQL_Type
import project.Data.Table.Table as Database_Table
import project.Internal.Statement_Setter.Statement_Setter
import project.Internal.In_Transaction.In_Transaction

from project.Errors import SQL_Error, SQL_Timeout

polyglot java import java.util.Properties

polyglot java import java.sql.Connection
polyglot java import java.sql.DatabaseMetaData
polyglot java import java.sql.PreparedStatement
polyglot java import java.sql.ResultSet
polyglot java import java.sql.SQLException
polyglot java import java.sql.SQLTimeoutException

polyglot java import org.enso.database.JDBCProxy

type JDBC_Connection
    ## PRIVATE
    Value connection_resource

    ## PRIVATE
       Closes the connection releasing the underlying database resources
       immediately instead of waiting for them to be automatically released.

       The connection is not usable afterwards.
    close : Nothing
    close self =
        self.connection_resource . finalize

    ## PRIVATE

       Open the connection to the database, then run the action wrapping any
       SQL errors.
    with_connection : (Connection -> Any) -> Any
    with_connection self ~action =
        handle_sql_errors <|
            self.connection_resource.with action

    ## PRIVATE

       Open the connection to the database, then run the action passing the
       database's metadata wrapping any SQL errors.
    with_metadata : (DatabaseMetaData -> Any) -> Any
    with_metadata self ~action =
        handle_sql_errors <|
            self.connection_resource.with connection->
                metadata = connection.getMetaData
                action metadata

    ## PRIVATE

       Runs the provided action with a prepared statement, adding contextual
       information to any thrown SQL errors.
    with_prepared_statement : Text  | SQL_Statement -> Statement_Setter -> (PreparedStatement -> Any) -> Any
    with_prepared_statement self query statement_setter action =
        prepare template values = self.connection_resource.with java_connection->
            stmt = java_connection.prepareStatement template
            handle_illegal_state caught_panic =
                Error.throw (Illegal_Argument.Error caught_panic.payload.message)
            handle_any caught_panic =
                stmt.close
                Panic.throw caught_panic
            result = Panic.catch Illegal_State handler=handle_illegal_state <|
                Panic.catch Any handler=handle_any <|
                    set_statement_values stmt statement_setter values
            result.if_not_error <|
                stmt

        go template values =
            handle_sql_errors related_query=template <|
                Managed_Resource.bracket (prepare template values) .close action

        case query of
            _ : Text -> go query []
            SQL_Statement.Value _ ->
                compiled = query.prepare
                go compiled.first compiled.second

    ## PRIVATE
       Given a prepared statement, gets the column names and types for the
       result set.
    raw_fetch_columns : Text | SQL_Statement -> Boolean -> Statement_Setter -> Any
    raw_fetch_columns self statement execute_query statement_setter =
        self.with_prepared_statement statement statement_setter stmt->
            metadata = if execute_query then stmt.executeQuery.getMetaData else
                stmt.getMetaData

            resolve_column ix =
                name = metadata.getColumnLabel ix+1
                sql_type = SQL_Type.from_metadata metadata ix+1
                [name, sql_type]

            Vector.new metadata.getColumnCount resolve_column

    ## PRIVATE
       Checks that the query has no holes, and if it does, throws an error.
    ensure_query_has_no_holes : Text -> Nothing ! Illegal_Argument
    ensure_query_has_no_holes self raw_sql =
        self.with_prepared_statement raw_sql Statement_Setter.null stmt->
            ## We cannot run this check on every query, because in some
              backends (e.g. Postgres) running `getParameterMetaData`
              seems to trigger logic for figuring out types of the holes.
              In some of our generated queries, the driver is unable to
              figure out the types and fails with an exception.
            expected_parameter_count = stmt.getParameterMetaData.getParameterCount
            if expected_parameter_count != 0 then
                Error.throw <| Illegal_Argument.Error 'The provided raw SQL query should not contain any holes ("?").'

    ## PRIVATE
       Runs the following action with auto-commit turned off on this connection
       and returns the result.

       Afterwards, the auto-commit setting is reverted to the state before
       running this function (so if it was off before, this method may not
       change anything).
    run_without_autocommit : Any -> Any
    run_without_autocommit self ~action =
        self.with_connection java_connection->
            default_autocommit = java_connection.getAutoCommit
            Managed_Resource.bracket (java_connection.setAutoCommit False) (_ -> java_connection.setAutoCommit default_autocommit) _->
                action

    ## PRIVATE
       Performs the given action within a transaction.
       Once the action is completed, the transaction is committed.
       If a panic or dataflow error escapes from the action, the transaction is
       rolled-back and closed.
       If the rollback fails and panics, the panic related to the rollback will
       take precedence over the original panic that caused that rollback.
    run_within_transaction : Any -> Any
    run_within_transaction self ~action =
        if In_Transaction.is_in_transaction then
            Panic.throw (Illegal_State.Error "`run_within_transaction` is executed within an existing transaction. Nesting transactions is not allowed as its semantics are unclear.")
        self.run_without_autocommit <|
            self.with_connection java_connection->
                handle_panic caught_panic =
                    java_connection.rollback
                    Panic.throw caught_panic
                result = Panic.catch Any handler=handle_panic <|
                    In_Transaction.mark_running_in_transaction action
                case result.is_error of
                    True -> java_connection.rollback
                    False -> java_connection.commit
                result

    ## PRIVATE
       Insert rows from an in-memory table using a prepared query template in
       batches for efficiency.

       It is the caller's responsibility to call this method from within a
       transaction to ensure consistency.
    batch_insert : Text -> Statement_Setter -> Materialized_Table -> Integer -> Nothing
    batch_insert self insert_template statement_setter table batch_size =
        self.with_connection java_connection->
            Managed_Resource.bracket (java_connection.prepareStatement insert_template) .close stmt->
                num_rows = table.row_count
                columns = table.columns
                check_rows updates_array expected_size =
                    updates = Vector.from_polyglot_array updates_array
                    if updates.length != expected_size then Panic.throw <| Illegal_State.Error "The batch update unexpectedly affected "+updates.length.to_text+" rows instead of "+expected_size.to_text+"." else
                        updates.each affected_rows->
                            if affected_rows != 1 then
                                Panic.throw <| Illegal_State.Error "A single update within the batch unexpectedly affected "+affected_rows.to_text+" rows."
                0.up_to num_rows . each row_id->
                    values = columns.map col-> col.at row_id
                    set_statement_values stmt statement_setter values
                    stmt.addBatch
                    if (row_id+1 % batch_size) == 0 then check_rows stmt.executeBatch batch_size
                if (num_rows % batch_size) != 0 then check_rows stmt.executeBatch (num_rows % batch_size)
            java_connection.commit

## PRIVATE

   Creates a JDBC connection based on a URL and optionally username and
   password.

   Arguments:
   - url: The URL to connect to.
   - properties: A vector of properties for the connection.
create : Text -> Vector -> JDBC_Connection
create url properties = handle_sql_errors <|
    java_props = Properties.new
    properties.each pair->
        case pair.second of
            Nothing -> Polyglot.invoke java_props "remove" [pair.first]
            _ -> Polyglot.invoke java_props "setProperty" [pair.first, pair.second]
    java_connection = JDBCProxy.getConnection url java_props

    resource = Managed_Resource.register java_connection close_connection
    JDBC_Connection.Value resource

## PRIVATE

   This cannot be a closure due to limitations of Managed_Resource.

   Arguments:
   - connection: The connection to close.
close_connection : Connection -> Nothing
close_connection connection =
    connection.close

## PRIVATE

   Executes `action` and returns its result, catching any panics and if they are
   coming from JDBC, wraps them with our own error types and returns as regular
   data-flow errors.

   Arguments:
   - action: The computation to execute. This computation may throw SQL errors.
handle_sql_errors : Any -> (Text | Nothing) -> Any ! (SQL_Error | SQL_Timeout)
handle_sql_errors ~action related_query=Nothing =
    Panic.catch SQLException action caught_panic->
        case caught_panic.payload of
            exc : SQLTimeoutException -> Error.throw (SQL_Timeout.Error exc related_query)
            exc -> Error.throw (SQL_Error.Error exc related_query)

## PRIVATE
   Uses the provided `Statement_Setter` strategy to fill holes in a
   provided `PreparedStatement`.
set_statement_values stmt statement_setter values =
    values.each_with_index ix-> value->
        statement_setter.fill_hole stmt (ix + 1) value

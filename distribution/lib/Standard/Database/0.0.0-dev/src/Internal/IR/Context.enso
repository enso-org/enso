from Standard.Base import all
import Standard.Base.Data.Vector.No_Wrap

import project.Internal.IR.From_Spec.From_Spec
import project.Internal.IR.Internal_Column.Internal_Column
import project.Internal.IR.Order_Descriptor.Order_Descriptor
import project.Internal.IR.SQL_Expression.SQL_Expression
import project.SQL.SQL_Builder
import project.SQL_Statement.SQL_Statement

## PRIVATE

   A context associated with an SQL query.
type Context
    ## PRIVATE

       Creates a query context that just fetches data from a table, without any
       additional processing.

       Arguments:
       - table_name: The name of the table for which the context is being created.
       - alias: An alias name to use for table within the query.
       - internal_temporary_keep_alive_reference: See `From_Spec.Table` for more
         details.
    for_table : Text -> Text -> Any -> Context
    for_table table_name:Text alias:Text=table_name internal_temporary_keep_alive_reference=Nothing =
        Context.Value (From_Spec.Table table_name alias internal_temporary_keep_alive_reference=internal_temporary_keep_alive_reference) [] [] [] Nothing []

    ## PRIVATE

       Creates a query context that runs a raw SQL query.

       Arguments:
       - raw_sql: The SQL statement (either as prepared statement or text).
       - alias: An alias name to use for table within the query.
    for_query (raw_sql:Text|SQL_Statement) (alias:Text) -> Context =
        Context.Value (From_Spec.Query raw_sql alias) [] [] [] Nothing []

    ## PRIVATE

       Creates a Context from a Sub_Query.

       Arguments:
       - subquery: The subquery to lift into a context.
    for_subquery subquery:From_Spec -> Context =
        Context.Value subquery [] [] [] Nothing []

    ## PRIVATE

       A context associated with an SQL query.

       The expressions can only be computed in a context which specifies from where
       their columns come and set filters and other settings for processing the
       query.

       Arguments:
       - from_spec: the sources for the query, see `From_Spec` for more
         details.
       - where_filters: a list of expressions for filtering - only the rows
         for which these expressions evaluate to true are included in the
         result.
       - orders: a list of ordering expressions, for each entry an ORDER BY
         clause is added.
       - groups: a list of grouping expressions, for each entry a GROUP BY is
         added, the resulting query can then directly include only the
         grouped-by columns or aggregate expressions.
       - limit: an optional maximum number of elements that the query should
         return.
    Value (from_spec : From_Spec) (where_filters : Vector SQL_Expression) (orders : Vector Order_Descriptor) (groups : Vector SQL_Expression) (limit : Nothing | Integer) (extensions : Vector Context_Extension)

    ## PRIVATE

       Returns a copy of the context with changed `where_filters`.

       Arguments:
       - new_filters: The new filters to set in the query.
    set_where_filters : Vector SQL_Expression -> Context
    set_where_filters self new_filters =
        Context.Value self.from_spec new_filters self.orders self.groups self.limit self.extensions

    ## PRIVATE

       Returns a copy of the context with added `where_filters`.

       Arguments:
       - new_filters: The new filters to add to the existing filters in the
         query.
    add_where_filters : Vector SQL_Expression -> Context
    add_where_filters self new_filters =
        Context.Value self.from_spec (self.where_filters+new_filters) self.orders self.groups self.limit self.extensions

    ## PRIVATE

       Returns a copy of the context with changed `orders`.

       Arguments:
       - new_orders: The new ordering clauses to set in the query.
    set_orders : Vector Order_Descriptor -> Context
    set_orders self new_orders =
        Context.Value self.from_spec self.where_filters new_orders self.groups self.limit self.extensions

    ## PRIVATE

       Returns a copy of the context with added `orders`.

       The orderings are modified in such a way that the newly added ordering
       takes precedence, but if any orderings were already present they are also
       taken into account to break ties within the new ordering.

       Arguments:
       - new_orders: The new ordering clauses to add to the query.
    add_orders : Vector Order_Descriptor -> Context
    add_orders self new_orders =
        deduped_orders = new_orders+self.orders . distinct .expression
        Context.Value self.from_spec self.where_filters deduped_orders self.groups self.limit self.extensions

    ## PRIVATE

       Returns a copy of the context with changed `groups`.

       Arguments:
       - new_groups: The new grouping clauses to set in the query.
    set_groups : Vector SQL_Expression -> Context
    set_groups self new_groups =
        Context.Value self.from_spec self.where_filters self.orders new_groups self.limit self.extensions

    ## PRIVATE

       Returns a copy of the context with changed `limit`.

       Arguments:
       - new_limit: The new limit clauses to set in the query.
    set_limit : (Nothing | Integer) -> Context
    set_limit self new_limit =
       Context.Value self.from_spec self.where_filters self.orders self.groups new_limit self.extensions

    ## PRIVATE
       Returns a copy of the context with an added extension.
    add_extension : Context_Extension -> Context
    add_extension self extension =
       Context.Value self.from_spec self.where_filters self.orders self.groups self.limit (self.extensions + [extension])

    ## PRIVATE

       'Lifts' this context into a subquery, so that the original context (with all filters etc.) is
       encapsulated within the subquery and all external references passed as the second argument,
       refer directly to that subquery.

       It takes a list of lists of columns that should be included in that subquery (this can for
       example the list of regular columns, the list of indices etc.)
       It assumes that columns on these lists all have unique names.

       It returns a new context and the lists transformed in such a way that each column corresponds
       to one from the original list but it is valid in the new context.

       This is useful as a preprocessing step between combining queries, for example in a join.
    as_subquery : Text -> Vector (Vector Internal_Column) -> Subquery_Setup
    as_subquery self alias column_lists =
        rewrite_internal_column : Internal_Column -> Internal_Column
        rewrite_internal_column column =
            Internal_Column.Value column.name column.sql_type_reference (SQL_Expression.Column alias column.name)

        new_columns = column_lists.map on_problems=No_Wrap columns->
            columns.map on_problems=No_Wrap rewrite_internal_column

        encapsulated_columns = column_lists.flat_map columns->
            columns.map column-> [column.name, column.expression]
        new_from = From_Spec.Sub_Query encapsulated_columns self alias

        Subquery_Setup.Value new_from new_columns rewrite_internal_column

    ## PRIVATE
       A bottom-up, depth-first traversal of this IR node and its children. Each
       node is passed to the provided function, and the return value of the
       function replaces this node in the tree.
    traverse : (Any -> Any) -> Context
    traverse self f -> Context =
        rec e = e.traverse f
        new_context = case self of
            Context.Value from_spec where_filters orders groups limit extensions ->
                Context.Value (from_spec.traverse f) (where_filters.map rec) (orders.map rec) (groups.map rec) limit (extensions.map rec)
        f new_context

## PRIVATE
type Subquery_Setup
    ## PRIVATE
       Arguments:
       - subquery: The From_Spec for the created subquery.
       - new_columns: All columns that are available in the subquery, remapped
         to be accessible from a context that uses `subquery` as its from spec.
       - remap_column: A function that remaps an internal column to be accessible
         from a context that uses `subquery` as its from spec.
         The behaviour is undefined if this method is called with a column that
         was not part of the subquery.
    Value (subquery : From_Spec) (new_columns : Vector (Vector Internal_Column)) (remap_column : Internal_Column -> Internal_Column)

## PRIVATE
   Describes an extension to a Context that can be used to add additional custom
   SQL code as part of the query.
type Context_Extension
    ## A recipe for building the extension.

       Arguments:
       - position: Determines where the extension code should be inserted.
         The positions of common query parts are following:
         - 100 - the SELECT keyword
         - 200 - the column descriptions
         - 300 - the FROM part
         - 400 - the WHERE part
         - 500 - the GROUP BY part
         - 600 - the ORDER BY part
         - 700 - the LIMIT part
         Setting the position to a value between these will result in it being
         sorted in the desired place.
       - expressions: Sub-expressions needed for the part. They will be
         generated and the results of that will be passed to `run_generator`.
       - run_generator: A function that takes the generated expressions and
         returns the SQL code that will be inserted at the desired position.
    Value (position : Integer) (expressions : Vector SQL_Expression) (run_generator : Vector SQL_Builder -> SQL_Builder)

    ## PRIVATE
       A bottom-up, depth-first traversal of this IR node and its children. Each
       node is passed to the provided function, and the return value of the
       function replaces this node in the tree.
    traverse : (Any -> Any) -> Context_Extension
    traverse self f -> Context_Extension =
        rec e = e.traverse f
        new_context_extension = case self of
            Context_Extension.Value position expressions run_generator ->
                Context_Extension.Value position (expressions.map rec) run_generator
        f new_context_extension

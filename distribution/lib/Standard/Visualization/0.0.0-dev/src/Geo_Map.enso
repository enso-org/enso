from Standard.Base import all

from Standard.Table import Table

import project.Helpers

## PRIVATE

   Construct JSON describing table geo map visualization.

   Arguments:
   - table: the Table to be visualized.
json_from_table : Table -> JS_Object
json_from_table table =
    names = ['label', 'latitude', 'longitude', 'radius', 'color']
    pairs = names.map <| name->
        column = table.lookup_ignore_case name
        column.when_valid ["df_" + name, column.to_vector] . catch Nothing

    JS_Object.from_pairs <| pairs.filter (x -> x.is_nothing.not)

## PRIVATE

   Default preprocessor for the geo map visualization, generating JSON text
   describing the geo map visualization.

   Arguments:
   - value: the value to be visualized.
process_to_json_text : Any -> Text
process_to_json_text value = case value of
    _ : Table -> json_from_table value . to_text
    _ -> value.to_json

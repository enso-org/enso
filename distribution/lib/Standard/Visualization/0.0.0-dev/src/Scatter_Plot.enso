from Standard.Base import all
import Standard.Base.Data.Vector.Builder
from Standard.Table import Column, Table, Value_Type

import project.Helpers

## PRIVATE

   Name of the index column that may be generated to plot against.
index_name : Text
index_name = 'index'

## PRIVATE
data_field : Text
data_field = 'data'

## PRIVATE
axis_field : Text
axis_field = 'axis'

## PRIVATE
label_field : Text
label_field = 'label'

## PRIVATE

   Represents a recognized point data field for a scatter plot visualization.
type Point_Data

    ## PRIVATE
    X

    ## PRIVATE
    Y

    ## PRIVATE
    Y_multi number:Integer=0

    ## PRIVATE
    Color

    ## PRIVATE
    Shape

    ## PRIVATE
    Label

    ## PRIVATE
    Size

    ## PRIVATE
    Row_Number

    ## PRIVATE

       Returns all recognized point data fields.
    all_fields : Vector
    all_fields = [Point_Data.X, Point_Data.Y, Point_Data.Color, Point_Data.Shape, Point_Data.Label, Point_Data.Size, Point_Data.Row_Number]

    ## PRIVATE
    recognized_names : Vector
    recognized_names = Point_Data.all_fields.map _.name

    ## PRIVATE
    is_recognized : Column -> Boolean
    is_recognized self column = Point_Data.recognized_names.contains column.name

    ## PRIVATE
    name : Text
    name self = self.to_text.to_case Case.Lower

    ## PRIVATE
    fallback_column : Table -> Column ! No_Fallback_Column
    fallback_column self table = case self of
        Point_Data.X ->
            candidate = table.columns.first
            is_good c = (c.is_numeric || c.value_type.is_date_or_time) && c.name != Point_Data.Y.name
            if is_good candidate then candidate else Point_Data.iota table.row_count
        Point_Data.Y ->
            x_column = Point_Data.X.lookup_in table
            candidates = table.columns
            is_good_enough c = c.is_numeric && c.name != x_column.name
            is_good c = is_good_enough c && (self.is_recognized c).not

            candidates.find if_missing=(Error.throw Nothing) is_good . catch_ <| candidates.find is_good_enough
        Point_Data.Y_multi _ ->
            x_column = Point_Data.X.lookup_in table
            y_column = Point_Data.Y.lookup_in table
            check_for_size c = c.name.equals_ignore_case Point_Data.Size.name != True
            candidates_filtered = table.columns.filter (c-> c.is_numeric && c.name != x_column.name && c.name != y_column.name && check_for_size c)
            candidates = candidates_filtered.drop self.number
            is_good c = (self.is_recognized c).not

            candidates.find if_missing=candidates.first is_good
        Point_Data.Row_Number ->
            Point_Data.iota table.row_count
            
        _ -> Error.throw No_Fallback_Column

    ## PRIVATE

       Returns a vector of subsequent integers beginning from 0.
    iota : Number -> Vector
    iota count =
        range = 0.up_to count
        Column.from_vector index_name range.to_vector

    ## PRIVATE
    lookup_in : Table -> Column
    lookup_in self table =
        named = table.lookup_ignore_case self.name
        named.catch_ <| self.fallback_column table

## PRIVATE
type No_Fallback_Column
    ## PRIVATE
    to_display_text : Text
    to_display_text self =
        "No fallback column found for the scatter plot."

## PRIVATE

   Generates JSON that describes points data.
Table.point_data : Vector -> Vector
Table.point_data self all_fields =
    get_point_data field = field.lookup_in self . rename field.name . catch Any (_->Nothing)
    is_not_nothing x = case x of
        Nothing -> False
        _ -> True
    columns = all_fields.map get_point_data . filter is_not_nothing
    (0.up_to self.row_count).to_vector.map <| row_n->
        pairs = columns.map column->
            value = column.at row_n . catch_ Nothing
            [column.name, value]
        JS_Object.from_pairs pairs

## PRIVATE

   Returns the number of numeric columns for the plot.
Table.numeric_column_count : Number
Table.numeric_column_count self =
    check_for_size c = c.name.equals_ignore_case Point_Data.Size.name != True
    columns = self.columns . filter (c-> c.is_numeric && check_for_size c)
    columns.length

## PRIVATE

   Generates JSON that describes plot axes.
Table.axes : Vector -> JS_Object
Table.axes self all_fields =
    describe_axis field =
        col_name = field.lookup_in self . name
        label = JS_Object.from_pairs [[label_field, col_name]]
        [field.name, label]
    axis = all_fields.map describe_axis
    is_valid axis_pair =
        label = axis_pair.at 1
        label.is_valid && (self.columns.length > 0)
    axes_obj = JS_Object.from_pairs <| axis.filter is_valid
    if axes_obj.length > 0 then axes_obj else Nothing

## PRIVATE
Vector.point_data : Vector
Vector.point_data self  =
    self.map_with_index i-> elem-> JS_Object.from_pairs [[Point_Data.X.name, i], [Point_Data.Y.name, elem], [Point_Data.Row_Number.name, i]]

## PRIVATE
bound_data bounds data = case bounds of
    Nothing -> data
    _ ->
        min_x = bounds.at 0
        min_y = bounds.at 1
        max_x = bounds.at 2
        max_y = bounds.at 3

        data.filter <| datum->
            x = datum.get "x"
            y = datum.get "y"

            min_x<=x && x<=max_x && min_y<=y && y<=max_y

## Represents the extreme range of points for a data set.
type Extreme
    ## Create an extreme range of points.
    Value min_x max_x min_y max_y

## PRIVATE
limit_data limit data = case limit of
    Nothing -> data
    _ -> if ((data.length <= limit) || (data.length == 0)) then data else
        x datum = datum.get "x"
        y datum = datum.get "y"

        update_extreme current idx point =
            new_min_x = if x current.min_x.second > x point then [idx, point] else current.min_x
            new_min_y = if y current.min_y.second > y point then [idx, point] else current.min_y
            new_max_x = if x current.max_x.second < x point then [idx, point] else current.max_x
            new_max_y = if y current.max_y.second < y point then [idx, point] else current.max_y
            Extreme.Value new_min_x new_max_x new_min_y new_max_y

        first = [0, data.first]
        bounds = case data.fold_with_index (Extreme.Value first first first first) update_extreme of
            Extreme.Value min_x max_x min_y max_y ->  [min_x, max_x, min_y, max_y]
            _ -> []
        extreme = Dictionary.from_vector bounds error_on_duplicates=False . values

        if limit <= extreme.length then extreme.take (..First limit) else
            extreme + data.take (..Sample (limit - extreme.length))


## PRIVATE
get_axes_field : Integer -> Vector
get_axes_field number_of_numeric =
    fields_for_multiseries = ((0.up_to number_of_numeric).map idx-> Point_Data.Y_multi idx)
    fields_for_multiseries . insert item=Point_Data.X . insert item=Point_Data.Y

## PRIVATE

   Returns whether the x axis should be time/date. This is either a column named 'X' or the left most column
get_x_value_type : Table -> Value_Type
get_x_value_type table =
    get_x_or_first t =
        x_col = t.lookup_ignore_case Point_Data.X.name
        x_col.catch_ <| t.columns.first
    x_col = get_x_or_first table
    x_col.value_type

## PRIVATE
json_from_table : Table -> Vector Integer | Nothing -> Integer | Nothing -> Text
json_from_table table bounds limit =
    x_value_type = get_x_value_type table
    x_is_date_or_time = x_value_type.is_date_or_time
    number_of_numeric_cols = if x_is_date_or_time then table.numeric_column_count-1 else table.numeric_column_count-2
    fields_for_multiseries = Point_Data.all_fields + ((0.up_to number_of_numeric_cols).map idx-> Point_Data.Y_multi idx)
    fields_for_plot = if number_of_numeric_cols > 0 then fields_for_multiseries else Point_Data.all_fields
    data = table.point_data fields_for_plot |> bound_data bounds |> limit_data limit
    fields_for_axes = get_axes_field number_of_numeric_cols
    axes = table.axes fields_for_axes
    is_multi_series = number_of_numeric_cols > 0
    error_message = if axes.length < 2 then "Two numeric columns required for Scatterplot" else Nothing
    JS_Object.from_pairs [[data_field, data], [axis_field, axes], ["is_multi_series", is_multi_series], ["get_row_method", "get_row"], ["x_value_type", x_value_type.to_name], ["error_message", error_message]] . to_json

## PRIVATE
json_from_vector : Vector Any -> Vector Integer | Nothing -> Integer | Nothing -> Text
json_from_vector vec bounds limit =
    data = vec.point_data |> bound_data bounds |> limit_data limit
    JS_Object.from_pairs [[data_field, data], [axis_field, Nothing], ["is_multi_series", False], ["get_row_method", "at"], ["x_value_type", Value_Type.Integer.to_name], ["error_message", Nothing]] . to_json

## PRIVATE

   Default preprocessor for the scatter-plot visualization.

   Generates JSON text describing the scatter-plot visualization.

   Arguments:
   - value: the value to be visualized.
process_to_json_text : Any -> Integer | Nothing -> Integer | Nothing -> Text
process_to_json_text value bounds=Nothing limit=Nothing =
    json = case value of
        _ : Table  -> json_from_table  value bounds limit
        _ : Vector -> json_from_vector value bounds limit
        _ : Column -> json_from_table  value.to_table bounds limit
        _          -> json_from_vector value.to_vector bounds limit

    json

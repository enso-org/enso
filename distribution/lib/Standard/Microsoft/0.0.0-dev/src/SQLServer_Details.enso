from Standard.Base import all
import Standard.Base.Data.Numbers.Number_Parse_Error
import Standard.Base.Errors.Illegal_State.Illegal_State

import Standard.Database.Connection.Connection_Options.Connection_Options
import Standard.Database.Connection.Credentials.Credentials

import project.SQLServer_Connection.SQLServer_Connection

type SQLServer_Details
    ## Connect to a SQLServer database.

       Arguments:
       - host: The name of the server to connect to.
       - port: The port to connect to.
       - credentials: The credentials to use for the connection.
       - database: The name of the database to connect to.
    SQLServer host:Text port:Text credentials:Credentials database:Text="master"

    ## PRIVATE
       Build the Connection resource.

       Arguments:
       - options: Overrides for the connection properties.
    connect : Connection_Options -> SQLServer_Connection
    connect self options =
        properties = options.merge self.jdbc_properties
        make_new database =
            SQLServer_Details.SQLServer self.host self.port self.credentials (database.if_nothing self.database) . connect options

        SQLServer_Connection.create self.jdbc_url properties make_new

    ## PRIVATE
       Provides the jdbc url for the connection.
    jdbc_url : Text
    jdbc_url self =
        'jdbc:sqlserver://' + self.host + ':' + self.port

    ## PRIVATE
       Provides the properties for the connection.
    jdbc_properties : Vector (Pair Text Text)
    jdbc_properties self =
        account = [Pair.new 'encrypt' 'false']
        database = [Pair.new 'databaseName' self.database]
        credentials = [Pair.new 'user' self.credentials.username, Pair.new 'password' self.credentials.password]
        account + database + credentials

from Standard.Base import all

import Standard.Database.Connection.Client_Certificate.Client_Certificate
import Standard.Database.Connection.Connection.Connection
import Standard.Database.Connection.Connection_Options.Connection_Options
import Standard.Database.Connection.Credentials.Credentials
import Standard.Database.Connection.SSL_Mode.SSL_Mode
import Standard.Database.Internal.JDBC_Connection
import Standard.Database.Internal.Postgres.Pgpass
import Standard.Database.Internal.Postgres.Postgres_Entity_Naming_Properties

import project.AWS_Credential.AWS_Credential
import project.Database.Redshift.Internal.Redshift_Dialect

polyglot java import com.amazon.redshift.jdbc.Driver
polyglot java import java.util.Properties

type Redshift_Details
    ## Connect to a AWS Redshift database.

       Arguments:
       - host: The hostname of the database server (defaults to localhost).
       - port: The port of the database server (defaults to 5432).
       - schema: The schema to connect to (if not provided or empty, the default schema will be used).
       - db_user: The user to connect to the database as.
       - credentials: The credentials to use for the connection (defaults AWS_Profile).
       - use_ssl: Whether to use SSL (defaults to `SSL_Mode.Require`).
       - client_cert: The client certificate to use or `Nothing` if not needed.
    Redshift (host:Text) (port:Integer=5439) (schema:Text='') (db_user:Text='') (credentials:Credentials|AWS_Credential=AWS_Credential.Profile) (use_ssl:SSL_Mode=SSL_Mode.Require) (client_cert:Client_Certificate|Nothing=Nothing)

    ## PRIVATE
       Build the Connection resource.

       Arguments:
       - options: Overrides for the connection properties.
    connect : Connection_Options -> Connection
    connect self options =
        if Driver.isRegistered.not then Driver.register

        properties = options.merge self.jdbc_properties
        java_props = Properties.new
        properties.each pair->
            java_props.setProperty pair.first pair.second

        jdbc_connection = JDBC_Connection.create self.jdbc_url properties

        # TODO [RW] can we inherit these from postgres?
        entity_naming_properties = Postgres_Entity_Naming_Properties.new jdbc_connection

        Connection.new jdbc_connection Redshift_Dialect.redshift entity_naming_properties

    ## PRIVATE
       Provides the jdbc url for the connection.
    jdbc_url : Text
    jdbc_url self =
        prefix = case self.credentials of
            _ : AWS_Credential -> 'jdbc:redshift:iam://'
            _ -> 'jdbc:redshift://'
        prefix + self.host + ':' + self.port.to_text + '/' + self.schema

    ## PRIVATE
       Provides the properties for the connection.
    jdbc_properties : Vector (Pair Text Text)
    jdbc_properties self =
        credentials = case self.credentials of
            Nothing -> Pgpass.read self.host self.port self.schema
            AWS_Credential.Profile profile ->
                [Pair.new 'user' self.db_user] + (if profile == '' then [] else [Pair.new 'profile' profile])
            AWS_Credential.Key access_key_id secret_access_key ->
                [Pair.new 'user' self.db_user, Pair.new 'AccessKeyID' access_key_id, Pair.new 'SecretAccessKey' secret_access_key]
            Credentials.Username_And_Password username password ->
                [Pair.new 'user' username, Pair.new 'password' password]

        ## Disabled as Redshift SSL settings are different to PostgreSQL.
        ssl_properties = []

        cert_properties = if self.client_cert.is_nothing then [] else
            self.client_cert.properties

        credentials + ssl_properties + cert_properties

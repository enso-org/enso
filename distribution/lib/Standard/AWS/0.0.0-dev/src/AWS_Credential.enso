from Standard.Base import all
from Standard.Base.Metadata import make_single_choice, Widget

polyglot java import org.enso.aws.ProfileReader

type AWS_Credential
    ## Access using IAM via an AWS profile.

       Arguments:
       - profile: AWS profile name (if empty uses default).
    @profile (make_single_choice AWS_Credential.profile_names)
    Profile profile:Text='default'

    ## Access using IAM via an AWS access key ID and secret access key.

       Arguments:
       - access_key_id: AWS access key ID.
       - secret_access_key: AWS secret access key.
    Key access_key_id:Text secret_access_key:Text

    ## Get a list of the available profiles
    profile_names : Vector Text
    profile_names = Vector.from_polyglot_array <|
        ProfileReader.INSTANCE.getProfiles

    ## PRIVATE
    default_widget : Widget
    default_widget =
        fqn = Meta.get_qualified_type_name AWS_Credential
        make_single_choice [["default", "Nothing"], ["by profile", fqn + ".Profile"], ["by key", fqn + ".Key"]]

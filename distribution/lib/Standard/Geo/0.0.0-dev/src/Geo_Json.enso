from Standard.Base import all

## PRIVATE

   A type of GeoJSON object.
type Object_Type

    ## PRIVATE

       A Geo JSON feature.
    Feature

    ## PRIVATE

       A Geo JSON feature collection.
    Feature_Collection

    ## PRIVATE

       Convert GeoJSON object type to Text.
    to_text : Text
    to_text self = case self of
        Object_Type.Feature -> "Feature"
        Object_Type.Feature_Collection -> "FeatureCollection"

## PRIVATE

   Get the type field of a GeoJSON object.
Json.Json.get_type : Any
Json.Json.get_type self = case self of
    Json.Object object ->
        object.get_or_else "type" Nothing.to_json . unwrap

## PRIVATE

   Get key-value pairs of a Feature GeoJSON object.
Json.Json.get_feature_row : Map
Json.Json.get_feature_row self =
    properties_row = self.get "properties" . get_properties_row
    geometry_row = self.get "geometry" . get_geometry_row
    geometry_row.fold_with_key properties_row acc-> k-> v->
        acc.insert k v

## PRIVATE

   Get column key-value pairs of a feature's "properties" object.
Json.Json.get_properties_row : Map
Json.Json.get_properties_row self = case self of
    Json.Object properties -> properties.map p-> case p of
        Json.Object _ -> Nothing.to_json
        _ -> p

## PRIVATE

   Get column key-value pairs of a feature's "geometry" object.
Json.Json.get_geometry_row : Map
Json.Json.get_geometry_row self = case self of
    Json.Object fields ->
          geometry_type = fields.get_or_else "type" Nothing
          if geometry_type == "Point".to_json then self.get_point_row else Map.empty

## PRIVATE

   Get column key-value pairs of a "Point" geometry object.
Json.Json.get_point_row : Map
Json.Json.get_point_row self =
    fields = ["longitude", "latitude", "elevation"]
    case self.get "coordinates" of
        Json.Array coordinates ->
            Map.from_vector (fields.zip coordinates)
        _ -> Map.empty

import project.Data.Text.Text
import project.Data.Vector.Vector

# Helpers for creating different parts of the form.

## The HTTP form containing a vector of parts.
type Form
    ## PRIVATE
       ADVANCED
       Create a text field of a Form.

       Arguments:
       - key: The key for the field in the form.
       - val: The text for the textual field.

       > Example
         Create a textual form field.

             import Standard.Base.Network.HTTP.Form.Form

             example_text_field = Form.text_field "Foo" "bar"
    text_field : Text -> Text -> Part
    text_field key val = Part.Value key (Part_Value.Text val)

    ## PRIVATE
       ADVANCED
       Create a file field of a Form.

       Arguments:
       - key: The key for the field in the form.
       - file: The textual file contents.

       > Example
         Create a file form field.

             import Standard.Base.Network.HTTP.Form.Form

             example_text_field = Form.file_field "Foo" "My file contents"
    file_field : Text -> Text -> Part
    file_field key file = Part.Value key (Part_Value.File file)

    ## PRIVATE
       ADVANCED
       Create Form data from Parts.

       Arguments:
       - parts: A vector of parts to make up the form.

       > Example
         Create a new form

             import Standard.Base.Network.HTTP.Form.Form

             example_form_new = Form.new (Form.text_field "foo" "bar")
    new : Vector -> Form
    new parts = Form.Value parts

    ## PRIVATE

       A type representing form data.

       Arguments:
       - parts: A vector of form segments.
    Value parts

    ## Convert this to a Form.

       > Example
         Convert to a form.

             import Standard.Base.Network.HTTP.Form.Form
             import Standard.Base.Network.HTTP.Form.Part
             import Standard.Base.Network.HTTP.Form.Part_Value

             example_to_form = Form.new [Part "foo" (Part_Value.Text "bar")] . to_form
    to_form : Form
    to_form self = self

## The key-value element of the form.
type Part
    ## A form part.

       Arguments:
       - key: The key for the form section.
       - value: The value of the form section.
    Value key value

## The value of the form element.
type Part_Value
    ## A textual value for a form part.

       Arguments:
       - part_text: The text for the form part.
    Text part_text

    ## A file value for a form part.

       Arguments:
       - part_file: The file for the form part.
    File part_file

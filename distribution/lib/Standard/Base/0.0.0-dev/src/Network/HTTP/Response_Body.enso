import project.Data.Boolean.Boolean
import project.Data.Json.Json
import project.Data.Json.JS_Object
import project.Data.Numbers.Number
import project.Data.Text.Encoding.Encoding
from project.Data.Text.Extensions import all
import project.Data.Text.Text
import project.Data.Vector.Vector
import project.Nothing.Nothing
import project.System.File.File
import project.System.File.Write_Extensions

## PRIVATE
type Response_Body
    ## PRIVATE
       Response body

       Arguments:
       - bytes: The body of the response as binary data.
    Value bytes

    ## ALIAS Parse as Text
       Decodes the body to a Text value.
    @encoding Encoding.default_widget
    decode_as_text : Encoding -> Text
    decode_as_text self encoding=Encoding.utf_8 =
        Text.from_bytes self.bytes encoding

    ## ALIAS Parse as JSON, Parse JSON
       Decodes the body as JSON.

       > Example
         Convert a response from JSON.

             import Standard.Examples

             example_to_text = Examples.get_geo_data.decode_as_json
    @encoding Encoding.default_widget
    decode_as_json : Encoding -> JS_Object | Boolean | Number | Nothing | Text | Vector
    decode_as_json self encoding=Encoding.utf_8 =
        self.decode_as_text encoding . parse_json

    ## PRIVATE
       Convert response body to Text.
    to_text : Text
    to_text self = "Response_Body [" + self.bytes.length.to_text + " bytes]"

    ## Write response body to a File.

       Arguments:
       - file: The file to write the bytes to.

       > Examples
         Write the contents of the request body to a scratch file on disk. The
         file will be created if it does not exist, and will be overwritten if
         it does.

             import Standard.Examples

             example_to_file =
                Examples.get_geo_data.to_file Examples.scratch_file
    to_file : File -> File
    to_file self file =
        self.bytes.write_bytes file
        file

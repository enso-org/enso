from Standard.Base import all

import Standard.Base.Network.Http.Header
import Standard.Base.Network.Http.Response.Body as Response_Body
import Standard.Base.Network.Http.Status_Code

polyglot java import org.enso.base.Http_Utils

## TODO Dubious constructor export
from project.Network.Http.Response.Response import all
from project.Network.Http.Response.Response export all

type Response

    ## PRIVATE

       A type representing an HTTP response.

       Arguments:
       - internal_http_response: The internal represnetation of the HTTP
         response.
    Response_Data internal_http_response

    ## Get the response headers.

       > Example
         Getting the headers from a response. NOTE that this example will make a
         network request.

             import Standard.Examples

             example_headers = Examples.get_response.headers
    headers : Vector.Vector
    headers self =
        header_entries = Vector.from_polyglot_array (Http_Utils.get_headers self.internal_http_response.headers)
        header_entries.map e-> Header.new e.getKey e.getValue

    ## Get the response body.

       > Example
         Getting the body from a response. NOTE that this example will make a
         network request.

             import Standard.Examples

             example_body = Examples.get_response.body
    body : Response_Body
    body self = Response_Body.Body_Data (Vector.from_polyglot_array self.internal_http_response.body)

    ## Get the response status code.

       > Example
         Getting the code from a response. NOTE that this example will make a
         network request.

             import Standard.Examples

             example_code = Examples.get_response.code
    code : Status_Code
    code self = Status_Code.Status_Code_Data self.internal_http_response.statusCode

    ## Convert the response to JSON.

       > Example
         Convert a response to JSON. NOTE that this example will make a network
         request.

             import Standard.Examples

             example_to_json = Examples.get_response.to_json
    to_json : Json.Object
    to_json self = Json.from_pairs [["type", "Response"], ["headers", self.headers], ["body", self.body], ["code", self.code]]


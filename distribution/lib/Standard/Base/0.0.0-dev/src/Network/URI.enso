import project.Data.Enso_Cloud.Enso_Secret.Enso_Secret
import project.Data.Json.JS_Object
import project.Data.Pair.Pair
import project.Data.Text.Text
import project.Data.Vector.Vector
import project.Error.Error
import project.Errors.Common.Syntax_Error
import project.Network.URI_With_Query.URI_With_Query
import project.Nothing.Nothing
import project.Panic.Panic

polyglot java import java.lang.Exception
polyglot java import java.net.URI as Java_URI

type URI
    ## ALIAS get uri
       GROUP Conversions

       Parse a URI from text.

       Arguments:
       - uri: The text to parse as a URI.

       Throws a Syntax_Error when the text cannot be parsed as a URI.

       > Example
         Parse URI text.

             import Standard.Base.Network.URI.URI

             example_parse = URI.parse "http://example.com"
    parse : Text -> URI ! Syntax_Error
    parse uri:Text =
        Panic.catch Exception (URI.Value (Java_URI.create uri)) caught_panic->
            message = caught_panic.payload.getMessage
            truncated = if message.is_nothing || message.length > 100 then "Invalid URI '" + uri.to_display_text + "'" else
                "URI syntax error: " + message
            Error.throw (Syntax_Error.Error truncated)

    ## PRIVATE

       Represents a Uniform Resource Identifier (URI) reference.

       Arguments:
       - internal_uri: The internal representation of the URI.
    Value internal_uri

    ## PRIVATE
       Convert this to URI.

       > Examples
         Convert a URI to a URI (a no op).

             import Standard.Examples

             example_to_uri = Examples.uri.to_uri
    to_uri : URI
    to_uri self = self

    ## GROUP Metadata
       Get the scheme part of this URI.

       > Example
         Return the scheme from the URI.

             import Standard.Examples

             example_scheme = Examples.uri.scheme
    scheme : Text | Nothing
    scheme self = self.internal_uri.getScheme

    ## GROUP Metadata
       Get the user info part of this URI.

       > Example
         Return the user info part of the URI.

             import Standard.Examples

             example_user_info = Examples.uri.user_info
    user_info : Text | Nothing
    user_info self = self.internal_uri.getUserInfo

    ## GROUP Metadata
       Get the host part of this URI.

       > Example
         Return the host portion of the URI.

             import Standard.Examples

             example_host = Examples.uri.host
    host : Text | Nothing
    host self = self.internal_uri.getHost

    ## Get the authority (user info and host) part of this URI.

       > Example
         Return the authority portion of the URI.

             import Standard.Examples

             example_authority = Examples.uri.authority
    authority : Text | Nothing
    authority self = self.internal_uri.getAuthority

    ## Get the port part of this URI.

       > Example
         Return the port portion of the URI

             import Standard.Examples

             example_port = Examples.uri.port
    port : Text | Nothing
    port self =
        port_number = self.internal_uri.getPort
        if port_number == -1 then Nothing else port_number.to_text

    ## GROUP Metadata
       Get the path part of this URI.

       > Example
         Return the path portion of the URI.

             import Standard.Examples

             example_path = Examples.uri.path
    path : Text | Nothing
    path self = self.internal_uri.getPath

    ## GROUP Metadata
       Get the query part of this URI.

       > Example
         Return the query portion of the URI.

             import Standard.Examples

             example_query = Examples.uri.query
    query : Text | Nothing
    query self = self.internal_uri.getQuery

    ## GROUP Calculations
       Adds a query parameter to the URI

       Arguments:
       - key: The key of the query parameter.
       - value: The value of the query parameter.
    add_query_argument : Text -> Text | Enso_Secret -> URI
    add_query_argument self key:Text value:(Text | Enso_Secret) =
        URI_With_Query.Value self [Pair.new key value]

    ## Get the fragment part of this URI.

       > Example
         Return the fragment portion of the URI.

             import Standard.Examples

             example_fragment = Examples.uri.fragment
    fragment : Text | Nothing
    fragment self = self.internal_uri.getFragment

    ## PRIVATE
       ADVANCED
       Get the unescaped user info part of this URI.
    raw_user_info : Text | Nothing
    raw_user_info self = self.internal_uri.getRawUserInfo

    ## PRIVATE
       ADVANCED
       Get the unescaped authority part of this URI.
    raw_authority : Text | Nothing
    raw_authority self = self.internal_uri.getRawAuthority

    ## PRIVATE
       ADVANCED
       Get the unescaped path part of this URI.
    raw_path : Text | Nothing
    raw_path self = self.internal_uri.getRawPath

    ## PRIVATE
       ADVANCED
       Get the unescaped query part of this URI.
    raw_query : Text | Nothing
    raw_query self = self.internal_uri.getRawQuery

    ## PRIVATE
       ADVANCED
       Get the unescaped fragment part of this URI.
    raw_fragment : Text | Nothing
    raw_fragment self = self.internal_uri.getRawFragment

    ## PRIVATE
       Convert this URI to text.
    to_text : Text
    to_text self = self.internal_uri.toString

    ## PRIVATE
       Convert to a display representation of this URI.
    to_display_text : Text
    to_display_text self = self.to_text.to_display_text

    ## PRIVATE
       Convert to a JavaScript Object representing this URI.

       > Example
         Convert a URI to a JS_Object.

             import Standard.Base.Network.URI.URI
             import Standard.Examples

             example_to_json = Examples.uri.to_js_object
    to_js_object : JS_Object
    to_js_object self =
        type_pair = ["type", "URI"]
        cons_pair = ["constructor", "parse"]
        JS_Object.from_pairs [type_pair, cons_pair, ["uri", self.to_text]]

## PRIVATE
URI.from (that:Text) = URI.parse that

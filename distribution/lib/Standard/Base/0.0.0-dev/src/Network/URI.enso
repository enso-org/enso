from Standard.Base import all

import Standard.Base.Network.URI.Internal

polyglot java import java.net.URI as Java_URI
polyglot java import java.util.Optional

## ALIAS Get URI

   Parse a URI from text.

   Arguments:
   - text: The text to parse as a URI.

   Throws a Syntax_Error when the text cannot be parsed as a URI.

   > Example
     Parse URI text.

         import Standard.Base.Network.URI

         example_parse = URI.parse "http://example.com"
parse : Text -> URI ! Syntax_Error
parse text =
    Panic.catch_java Any (URI_Data (Java_URI.create text)) java_exception->
        Error.throw (Syntax_Error_Data ("URI syntax error: " + java_exception.getMessage))

## Convert Text to a URI.

   Throws a Syntax_Error when `self` cannot be parsed as a URI.

   > Example
     Parse URI text.

         import Standard.Base.Network.URI

         example_parse = "http://example.com".to_uri
Text.to_uri : URI ! Syntax_Error
Text.to_uri self = parse self

## TODO Dubious constructor export
from project.Network.URI.URI import all
from project.Network.URI.URI export all

type URI

    ## PRIVATE

       Represents a Uniform Resource Identifier (URI) reference.

       Arguments:
       - internal_uri: The internal representation of the URI.
    URI_Data internal_uri

    ## Convert this to URI.

       > Examples
         Convert a URI to a URI (a no op).

             import Standard.Examples

             example_to_uri = Examples.uri.to_uri
    to_uri : URI
    to_uri self = self

    ## Get the scheme part of this URI.

       > Example
         Return the scheme from the URI.

             import Standard.Examples

             example_scheme = Examples.uri.scheme
    scheme : Text ! Nothing
    scheme self = Internal.handle_nothing self.internal_uri.getScheme

    ## Get the user info part of this URI.

       > Example
         Return the user info part of the URI.

             import Standard.Examples

             example_user_info = Examples.uri.user_info
    user_info : Text ! Nothing
    user_info self = Internal.handle_nothing self.internal_uri.getUserInfo

    ## Get the host part of this URI.

       > Example
         Return the host portion of the URI.

             import Standard.Examples

             example_host = Examples.uri.host
    host : Text ! Nothing
    host self = Internal.handle_nothing self.internal_uri.getHost

    ## Get the authority (user info and host) part of this URI.

       > Example
         Return the authority portion of the URI.

             import Standard.Examples

             example_authority = Examples.uri.authority
    authority : Text ! Nothing
    authority self = Internal.handle_nothing self.internal_uri.getAuthority

    ## Get the port part of this URI.

       > Example
         Return the port portion of the URI

             import Standard.Examples

             example_port = Examples.uri.port
    port : Text ! Nothing
    port self =
        port_number = self.internal_uri.getPort
        Internal.handle_nothing <|
            if port_number == -1 then Nothing else port_number.to_text

    ## Get the path part of this URI.

       > Example
         Return the path portion of the URI.

             import Standard.Examples

             example_path = Examples.uri.path
    path : Text ! Nothing
    path self = Internal.handle_nothing self.internal_uri.getPath

    ## Get the query part of this URI.

       > Example
         Return the query portion of the URI.

             import Standard.Examples

             example_query = Examples.uri.query
    query : Text ! Nothing
    query self = Internal.handle_nothing self.internal_uri.getQuery

    ## Get the fragment part of this URI.

       > Example
         Return the fragment portion of the URI.

             import Standard.Examples

             example_fragment = Examples.uri.fragment
    fragment : Text ! Nothing
    fragment self = Internal.handle_nothing self.internal_uri.getFragment

    ## ADVANCED

       Get the unescaped user info part of this URI.
    raw_user_info : Text ! Nothing
    raw_user_info self = Internal.handle_nothing self.internal_uri.getRawUserInfo

    ## ADVANCED

       Get the unescaped authority part of this URI.
    raw_authority : Text ! Nothing
    raw_authority self = Internal.handle_nothing self.internal_uri.getRawAuthority

    ## ADVANCED

       Get the unescaped path part of this URI.
    raw_path : Text ! Nothing
    raw_path self = Internal.handle_nothing self.internal_uri.getRawPath

    ## ADVANCED

       Get the unescaped query part of this URI.
    raw_query : Text ! Nothing
    raw_query self = Internal.handle_nothing self.internal_uri.getRawQuery

    ## ADVANCED

       Get the unescaped fragment part of this URI.
    raw_fragment : Text ! Nothing
    raw_fragment self = Internal.handle_nothing self.internal_uri.getRawFragment

    ## Convert this URI to text.

       > Example
         Convert a URI to text.

             import Standard.Examples

             example_to_text = Examples.uri.to_text
    to_text : Text
    to_text self = self.internal_uri.toString

    ## Convert a URI to JSON.

       > Example
         Convert a URI to JSON.

             import Standard.Base.Network.URI
             import Standard.Examples

             example_to_json = Examples.uri.to_json
    to_json : Json.String
    to_json self = Json.String self.to_text

    ## Check if this URI is equal to another URI.

       > Example
         Check if two URIs are equal.

             import Standard.Base.Network.URI

             example_eq = "https://example.com".to_uri == "http://example.org".to_uri
    == : URI -> Boolean
    == self that = self.internal_uri.equals that.internal_uri

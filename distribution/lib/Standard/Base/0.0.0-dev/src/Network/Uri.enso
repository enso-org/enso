from Standard.Base import all

import Standard.Base.Network.Uri.Internal

polyglot java import java.net.URI as Java_URI
polyglot java import java.util.Optional

## ALIAS Get URI

   Parse a Uri from text.

   Arguments:
   - text: The text to parse as a URI.

   Throws a Syntax_Error when the text cannot be parsed as a Uri.

   > Example
     Parse Uri text.

         import Standard.Base.Network.Uri

         example_parse = Uri.parse "http://example.com"
parse : Text -> Uri ! Syntax_Error
parse text =
    Panic.catch_java Any (Uri (Java_URI.create text)) java_exception->
        Error.throw (Syntax_Error java_exception.getMessage)

## Convert Text to a Uri.

   Throws a Syntax_Error when `self` cannot be parsed as a Uri.

   > Example
     Parse Uri text.

         import Standard.Base.Network.Uri

         example_parse = "http://example.com".to_uri
Text.to_uri : Uri ! Syntax_Error
Text.to_uri = parse self

type Uri

    ## PRIVATE

       Represents a Uniform Resource Identifier (URI) reference.

       Arguments:
       - internal_uri: The internal representation of the URI.
    type Uri internal_uri

    ## Convert this to Uri.

       > Examples
         Convert a URI to a URI (a no op).

             import Standard.Examples

             example_to_uri = Examples.uri.to_uri
    to_uri : Uri
    to_uri = self

    ## Get the scheme part of this Uri.

       > Example
         Return the scheme from the URI.

             import Standard.Examples

             example_scheme = Examples.uri.scheme
    scheme : Text ! Nothing
    scheme = Internal.handle_nothing self.internal_uri.getScheme

    ## Get the user info part of this Uri.

       > Example
         Return the user info part of the URI.

             import Standard.Examples

             example_user_info = Examples.uri.user_info
    user_info : Text ! Nothing
    user_info = Internal.handle_nothing self.internal_uri.getUserInfo

    ## Get the host part of this Uri.

       > Example
         Return the host portion of the URI.

             import Standard.Examples

             example_host = Examples.uri.host
    host : Text ! Nothing
    host = Internal.handle_nothing self.internal_uri.getHost

    ## Get the authority (user info and host) part of this Uri.

       > Example
         Return the authority portion of the URI.

             import Standard.Examples

             example_authority = Examples.uri.authority
    authority : Text ! Nothing
    authority = Internal.handle_nothing self.internal_uri.getAuthority

    ## Get the port part of this Uri.

       > Example
         Return the port portion of the URI

             import Standard.Examples

             example_port = Examples.uri.port
    port : Text ! Nothing
    port =
        port_number = self.internal_uri.getPort
        Internal.handle_nothing <|
            if port_number == -1 then Nothing else port_number.to_text

    ## Get the path part of this Uri.

       > Example
         Return the path portion of the URI.

             import Standard.Examples

             example_path = Examples.uri.path
    path : Text ! Nothing
    path = Internal.handle_nothing self.internal_uri.getPath

    ## Get the query part of this Uri.

       > Example
         Return the query portion of the URI.

             import Standard.Examples

             example_query = Examples.uri.query
    query : Text ! Nothing
    query = Internal.handle_nothing self.internal_uri.getQuery

    ## Get the fragment part of this Uri.

       > Example
         Return the fragment portion of the URI.

             import Standard.Examples

             example_fragment = Examples.uri.fragment
    fragment : Text ! Nothing
    fragment = Internal.handle_nothing self.internal_uri.getFragment

    ## ADVANCED

       Get the unescaped user info part of this Uri.
    raw_user_info : Text ! Nothing
    raw_user_info = Internal.handle_nothing self.internal_uri.getRawUserInfo

    ## ADVANCED

       Get the unescaped authority part of this Uri.
    raw_authority : Text ! Nothing
    raw_authority = Internal.handle_nothing self.internal_uri.getRawAuthority

    ## ADVANCED

       Get the unescaped path part of this Uri.
    raw_path : Text ! Nothing
    raw_path = Internal.handle_nothing self.internal_uri.getRawPath

    ## ADVANCED

       Get the unescaped query part of this Uri.
    raw_query : Text ! Nothing
    raw_query = Internal.handle_nothing self.internal_uri.getRawQuery

    ## ADVANCED

       Get the unescaped fragment part of this Uri.
    raw_fragment : Text ! Nothing
    raw_fragment = Internal.handle_nothing self.internal_uri.getRawFragment

    ## Convert this Uri to text.

       > Example
         Convert a URI to text.

             import Standard.Examples

             example_to_text = Examples.uri.to_text
    to_text : Text
    to_text = self.internal_uri.toString

    ## Convert a Uri to JSON.

       > Example
         Convert a URI to JSON.

             import Standard.Base.Network.Uri
             import Standard.Examples

             example_to_json = Examples.uri.to_json
    to_json : Json.String
    to_json = Json.String self.to_text

    ## Check if this URI is equal to another URI.

       > Example
         Check if two URIs are equal.

             import Standard.Base.Network.Uri

             example_eq = "https://example.com".to_uri == "http://example.org".to_uri
    == : Uri -> Boolean
    == that = self.internal_uri.equals that.internal_uri

## UNSTABLE

   Syntax error when parsing a Uri.

   Arguments:
   - message: The error message for the URI syntax error.
type Syntax_Error message

## UNSTABLE

   Converts the URI syntax error to a human-readable form.
Syntax_Error.to_display_text =
    "Uri syntax error: " + self.message


import project.Any.Any
import project.Data.Boolean.Boolean
import project.Data.Numbers.Integer
import project.Data.Text.Text
import project.Data.Text.Extensions
import project.Data.Vector.Vector
import project.Errors.Common.Index_Out_Of_Bounds
import project.Errors.Common.Not_Found
import project.Error.Error
import project.Function.Function
import project.Nothing.Nothing

## A pair of elements.
type Pair
    ## Create a new Pair from two elements.

       Arguments:
       - first: The first element.
       - second: The second element.
    new : Any -> Any -> Pair
    new first second = Pair.Value first second

    ## UNSTABLE

       A simple pair of elements.

       Arguments:
       - first: The first element.
       - second: The second element.
    Value first second

    ## Get the last element of the pair.

       > Example
         The following code returns 4.

             Pair.new 2 4 . last
    last : Any
    last self = self.second

    ## UNSTABLE

       Applies the provided function to both elements of the pair.

       > Example
         Adding one to two elements of the pair.

         (Pair 1 2).map (+1) == (Pair 2 3)
    map : (Any -> Any) -> Pair
    map self fun =
        Pair.new (fun self.first) (fun self.second)

    ## UNSTABLE
       Returns the length (2) of the Pair.
    length : Integer
    length self = 2

    ## UNSTABLE
       Returns the pair as a Vector.
    to_vector : Vector
    to_vector self = [self.first, self.second]

    ## Gets an element from the pair at a specified index (0-based).

       Arguments:
       - index: The location in the pair to get the element from. The index is
         also allowed be negative, then the elements are indexed from the back
         of the pair, i.e. -1 will correspond to the last element.
    at : Integer -> Any
    at self index =
        self.get index (Error.throw (Index_Out_Of_Bounds.Error index 2))

    ## Gets an element from the pair at a specified index (0-based).
       If the index is invalid then `if_missing` is returned.

       Arguments:
       - index: The location in the pair to get the element from. The index is
         also allowed be negative, then the elements are indexed from the back
         of the pair, i.e. -1 will correspond to the last element.
       - if_missing: The value to return if the index is out of bounds.
    get : Integer -> Any -> Any
    get self index ~if_missing=Nothing =
        case index of
            0 -> self.first
            1 -> self.second
            -1 -> self.second
            -2 -> self.first
            _ -> if_missing

    ## Checks whether a predicate holds for at least one element of self pair.

       Arguments:
       - predicate: A function that takes a list element and returns a boolean
         value that says whether that value satisfies the conditions of the
         function.

       > Example
         Checking if any element of the list is larger than 3.

             Pair.new 1 5 . any (> 3)
    any : (Any -> Boolean) -> Boolean
    any self predicate = predicate self.first || predicate self.second

    ## Checks whether a predicate holds for all elements in this pair.

       Arguments:
       - predicate: A function that takes a list element and returns a boolean
         value that says whether that value satisfies the conditions of the
         function.

       > Example
         Check if all elements in the pair are less than zero.

             Pair.new -1 3 . all (< 0)
    all : (Any -> Boolean) -> Boolean
    all self predicate = predicate self.first && predicate self.second

    ## Checks whether this pair contains a given value as an element.

       Arguments:
       - elem: The item to see if it exists in the pair.

       > Example
         Checking if the vector contains the number 72.

             Pair.new 1 42 . contains 72
    contains : Any -> Boolean
    contains self elem = self.first == elem || self.second == elem

    ## Returns the first element of the pair that satisfies the predicate or
       if no elements satisfy the predicate, returns `if_missing`.

       Arguments:
       - predicate: A function that takes a list element and returns a boolean
         value that says whether that value satisfies the conditions of the
         function.
       - start: The index to start searching from. If the index is negative, it
         is counted from the end of the pair.
       - if_missing: Value returned if no element satisfies the predicate.

       > Example
         Finding a first element of the pair that is larger than 3.

             Pair.new 1 6 .find (> 3)
    find : (Any -> Boolean) -> Integer -> Any -> Any
    find self predicate start=0 ~if_missing=(Error.throw Not_Found) =
        check_start_valid start used_start->
            if used_start<1 && predicate self.first then self.first else
                if used_start<2 && predicate self.second then self.second else
                    if_missing

    ## Returns the index of an element in the pair.
       Returns Nothing if the element is not found.

       Arguments:
       - element: The element to search for or a predicate function to test for
         each element.
       - start: The index to start searching from. If the index is negative, it
         is counted from the end of the pair.

       > Example
         Find the index of an element in a pair.

             Pair.new 1 2 . index_of 2 == 1
             Pair.new 2 2 . index_of 2 == 0
    index_of : (Any | (Any -> Boolean)) -> Integer -> Integer | Nothing
    index_of self element start=0 = check_start_valid start used_start->
        predicate = case element of
            _ : Function -> element
            _ -> (==element)
        if used_start<1 && predicate self.first then 0 else
            if used_start<2 && predicate self.second then 1 else
                Nothing

    ## Returns the last index of an element in the pair.
       Returns Nothing if the element is not found.

       Arguments:
       - element: The element to search for or a predicate function to test for
         each element.
       - start: The index to start searching backwards from. If the index is
         negative, it is counted from the end of the pair.

       > Example
         Find the last index of an element in a pair.

             Pair.new 2 2 . last_index_of 2 == 1
    last_index_of : (Any | (Any -> Boolean)) -> Integer -> Integer | Nothing
    last_index_of self element start=-1 = check_start_valid start max=2 used_start->
        predicate = case element of
            _ : Function -> element
            _ -> (==element)
        if used_start>0 && predicate self.second then 1 else
            if used_start>=0 && predicate self.first then 0 else
                Nothing

    ## Combines all the elements of the pair, by iteratively applying the
       passed function with next elements of the pair.

       Arguments:
       - init: The initial value for the fold.
       - function: A function taking two elements and combining them.

       > Example
         Compute the sum of all of the elements in a pair.

             Pair.new 10 20 . fold 0 (+)
    fold : Any -> (Any -> Any -> Any) -> Any
    fold self init function =
        function (function init self.first) self.second

    ## Combines all the elements of the pair using a binary operation.

       Arguments:
       - function: A binary operation that takes two items and combines them.

       > Example
         Compute the sum of all the elements in a Pair.

             Pair.new 10 20 . reduce (+)
    reduce : (Any -> Any -> Any) -> Any
    reduce self function =
        function self.first self.second

    ## Reverses the pair, returning a pair with the same elements, but in
       the opposite order.

       > Example
         Reverse a two-element vector.

             Pair.new 1 2 . reverse
    reverse : Pair
    reverse self = Pair.new self.second self.first

    ## Applies a function to each element of the pair.

       Unlike `map`, this method does not return the individual results,
       therefore it is only useful for side-effecting computations.

       Arguments:
       - function: A function to apply to each element of the pair.

       > Example
         Print each element in the vector to standard output.

             Pair.new 1 2 . each IO.println
    each : (Any -> Any) -> Nothing
    each self f =
        f self.first
        f self.second
        Nothing

    ## PRIVATE
       Convert to a display representation of this Pair.
    to_display_text : Text
    to_display_text self =
        first = self.first.to_display_text
        second = self.second.to_display_text
        if first.length + second.length < 73 then "Pair(" + first + ", " + second + ")" else
            first_trim = if first.length > 36 then first.take 34 + " …" else first
            second_trim = if second.length > 36 then second.take 34 + " …" else second
            "Pair(" + first_trim + ", " + second_trim + ")"

## PRIVATE
check_start_valid start function max=3 =
    used_start = if start < 0 then start + 2 else start
    if used_start < 0 || used_start >= max then Error.throw (Index_Out_Of_Bounds.Error start max) else
        function used_start

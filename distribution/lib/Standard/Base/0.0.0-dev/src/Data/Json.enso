from Standard.Base import all

import Standard.Base.Data.Json.Internal

## A smart constructor, building an object representation based on a vector
   of key-value pairs.

   Arguments:
   - contents: A vector of key-value pairs.

   All values used as keys must define a `to_json_key : Text` method.

   > Example
     The following code returns a JSON object, that after serialization becomes
     { "foo": 533, "bar": false }

         import Standard.Base.Data.Json

         example_from_pairs = Json.from_pairs [["foo", 533], ["bar", False]]
from_pairs : Vector.Vector -> Object
from_pairs contents =
    fs = contents.fold Map.empty map-> kv_pair->
        key = kv_pair . at 0 . to_json_key
        val = kv_pair . at 1 . to_json
        map.insert key val
    Object fs

## Parses an RFC-8259 compliant JSON text into a `Json` structure.

   Arguments:
   - json_text: The RFC-8259-compliant JSON text.

   > Example
     Convert some text representing a JSON object into JSON.

         import Standard.Base.Data.Json

         example_parse = Json.parse '{ "a": 1 }'
parse : Text -> Json ! Parse_Error
parse json_text =
    Panic.catch Polyglot_Error (Internal.parse_helper json_text) caught_panic->
        Error.throw (Parse_Error caught_panic.payload.cause.getMessage)

## Represents a JSON structure.
type Json

    ## A representation of a JSON object.

       Arguments:
       - fields: The fields of the JSON object.
    type Object fields

    ## A representation of a JSON array.

       Arguments:
       - items: The items in the JSON array.
    type Array items

    ## A representation of a JSON string.

       Arguments:
       - value: The text contained in the JSON string.
    type String value

    ## A representation of a JSON number.

       Arguments:
       - value: The number contained in the JSON number.
    type Number value

    ## A representation of a JSON boolean.

       Arguments:
       - value: The boolean contained in a JSON boolean.
    type Boolean value

    ## A representation of a JSON null.
    type Null

    ## Marshalls this JSON into an arbitrary value described by
       `type_descriptor`.

       Arguments:
       - `type_dscriptor`: The type descriptor is a fully-applied type,
         describing all required sub-types. It can either be an Atom or one of
         the primitive types (`Number`, `Text`, `Boolean`, `Vector`).

       > Example
         The following shows an example of reading a nested JSON into a desired
         type. It will return a vector of `Book` objects containing data from
         `json_string`.

             import Standard.Base.Data.Json
             import Standard.Examples

             type Book title author
             type Author name year_of_birth

             example_into =
                 json = Examples.json
                 format = (Vector.Vector (Book title=Text (Author name=Text year_of_birth=Number)))
                 json.into format
    into : Any -> Any ! Marshalling_Error
    into type_descriptor =
        Panic.recover Any (Internal.into_helper type_descriptor self)

    ## Returns this Json object.

       This is a no-op on a JSON object, but is included to implement the
       `to_json` interface.
    to_json : Json
    to_json = self

    ## Renders this JSON into an RFC-8259 compliant text.

       > Example
         Convert some JSON to text.

             import Standard.Base.Data.Json
             import Standard.Examples

             example_to_text = Examples.json.to_text
    to_text : Text
    to_text = Internal.render_helper "" self

    ## Recursively unwraps the JSON value into primitive values.

       > Example
         Unwrap the JSON number 3 to the primitive number 3.

             import Standard.Base.Data.Json

             example_unwrap = Json.Number 3 . unwrap
    unwrap : Any
    unwrap = case self of
        Json.Array its -> its.map .unwrap
        Json.Boolean b -> b
        Json.Number n -> n
        Json.String t -> t
        Json.Null -> Nothing
        Json.Object f -> f.map .unwrap

## UNSTABLE

   A failure indicating malformed text input into the JSON parser.

   Check the `message` field for detailed information on the specific failure.
type Parse_Error message

## UNSTABLE

   Converts the error to a display representation.
Parse_Error.to_display_text : Text
Parse_Error.to_display_text =
    "Parse error in parsing JSON: " + self.message.to_text + "."

## Gets the value associated with the given key in this object.

   Arguments:
   - field: The name of the field from which to get the value.

   Throws `Nothing` if the associated key is not defined.

   > Example
     Get the "title" field from this JSON representing a book.

         import Standard.Base.Data.Json
         import Standard.Examples

         example_get = Examples.json_object.get "title"
Object.get : Text -> Json ! No_Such_Field_Error
Object.get field = self.fields.get field . map_error case _ of
    Map.No_Value_For_Key_Error _ -> No_Such_Field_Error field
    x -> x

## UNSTABLE

   An error indicating that there is no such field in the JSON object.
type No_Such_Field_Error field_name

## UNSTABLE

   Pretty prints the no such field error.
No_Such_Field_Error.to_display_text : Text
No_Such_Field_Error.to_display_text =
    "The field " + self.field_name.to_text + " is not present in this object."

## UNSTABLE

   A failure indicating the inability to marshall a `Json` object into the
   specified format.
type Marshalling_Error

    ## UNSTABLE

       The `json` object could not be converted into `format`, due to a type
       mismatch.

       Arguments:
       - json: The JSON that could not be marshalled.
       - format: The type format that did not match.

       This can occur e.g. when trying to reinterpret a number as a `Text`, etc.
    type Type_Mismatch_Error json format

    ## UNSTABLE

       The `json` object could not be converted into `format`, due to a field
       missing in the `json` structure.

       Arguments:
       - json: The json that had a missing field.
       - field: The field name that was missing.
       - format: The type format that diud not match.

       This can occure when trying to reinterpret a JSON object into an atom,
       when the JSON does not contain all the fields required by the atom.
    type Missing_Field_Error json field format

    ## UNSTABLE

       Convert the marshalling error into a human-readable format.
    to_display_text : Text
    to_display_text = case self of
        Type_Mismatch_Error json format ->
            json_text = Meta.get_simple_type_name json
            format_text = Meta.get_simple_type_name format
            "Type mismatch error: the json with type `" + json_text + "` did not match the format `" + format_text + "`."
        Missing_Field_Error _ field _ ->
            "Missing field in Json: the field `" + field.to_text "` was missing in the json."

## ALIAS To JSON

   Generically converts an atom into a JSON object.

   The input atom is converted into a JSON object, with a `"type"` field set to
   the atom's type name and all other fields serialized with their name as
   object key and the value as the object value.

   > Example
     Convert a vector to JSON.
         [1, 2, 3, 4].to_json
Any.to_json =
    m = Meta.meta self
    case m of
        Meta.Atom _ ->
            cons = Meta.Constructor m.constructor
            fs = m.fields
            fnames = cons.fields
            json_fs = 0.up_to fnames.length . fold Map.empty m-> i->
                m.insert (fnames.at i) (fs.at i . to_json)
            with_tp = json_fs . insert "type" (String cons.name)
            Object with_tp
        Meta.Constructor _ ->
            Object (Map.empty . insert "type" (String m.name))

        ## The following two cases cannot be handled generically and should
           instead define their own `to_json` implementations.
        Meta.Polyglot _ -> Null
        Meta.Primitive _ -> Null

## Method used by object builders to convert a value into a valid JSON key.

   > Example
     Ensure that the text "foo" is a JSON key.
         "foo".to_json_key
Text.to_json_key : Text
Text.to_json_key = self

## Convert a boolean to JSON.

   > Example
     Convert `True` to JSON.
         True.to_json
Base.Boolean.to_json : Boolean
Base.Boolean.to_json = Boolean self

## Convert `Nothing` to JSON.

   > Example
     Convert `Nothing` to JSON.
         Nothing.to_json
Nothing.to_json : Null
Nothing.to_json = Null


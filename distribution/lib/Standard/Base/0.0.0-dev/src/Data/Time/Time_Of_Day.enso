import project.Any.Any
import project.Data.Json.JS_Object
import project.Data.Locale.Locale
import project.Data.Numbers.Integer
import project.Data.Ordering.Comparable
import project.Data.Text.Text
import project.Data.Time.Date.Date
import project.Data.Time.Date_Time.Date_Time
import project.Data.Time.Date_Time_Formatter.Date_Time_Formatter
import project.Data.Time.Duration.Duration
import project.Data.Time.Period.Period
import project.Data.Time.Time_Period.Time_Period
import project.Data.Time.Time_Zone.Time_Zone
import project.Error.Error
import project.Errors.Common.Type_Error
import project.Errors.Illegal_Argument.Illegal_Argument
import project.Errors.Time_Error.Time_Error
import project.Meta
import project.Nothing.Nothing
import project.Panic.Panic
from project.Data.Boolean import Boolean, False, True
from project.Data.Text.Extensions import all
from project.Widget_Helpers import make_time_format_selector

polyglot java import java.lang.Exception as JException
polyglot java import java.time.Instant
polyglot java import java.time.LocalTime
polyglot java import org.enso.base.Time_Utils

## PRIVATE

   Obtains an instance of `Time_Of_Day` from an hour, minute, second
   and nanosecond.

   Arguments:
   - hour: The hour-of-day to represent, from 0 to 23.
   - minute: The minute-of-hour to represent, from 0 to 59.
   - second: The second-of-minute to represent, from 0 to 59.
   - nanosecond: The nano-of-second to represent, from 0 to 999,999,999.

   Recommended to use `Time_Of_Day.new` instead which handles potential exceptions.
new_builtin : Integer -> Integer -> Integer -> Integer -> Time_Of_Day
new_builtin hour minute second nanosecond = @Builtin_Method "Time_Of_Day.new_builtin"

## PRIVATE

   This type is a date-time object that represents a time, often viewed
   as hour-minute-second.

   Time is represented to nanosecond precision. For example, the value
   "13:45.30.123456789" can be stored in a `Time_Of_Day`.
@Builtin_Type
type Time_Of_Day
    ## GROUP DateTime
       Obtains the current time from the system clock in the default time-zone.

       > Example
         Get the current time in the default time zone.

             from Standard.Base import Time_Of_Day

             example_now = Time_Of_Day.now
    now : Time_Of_Day
    now = @Builtin_Method "Time_Of_Day.now"

    ## GROUP DateTime
       Obtains an instance of `Time_Of_Day` from an hour, minute, second
       and nanosecond.

       Arguments:
       - hour: The hour-of-day to represent, from 0 to 23.
       - minute: The minute-of-hour to represent, from 0 to 59.
       - second: The second-of-minute to represent, from 0 to 59.
       - millisecond: the millisecond-of-second to represent, from 0 to 999.
       - microsecond: the microsecond-of-second to represent, from 0 to 999,999.
       - nanosecond: The nano-of-second to represent, from 0 to 999,999,999.

       Returns a `Time_Error` if the provided time is not a valid time.

       > Example
         Create a new local time at Unix epoch.

             from Standard.Base import Time_Of_Day

             example_epoch = Time_Of_Day.new

       > Example
         Get the local time at 9:30.

             from Standard.Base import Time_Of_Day

             example_epoch = Time_Of_Day.new hour=9 minute=30
    new : Integer -> Integer -> Integer -> Integer -> Integer -> Integer -> Time_Of_Day ! Time_Error
    new (hour = 0) (minute = 0) (second = 0) (millisecond = 0) (microsecond = 0) (nanosecond = 0) =
        total_nanoseconds = nanosecond + microsecond * 1000 + millisecond * 1000000
        Panic.catch JException (new_builtin hour minute second total_nanoseconds) caught_panic->
            Error.throw (Time_Error.Error caught_panic.payload.getMessage)

    ## GROUP Conversions
       Obtains an instance of `Time_Of_Day` from a text such as "10:15".

       Arguments:
       - text: The text to parse as a time of day.
       - format: A pattern describing how to parse the text,
         or a `Date_Time_Formatter`.

       Returns a `Time_Error` if the provided text cannot be parsed using the
       default format.

       ? Default Time Format
         Unless you provide a custom format, the text must represent a valid
         time and is parsed using the ISO-8601 extended local time format.
         The format consists of:

         - Two digits for the hour-of-day. This is pre-padded by zero to ensure two
           digits.
         - A colon
         - Two digits for the minute-of-hour. This is pre-padded by zero to ensure
           two digits.
         - If the second-of-minute is not available then the format is complete.
         - A colon
         - Two digits for the second-of-minute. This is pre-padded by zero to ensure
           two digits.
         - If the nano-of-second is zero or not available then the format is
           complete.
         - A decimal point
         - One to nine digits for the nano-of-second. As many digits will be output
           as required.

       ? Pattern Syntax
         If the pattern is provided as `Text`, it is parsed using the format
         described below. See `Date_Time_Formatter` for more options.
         - H: 24h hour of day (0-23).
         - h: 12h hour of day (0-12). The `a` pattern is needed to disambiguate
           between AM and PM.
         - m: Minute of hour.
         - s: Second of minute.
         - f: Fractional part of the second. The number of pattern letters
           determines the number of digits. If one letter is used, any number of
           digits will be accepted.
         - a: AM/PM marker.

       > Example
         Get the time 15:05:30.

             from Standard.Base import Time_Of_Day

             example_parse = Time_Of_Day.parse "15:05:30"

       > Example
         Recover from the parse error.

             from Standard.Base import Time_Of_Day
             from Standard.Base.Errors.Common import Time_Error

             example_parse = Time_Of_Day.parse "half twelve" . catch Time_Error _->
                 Time_Of_Day.new

       > Example
         Parse "04:30:20" as Time_Of_Day.

             from Standard.Base import Time_Of_Day

             example_parse = Time_Of_Day.parse "04:30:20" "HH:mm:ss"

       > Example
         Parse "4:30AM" as Time_Of_Day

             from Standard.Base import Time_Of_Day

             example_parse = Time_Of_Day.parse "4:30AM" "h:mma"
    @format make_time_format_selector
    parse : Text -> Date_Time_Formatter -> Time_Of_Day ! Time_Error
    parse text:Text format:Date_Time_Formatter=Date_Time_Formatter.iso_time =
        format.parse_time text

    ## GROUP Metadata
       Get the hour portion of the time of day.

       > Example
         Get the current hour.

             from Standard.Base import Time_Of_Day

             example_hour = Time_Of_Day.now.hour
    hour : Integer
    hour self = @Builtin_Method "Time_Of_Day.hour"

    ## GROUP Metadata
       Get the minute portion of the time of day.

       > Example
         Get the current minute.

             from Standard.Base import Time_Of_Day

             example_minute = Time_Of_Day.now.minute
    minute : Integer
    minute self = @Builtin_Method "Time_Of_Day.minute"

    ## GROUP Metadata
       Get the second portion of the time of day.

       > Example
         Get the current second.

             from Standard.Base import Time_Of_Day

             example_second = Time_Of_Day.now.second
    second : Integer
    second self = @Builtin_Method "Time_Of_Day.second"

    ## GROUP Metadata
       Get the millisecond portion of the time of day.

       > Example
         Get the current millisecond.

             from Standard.Base import Time_Of_Day

             example_millisecond = Time_Of_Day.now.millisecond
    millisecond : Integer
    millisecond self = @Builtin_Method "Time_Of_Day.millisecond"

    ## GROUP Metadata
       Get the microsecond portion of the time of day.

       > Example
         Get the current microsecond.

             from Standard.Base import Time_Of_Day

             example_microsecond = Time_Of_Day.now.microsecond
    microsecond : Integer
    microsecond self = @Builtin_Method "Time_Of_Day.microsecond"

    ## GROUP Metadata
       Get the nanosecond portion of the time of day.

       Arguments:
       - include_milliseconds: Specifies if the whole fractional part of the
         second should be returned as nanoseconds. Defaults to `False`, meaning
         it will only return the nanosecond part in the range 0-999.

       > Example
         Get the current nanosecond.

             from Standard.Base import Time_Of_Day

             example_nanosecond = Time_Of_Day.now.nanosecond
    nanosecond : Boolean -> Integer
    nanosecond self include_milliseconds=False =
        self.nanosecond_builtin include_milliseconds

    ## GROUP DateTime
       Returns the first time within the `Time_Period` containing self.
    start_of : Time_Period -> Time_Of_Day
    start_of self period=Time_Period.Day = period.adjust_start self

    ## GROUP DateTime
       Returns the last time within the `Time_Period` containing self.
    end_of : Time_Period -> Time_Of_Day
    end_of self period=Time_Period.Day = period.adjust_end self

    ## GROUP Conversions
       Extracts the time as the number of seconds, from 0 to 24 * 60 * 60 - 1.

       > Example
         Convert the current time into elapsed seconds in the day.

             from Standard.Base import Time_Of_Day

             example_to_seconds = Time_Of_Day.now.to_seconds
    to_seconds : Integer
    to_seconds self = @Builtin_Method "Time_Of_Day.to_seconds"

    ## GROUP Conversions
       Combine this time of day with a date to create a point in time.

       Arguments:
       - date: The date on which this time should occur.
       - zone: The time-zone in which the time is specified.

       > Example
         Convert local time to 1st January 2020 12:30 at system timezone.

             from Standard.Base import Time_Of_Day

             example_to_time = Time_Of_Day.new 12 30 . to_time (Date.new 2020)
    to_date_time : Date -> Time_Zone -> Date_Time
    to_date_time self date (zone=Time_Zone.system) =
        Time_Utils.make_zoned_date_time date self zone


    ## GROUP DateTime
       Returns a requested time part as integer.
    date_part : Time_Period -> Integer
    date_part self (period : Time_Period) =
        case period of
            Time_Period.Day         -> Error.throw (Illegal_Argument.Error "The Time_Of_Day does not have a day part.")
            Time_Period.Hour        -> self.hour
            Time_Period.Minute      -> self.minute
            Time_Period.Second      -> self.second
            Time_Period.Millisecond -> self.millisecond
            Time_Period.Microsecond -> self.microsecond
            Time_Period.Nanosecond  -> self.nanosecond

    ## GROUP DateTime
       Computes a time difference between the two times of day.

       It returns an integer expressing how many periods fit between the two
       times of day.

       The difference will be positive if `end` is greater than `self`.

       Arguments:
       - end: A time of day to compute the difference from.
       - period: The period to compute the difference in.
    date_diff : Time_Of_Day -> Time_Period -> Integer
    date_diff self (end : Time_Of_Day) (period : Time_Period) = case period of
        Time_Period.Day ->
            Error.throw (Illegal_Argument.Error "The Time_Of_Day does not have a day part to compute a difference in days.")
        _ ->
            Time_Utils.unit_time_difference period.to_java_unit self end

    ## GROUP DateTime
       Shifts the time of day by a specified period.

       Arguments:
       - amount: An integer specifying by how many periods to shift the time.
       - period: The period by which to shift.
    date_add : Integer -> Time_Period -> Time_Of_Day
    date_add self (amount : Integer) (period : Time_Period) = case period of
        Time_Period.Day ->
            Error.throw (Illegal_Argument.Error "The Time_Of_Day does not have a day part to add days to.")
        _ ->
            Time_Utils.unit_time_add period.to_java_unit self amount

    ## ALIAS add duration
       GROUP Operators
       Add the specified amount of time to this instant to get a new instant.

       Arguments:
       - amount: The amount of time to add to this instant. It can be a
         `Duration` or `Time_Period`.

       > Example
         Add a `Duration` to a `Time_Of_Day`.

             from Standard.Base import Time_Of_Day, Duration

             example_plus = Time_Of_Day.new + (Duration.new seconds=3)

       > Example
         Add a `Time_Period` to a `Time_Of_Day`.

             from Standard.Base import Time_Of_Day, Time_Period

             example_plus = Time_Of_Day.new + Time_Period.Hour
    + : Duration | Time_Period -> Time_Of_Day ! Time_Error
    + self (amount : Duration | Time_Period) =
        case amount of
            duration : Duration -> self.plus_builtin duration
            time_period : Time_Period ->
                self + time_period.to_duration

    ## ALIAS subtract duration
       GROUP Operators
       Subtract the  specified amount of time from this instant to get a new
       instant.

       Arguments:
       - amount: The amount of time to add to this instant. It can be a
         `Duration` or `Time_Period`.

       > Example
         Subtract 12 hours from a local time.

             from Standard.Base import Time_Of_Day, Duration

             example_minus = Time_Of_Day.now - (Duration.new hours=12)

       > Example
         Subtract a `Time_Period` from a `Time_Of_Day`.

             from Standard.Base import Time_Of_Day, Time_Period

             example_plus = Time_Of_Day.new - Time_Period.Hour
    - : Duration | Time_Period -> Time_Of_Day ! Time_Error
    - self amount:(Duration | Time_Period) =
        case amount of
            duration : Duration -> self.minus_builtin duration
            time_period : Time_Period ->
                self - time_period.to_duration

    ## PRIVATE
       Convert to a JavaScript Object representing this Time_Of_Day.

       > Example
         Convert the current time to a JS_Object.

             example_to_json = Time_Of_Day.now.to_js_object
    to_js_object : JS_Object
    to_js_object self =
        type_pair = ["type", "Time_Of_Day"]
        cons_pair = ["constructor", "new"]
        JS_Object.from_pairs [type_pair, cons_pair, ["hour", self.hour], ["minute", self.minute], ["second", self.second], ["nanosecond", self.nanosecond include_milliseconds=True]]

    ## PRIVATE
       Convert to a display representation of this Time_Of_Day.
    to_display_text : Text
    to_display_text self =
        if self.nanosecond include_milliseconds=True == 0 then self.format "HH:mm:ss" else
            self.format "HH:mm:ss.ffffff"

    ## GROUP Conversions
       Format this time of day using the provided formatter pattern.

       Arguments:
       - format: A pattern describing how to format the text,
         or a `Date_Time_Formatter`.

       ? Pattern Syntax
         If the pattern is provided as `Text`, it is parsed using the format
         described below. See `Date_Time_Formatter` for more options.
         - H: 24h hour of day (0-23).
         - h: 12h hour of day (0-12). The `a` pattern is needed to disambiguate
           between AM and PM.
         - m: Minute of hour.
         - s: Second of minute.
         - f: Fractional part of the second. The number of pattern letters
           determines the number of digits. If one letter is used, any number of
           digits will be accepted.
         - a: AM/PM marker.

       > Example
         Format "16:21:10" as "16:21:00.1234"

             from Standard.Base import Time_Of_Day

             example_format = Time_Of_Day.new 16 21 10 . format "HH:mm:ss.SSSS"

       > Example
         Format "16:21:10" as "16:21:00.123456789"

             from Standard.Base import Time_Of_Day

             example_format = Time_Of_Day.new 16 21 10 . format "HH:mm:ss.n"

       > Example
         Format "16:21:10" as "4:21pm"

             from Standard.Base import Time_Of_Day

             example_format = Time_Of_Day.new 16 21 10 . format "h:mma"

       > Example
         Format "16:21:10" as "04:21:10pm"

             from Standard.Base import Time_Of_Day

             example_format = Time_Of_Day.new 16 21 10 . format "hh:mm:ssa"

       > Example
         Format "16:21:10" as "hour:4"

             from Standard.Base import Time_Of_Day

             example_format = Time_Of_Day.new 16 21 10 . format "'hour:'h"
    @format (value-> make_time_format_selector value)
    format : Date_Time_Formatter -> Text
    format self format:Date_Time_Formatter =
        format.format_time self

## PRIVATE
Time_Of_Day.from (that:JS_Object) =
    ## Must have hour and minute but second and nanosecond are optional
    case that.get "type" == "Time_Of_Day" && ["hour", "minute"].all that.contains_key of
        True ->
            if that.contains_key "nanosecond" && (that.contains_key "second" . not) then Error.throw (Illegal_Argument.Error "Invalid JS_Object for Time_Of_Day.from.") else
                Time_Of_Day.new (that.get "hour") (that.get "minute") (that.get "second" 0) nanosecond=(that.get "nanosecond" 0)
        False -> Error.throw (Illegal_Argument.Error "Invalid JS_Object for Time_Of_Day.from.")

from Standard.Base import all
from Standard.Base.Error.Common import Index_Out_Of_Bounds_Error
from Standard.Base.Data.Text.Span import Span, range_to_char_indices
import Standard.Base.Data.Index_Sub_Range
from Standard.Base.Data.Index_Sub_Range import First, Last, While, By_Index, Sample, Every
import Standard.Base.Random

from Standard.Base.Data.Index_Sub_Range export First, Last, While, By_Index, Sample, Every

polyglot java import com.ibm.icu.text.BreakIterator
polyglot java import org.enso.base.Text_Utils

## Type defining a substring of a Text
type Text_Sub_Range
    ## Select characters until the first instance of `delimiter`.
       Select an empty string if `delimiter` is empty.
       Select the entire string if the input does not contain `delimiter`.
    Before (delimiter : Text)

    ## Select characters until the last instance of `delimiter`.
       Select an empty string if `delimiter` is empty.
       Select the entire string if the input does not contain `delimiter`.
    Before_Last (delimiter : Text)

    ## Select characters after the first instance of `delimiter`.
       Select an empty string if the input does not contain `delimiter`.
    After (delimiter : Text)

    ## Select characters after the last instance of `delimiter`.
       Select an empty string if the input does not contain `delimiter`.
    After_Last (delimiter : Text)

## PRIVATE
   Finds code-point indices corresponding to the part of the input matching the
   range specified by one of the types: `Text_Sub_Range`, `Index_Sub_Range`,
   `Range`.

   This method may return either a single range instance or a vector of ranges.

   While the input ranges may have varying steps, they are processed and split
   in such a way that the ranges returned by this method always have a step
   equal to 1.
find_codepoint_ranges : Text -> (Text_Sub_Range | Index_Sub_Range | Range) -> (Range_Data | Codepoint_Ranges)
find_codepoint_ranges text subrange =
    case subrange of
        Before delimiter ->
            if delimiter.is_empty then (Range_Data 0 0) else
                span = Text_Utils.span_of text delimiter
                if span.is_nothing then (Range_Data 0 (Text_Utils.char_length text)) else
                    (Range_Data 0 span.codeunit_start)
        Before_Last delimiter ->
            if delimiter.is_empty then (Range_Data 0 (Text_Utils.char_length text)) else
                span = Text_Utils.last_span_of text delimiter
                if span.is_nothing then (Range_Data 0 (Text_Utils.char_length text)) else
                    (Range_Data 0 span.codeunit_start)
        After delimiter ->
            if delimiter.is_empty then (Range_Data 0 (Text_Utils.char_length text)) else
                span = Text_Utils.span_of text delimiter
                if span.is_nothing then (Range_Data 0 0) else
                    (Range_Data span.codeunit_end (Text_Utils.char_length text))
        After_Last delimiter ->
            if delimiter.is_empty then (Range_Data 0 0) else
                span = Text_Utils.last_span_of text delimiter
                if span.is_nothing then (Range_Data 0 0) else
                    (Range_Data span.codeunit_end (Text_Utils.char_length text))
        First count ->
            if count <= 0 then (Range_Data 0 0) else
                iterator = BreakIterator.getCharacterInstance
                iterator.setText text
                start_index = iterator.next count
                Range_Data 0 (if start_index == -1 then (Text_Utils.char_length text) else start_index)
        Last count ->
            if count <= 0 then (Range_Data 0 0) else
                iterator = BreakIterator.getCharacterInstance
                iterator.setText text
                iterator.last
                start_index = iterator.next -count
                Range_Data (if start_index == -1 then 0 else start_index) (Text_Utils.char_length text)
        While predicate ->
            indices = find_sub_range_end text _-> start-> end->
                predicate (Text_Utils.substring text start end) . not
            if indices.first.is_nothing then (Range_Data 0 indices.second) else
                Range_Data 0 indices.first
        By_Index indices ->
            case indices of
                Vector.Vector_Data _ ->
                    if indices.length == 1 then resolve_index_or_range text indices.first else
                        batch_resolve_indices_or_ranges text indices
                _ -> resolve_index_or_range text indices
        Sample count seed ->
            rng = Random.new seed
            indices = Random.random_indices text.length count rng
            find_codepoint_ranges text (By_Index indices)
        Every step start ->
            if step <= 0 then Error.throw (Illegal_Argument_Error_Data "Step within Every must be positive.") else
                len = text.length
                if start >= len then Range_Data 0 0 else
                    range = Range_Data start text.length step
                    find_codepoint_ranges text (By_Index range)
        Range_Data _ _ _ ->
            find_codepoint_ranges text (By_Index subrange)

type Codepoint_Ranges
    ## PRIVATE
       A list of codepoint ranges corresponding to the matched parts of the
       input.

       Arguments:
       - ranges: the list of ranges. Each `Range` has `step` equal to 1.
       - is_sorted_and_distinct: A helper value specifying if the ranges are
         already sorted and non-intersecting.
    Codepoint_Ranges_Data (ranges : Vector Range) (is_sorted_and_distinct : Boolean)

    ## PRIVATE
       Returns a new sorted list of ranges where intersecting ranges have been
       merged.

       Empty subranges are not discarded.
    sorted_and_distinct_ranges : Vector Range
    sorted_and_distinct_ranges self = if self.is_sorted_and_distinct then self.ranges else
        Index_Sub_Range.sort_and_merge_ranges self.ranges

## PRIVATE
   Utility function to find char indices for Text_Sub_Range.
   Arguments:
   - text: Text to search
   - predicate: Function to test each character, receives:
     - index: current index
     - start: index the char array to start of grapheme cluster
     - end: index the char array to start of next grapheme cluster
     If the predicate returns True for a given character, the loop will exit.
   Returns: either a Pair of char indices for current grapheme cluster or
     Pair Nothing (char array length) if not found.
find_sub_range_end text predicate =
    iterator = BreakIterator.getCharacterInstance
    iterator.setText text

    loop index start end =
        if end == -1 then (Pair_Data Nothing start) else
            if predicate index start end then (Pair_Data start end) else
                @Tail_Call loop (index + 1) end iterator.next

    loop 0 0 iterator.next

## PRIVATE
resolve_index_or_range text descriptor = Panic.recover [Index_Out_Of_Bounds_Error_Data, Illegal_Argument_Error_Data] <|
    iterator = BreakIterator.getCharacterInstance
    iterator.setText text
    case descriptor of
        Integer ->
            if descriptor < 0 then
                iterator.last
            start = iterator.next descriptor
            end = iterator.next
            if (start == -1) || (end == -1) then Error.throw (Index_Out_Of_Bounds_Error_Data descriptor text.length) else
                Range_Data start end
        Range_Data _ _ _ ->
            len = text.length
            true_range = normalize_range descriptor len
            if descriptor.is_empty then Range_Data 0 0 else
                case true_range.step == 1 of
                    True -> range_to_char_indices text true_range
                    False ->
                        ranges = Vector.new_builder
                        if true_range.step <= 0 then panic_on_non_positive_step
                        go start_index current_grapheme =
                            end_index = iterator.next
                            if (start_index == -1) || (end_index == -1) || (current_grapheme >= true_range.end) then Nothing else
                                ranges.append (Range_Data start_index end_index)
                                ## We advance by step-1, because we already advanced by
                                   one grapheme when looking for the end of the previous
                                   one.
                                @Tail_Call go (iterator.next true_range.step-1) current_grapheme+true_range.step

                        go (iterator.next true_range.start) true_range.start
                        Codepoint_Ranges_Data ranges.to_vector is_sorted_and_distinct=True

## PRIVATE
   Returns an array of UTF-16 code-unit indices corresponding to the beginning
   and end of each consecutive grapheme cluster.

   These indices are consistent with the vector returned by `Text.char_vector`.
character_ranges text =
    iterator = BreakIterator.getCharacterInstance
    iterator.setText text
    ranges = Vector.new_builder
    go prev nxt = if nxt == -1 then Nothing else
        ranges.append (Range_Data prev nxt)
        @Tail_Call go nxt iterator.next
    go iterator.first iterator.next
    ranges.to_vector

## PRIVATE
batch_resolve_indices_or_ranges text descriptors = Panic.recover [Index_Out_Of_Bounds_Error_Data, Illegal_Argument_Error_Data] <|
    ## This is pre-computing the ranges for all characters in the string, which
       may be much more than necessary, for example if all ranges reference only
       the beginning of the string. In the future we may want to replace this
       with a lazy data structure which advances the break iterator only on
       demand, using a Vector.Builder to cache any prior ranges for random
       access.
    characters = character_ranges text
    ranges = Vector.new_builder
    descriptors.each descriptor->
        case descriptor of
            Integer ->
                ranges.append (Panic.rethrow <| characters.at descriptor)
            Range_Data _ _ _ ->
                if descriptor.is_empty then Range_Data 0 0 else
                    true_range = normalize_range descriptor characters.length
                    case true_range.step == 1 of
                        True ->
                            first_grapheme = Panic.rethrow <| characters.at true_range.start
                            last_grapheme = Panic.rethrow <| characters.at true_range.end-1
                            ranges.append (Range_Data first_grapheme.start last_grapheme.end)
                        False ->
                            if true_range.start >= characters.length then
                                Panic.throw (Index_Out_Of_Bounds_Error_Data true_range.start characters.length)
                            true_range.to_vector.each ix->
                                ranges.append (Panic.rethrow <| characters.at ix)
    Codepoint_Ranges_Data ranges.to_vector is_sorted_and_distinct=False

## PRIVATE
panic_on_non_positive_step =
    Panic.throw (Illegal_Argument_Error_Data "Range step must be positive.")

## PRIVATE
   Ensures that the range is valid and trims it to the length of the collection.
normalize_range range length =
    if range.step <= 0 then panic_on_non_positive_step
    # We may add support for negative indices in the future.
    if (range.start < 0) || (range.end < 0) then
        Panic.throw (Illegal_Argument_Error_Data "Ranges with negative indices are not supported for indexing.")
    if (range.start >= length) then
        Panic.throw (Index_Out_Of_Bounds_Error_Data range.start length)
    if range.end >= length then Range_Data range.start length range.step else
        range

import project.Data.Locale.Locale
import project.Data.Text.Case_Sensitivity.Case_Sensitivity
import project.Data.Text.Text
import project.Nothing.Nothing
from project.Data.Boolean import Boolean, False, True

type Text_Ordering
    ## Specifies the ordering of text values.

       For the In-memory backend, the default ordering is case-sensitive. In
       databases, the default ordering depends on the database configuration.

       Arguments:
       - sort_digits_as_numbers: Sort digits in the text as numbers. Setting
         this to `True` results in a "Natural" ordering.
    Default (sort_digits_as_numbers:Boolean=False)

    ## Case sensitive ordering of values.

       It will ensure case-sensitive ordering regardless of backend defaults.
       This may make database queries more complicated and may result in being
       unable to rely on existing indices, thus potentially making the queries
       much slower. The `Default` ordering is preferred wherever possible.

       Arguments:
       - sort_digits_as_numbers: Sort digits in the text as numbers. Setting
         this to `True` results in a "Natural" ordering.
    Case_Sensitive (sort_digits_as_numbers:Boolean=False)

    ## Case insensitive ordering of values.

       It will ensure case-insensitive ordering regardless of backend defaults.
       This may make database queries more complicated and may result in being
       unable to rely on existing indices, thus potentially making the queries
       much slower. The `Default` ordering is preferred wherever possible.

       Arguments:
       - sort_digits_as_numbers: Sort digits in the text as numbers. Setting
         this to `True` results in a "Natural" ordering.
    @locale Locale.default_widget
    Case_Insensitive (locale:Locale=Locale.default) (sort_digits_as_numbers:Boolean=False)

    ## PRIVATE
       Returns this ordering's case sensitivity setting.
    case_sensitivity : Case_Sensitivity
    case_sensitivity self = case self of
        Text_Ordering.Default _ -> Case_Sensitivity.Default
        Text_Ordering.Case_Sensitive _ -> Case_Sensitivity.Sensitive
        Text_Ordering.Case_Insensitive locale _ -> Case_Sensitivity.Insensitive locale

    ## PRIVATE
       Convert Text_Ordering to a friendly string.
    to_display_text : Text
    to_display_text self =
        self.case_sensitivity.to_display_text + if self.sort_digits_as_numbers then " (Natural Order)" else ""

import project.Any.Any
import project.Data.Text.Case_Sensitivity.Case_Sensitivity
import project.Data.Text.Regex.Regex
import project.Data.Text.Text
import project.Data.Vector.Vector
import project.Error.Error
import project.Errors.Illegal_Argument.Illegal_Argument
import project.Function.Function
import project.Meta
import project.Nothing.Nothing
from project.Data.Boolean import Boolean, False, True
from project.Data.Filter_Condition.Filter_Condition import all
from project.Data.Text.Extensions import all
from project.Metadata import make_single_choice, Widget

polyglot java import org.enso.base.Regex_Utils

type Filter_Condition
    ## Is less than a value (or another column, in case of Table operations)?
    Less than:Any

    ## Is less than or equal to a value (or another column, in case of Table operations)?
    Equal_Or_Less than:Any

    ## Is equal to a value (or another column, in case of Table operations)?
    Equal to:Any

    ## Is greater than or equal to a value (or another column, in case of Table operations)?
    Equal_Or_Greater than:Any

    ## Is greater than a value (or another column, in case of Table operations)?
    Greater than:Any

    ## Is not equal to a value (or another column, in case of Table operations)?
    Not_Equal to:Any

    ## Is between (inclusive) two values (or columns, in case of Table operations)?
    Between lower:Any upper:Any

    ## Does the value start with a prefix (Text only)?

       ? Table Operations
         It accepts a Text value to check if the value contains it. In case of
         Table operations, it can accept another column - then the corresponding
         values from the source column and the provided column are checked.
    Starts_With (prefix : Text | Any) (case_sensitivity:Case_Sensitivity=Case_Sensitivity.Default)

    ## Does the value end with a suffix (Text only)?

       ? Table Operations
         It accepts a Text value to check if the value contains it. In case of
         Table operations, it can accept another column - then the corresponding
         values from the source column and the provided column are checked.
    Ends_With (suffix : Text | Any) (case_sensitivity:Case_Sensitivity=Case_Sensitivity.Default)

    ## Does the value contain the substring (Text only)?

       ? Table Operations
         It accepts a Text value to check if the value contains it. In case of
         Table operations, it can accept another column - then the corresponding
         values from the source column and the provided column are checked.
    Contains (substring : Text | Any) (case_sensitivity:Case_Sensitivity=Case_Sensitivity.Default)

    ## Is the substring not present in the value (Text only)?

       ? Table Operations
         It accepts a Text value to check if the value contains it. In case of
         Table operations, it can accept another column - then the corresponding
         values from the source column and the provided column are checked.
    Not_Contains (substring : Text | Any) (case_sensitivity:Case_Sensitivity=Case_Sensitivity.Default)

    ## Is equal to Nothing?
    Is_Nothing

    ## Is not equal to Nothing?
    Not_Nothing

    ## Is the value equal to True (Boolean only)?
    Is_True

    ## Is the value equal to False (Boolean only)?
    Is_False

    ## Is equal to "" or Nothing (Text only)?
    Is_Empty

    ## Is not equal to "" and Nothing (Text only)?
    Not_Empty

    ## Does the value match the SQL pattern (Text only)?

       The pattern is interpreted according to the standard SQL convention:
       - the `%` character matches any sequence of characters,
       - the `_` character matches any single character,
       - any other character is matched literally.

       ? Table Operations
         It accepts a Text value to check if the value contains it. In case of
         Table operations, it can accept another column - then the corresponding
         values from the source column and the provided column are checked.

       ! Known Limitations.
         The Truffle regex engine does not transparently handle normalization.
         Due to this limitation, Unicode normalization has been disabled for
         this function, so beware that some equivalent graphemes like 'ś' and
         's\u0301' will not be matched.
    Like (pattern : Text | Any)

    ## Does the value not match the SQL pattern (Text only)?

       The pattern is interpreted according to the standard SQL convention:
       - the `%` character matches any sequence of characters,
       - the `_` character matches any single character,
       - any other character is matched literally.

       ? Table Operations
         It accepts a Text value to check if the value contains it. In case of
         Table operations, it can accept another column - then the corresponding
         values from the source column and the provided column are checked.

       ! Known Limitations.
         The Truffle regex engine does not transparently handle normalization.
         Due to this limitation, Unicode normalization has been disabled for
         this function, so beware that some equivalent graphemes like 'ś' and
         's\u0301' will not be matched.
    Not_Like pattern:Text|Any

    ## Is the value contained in `values`?

       ? Table Operations
         It accepts a `Vector` of values. In case of Table operations, it can
         also accept another column - then it acts as if `column.to_vector` was
         passed - i.e. every element of the original table's column is checked
         if it is contained in the provided column. The columns can have
         different lengths.

         Using Columns can be particularly useful for Database operations, as
         uploading a temporary table and using its column for an `Is_In` check
         will likely be faster than using the vector directly.
    Is_In values:Vector|Any

    ## Is the value not contained in `values`?

       ? Table Operations
         It accepts a `Vector` of values. In case of Table operations, it can
         also accept another column - then it acts as if `column.to_vector` was
         passed - i.e. every element of the original table's column is checked
         if it is contained in the provided column. The columns can have
         different lengths.

         Using Columns can be particularly useful for Database operations, as
         uploading a temporary table and using its column for an `Not_In` check
         will likely be faster than using the vector directly.
    Not_In values:(Vector | Any)

    ## Converts a `Filter_Condition` condition into a predicate taking an
       element and returning a value indicating whether the element should be
       accepted by the filter.
    to_predicate : (Any -> Boolean)
    to_predicate self = case self of
        Less value -> <value
        Equal_Or_Less value -> <=value
        Equal value -> ==value
        Equal_Or_Greater value -> >=value
        Greater value -> >value
        Not_Equal value -> !=value
        Between lower upper -> elem ->
            (lower <= elem) && (elem <= upper)
        Starts_With prefix case_sensitivity -> _.starts_with prefix case_sensitivity
        Ends_With suffix case_sensitivity -> _.ends_with suffix case_sensitivity
        Contains substring case_sensitivity -> _.contains substring case_sensitivity
        Not_Contains substring case_sensitivity -> v-> v.contains substring case_sensitivity . not
        Is_Nothing -> elem -> case elem of
            Nothing -> True
            _ -> False
        Not_Nothing -> elem -> case elem of
            Nothing -> False
            _ -> True
        Is_True -> ==True
        Is_False -> ==False
        Is_Empty -> elem -> case elem of
            Nothing -> True
            "" -> True
            _ -> False
        Not_Empty -> elem -> case elem of
            Nothing -> False
            "" -> False
            _ -> True
        Like sql_pattern ->
            regex = sql_like_to_regex sql_pattern
            regex.matches
        Not_Like sql_pattern ->
            regex = sql_like_to_regex sql_pattern
            elem -> regex.matches elem . not
        ## TODO once we have proper hashing we could create a hashmap and
           answer quicker, currently we need to do a full scan for each element.
        Is_In values -> values.contains
        Not_In values -> elem -> values.contains elem . not

    ## PRIVATE
       Convert to a display representation of this Filter_Condition.
    to_display_text : Text
    to_display_text self =
        render_case case_sensitivity =
            if case_sensitivity == Case_Sensitivity.Default then "" else " Case " + case_sensitivity.to_display_text

        condition = case self of
            Less value -> "<" + value.to_display_text
            Equal_Or_Less value -> "<=" + value.to_display_text
            Equal value -> "==" + value.to_display_text
            Equal_Or_Greater value -> ">=" + value.to_display_text
            Greater value -> ">" + value.to_display_text
            Not_Equal value -> "!=" + value.to_display_text
            Between lower upper -> "Between " + lower.to_display_text + " And " + upper.to_display_text
            Starts_With prefix case_sensitivity -> "Starts With " + prefix.to_display_text + (render_case case_sensitivity)
            Ends_With suffix case_sensitivity -> "Ends With " + suffix.to_display_text + (render_case case_sensitivity)
            Contains substring case_sensitivity -> "Contains " + substring.to_display_text + (render_case case_sensitivity)
            Not_Contains substring case_sensitivity -> "Not Contains " + substring.to_display_text + (render_case case_sensitivity)
            Is_Nothing -> "is Nothing"
            Not_Nothing -> "is Not Nothing"
            Is_True -> "is True"
            Is_False -> "is False"
            Is_Empty -> "is Empty"
            Not_Empty -> "is Not Empty"
            Like sql_pattern -> "Like " + sql_pattern.to_display_text
            Not_Like sql_pattern -> "Not Like " + sql_pattern.to_display_text
            Is_In values -> "is in " + values.to_display_text
            Not_In values -> "is not in " + values.to_display_text
        "Filter Condition: " + condition

    ## PRIVATE
       Creates a Single_Choice Widget for delimiters.
    default_widget : Boolean -> Boolean -> Boolean -> Boolean -> Widget
    default_widget include_comparable=True include_text=True include_boolean=True include_nullable=True =
        options_builder = Vector.new_builder
        if include_comparable then
            options_builder.append "Less"
            options_builder.append "Equal_Or_Less"
            options_builder.append "Equal"
            options_builder.append "Equal_Or_Greater"
            options_builder.append "Greater"
            options_builder.append "Not_Equal"
            options_builder.append "Between"
        if include_boolean then
            options_builder.append "Is_True"
            options_builder.append "Is_False"
        if include_nullable then
            options_builder.append "Is_Nothing"
            options_builder.append "Not_Nothing"
        if include_text then
            options_builder.append "Is_Empty"
            options_builder.append "Not_Empty"
            options_builder.append "Starts_With"
            options_builder.append "Ends_With"
            options_builder.append "Contains"
            options_builder.append "Not_Contains"
            options_builder.append "Like"
            options_builder.append "Not_Like"
        options_builder.append "Is_In"
        options_builder.append "Not_In"

        options = options_builder.to_vector.map constructor_name->
            name = constructor_name.replace "_" " "
            code = "(Filter_Condition." + constructor_name + ")"
            [name, code]
        make_single_choice options


## PRIVATE
sql_like_to_regex sql_pattern =
    regex_pattern = Regex_Utils.sql_like_pattern_to_regex sql_pattern
    Regex.compile regex_pattern

## PRIVATE
unify_condition_or_predicate : Filter_Condition | (Any -> Boolean) -> (Any -> Boolean)
unify_condition_or_predicate (condition_or_predicate : Filter_Condition | Function) =
    case condition_or_predicate of
        condition : Filter_Condition -> condition.to_predicate
        predicate -> handle_constructor_missing_arguments predicate predicate

## PRIVATE
unify_condition_predicate_or_element condition =
    case condition of
        condition : Filter_Condition -> condition.to_predicate
        predicate : Function -> handle_constructor_missing_arguments predicate predicate
        element -> (== element)

## PRIVATE
   Checks if the given `function` is actually a `Filter_Condition` constructor
   that is just missing arguments. If so, it will report a more friendly error.
   Otherwise it will run the `continuation`.
handle_constructor_missing_arguments function ~continuation =
    is_filter_condition_constructor = case Meta.meta function of
        constructor : Meta.Constructor ->
            constructor.declaring_type == Meta.meta Filter_Condition
        ## Special case for multi-argument constructors with some but not all
           arguments applied.

           For example, `Meta.meta (Filter_Condition.Between 10)` will return a `Meta.Primitive`.
           We rely on its text representation being of the form `Filter_Condition.Between[Filter_Condition.enso:41-343]`.
        _ : Meta.Primitive ->
            text = function.to_text
            text.starts_with "Filter_Condition." && text.contains "[Filter_Condition.enso:"
        _ -> False
    if is_filter_condition_constructor.not then continuation else
        message = "Got a Filter_Condition constructor without all required arguments provided. Please provide the missing arguments."
        Error.throw (Illegal_Argument.Error message)

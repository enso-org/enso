from Standard.Base import all

## A type representing computations that may fail.
type Maybe

    ## No contained value.
    Nothing

    ## A value.

       Arguments:
       - value: The contained value in the maybe.

       > Example
         Construct a some value.

             import Standard.Base.Data.Maybe

             example_some = Maybe.Some "yes!"
    type Some value

    ## Applies the provided function to the contained value if it exists,
       otherwise returning the provided default value.

       Arguments:
       - default: The value to return if `self` is Nothing. This value is lazy
         and hence will not execute any provided computation unless it is used.
       - function: The function to execute on the value inside the `Some`, if it
         is a just.

       > Example
         Apply a function over a Some value to get 4.

             import Standard.Base.Data.Maybe

             example_maybe = Maybe.Some 2 . maybe 0 *2
    maybe : Any -> (Any -> Any) -> Any
    maybe ~default function = case self of
        Nothing -> default
        Some val -> function val

    ## Check if the maybe value is `Some`.

       > Example
         Check if `Nothing` is `Some`.

             import Standard.Base.Data.Maybe

             example_is_some = Maybe.Some "yes!" . is_some
    is_some : Boolean
    is_some = self.is_nothing.not

import project.Any.Any
import project.Data.Numbers.Decimal
import project.Data.Numbers.Integer
import project.Data.Numbers.Number
import project.Errors.Common.Incomparable_Values
import project.Errors.Common.Type_Error
import project.Error.Error
import project.Errors.Illegal_State.Illegal_State
import project.Errors.Unimplemented.Unimplemented
import project.Nothing.Nothing
import project.Meta
import project.Meta.Atom
import project.Panic.Panic
from project.Data.Boolean import all

## Provides custom ordering, equality check and hash code for types that need it.

    The Enso runtime system offers default implementation of _equality_
    as well as capability to _compute hash code_ (e.g. for use in `Map`) automatically.
    The default implementation is sufficient for most of the programming activities.
    Especially when defining new type and its constructors, they get sensible
    implementation of both functions.

    Should there be a need to redefine the default implementation, here is a way:
    Define conversion function `Comparable.from` for your `type` and return pointer to
    another `type` that satisfies the following definition:

    ```
    type Comparator T
        compare : T -> T -> (Ordering|Nothing)
        hash : T -> Integer
    ```

    Every type must provide exactly one comparator, i.e., the method of form
    `Comparable.from (_:My_Type)` must return the same comparator type for every
    value. Note that there is an implicit `Default_Comparator` assigned for every
    type by default.

    A comparator has to implement `compare` and `hash` methods. `compare x y` method
    returns `Ordering.Less` if x is ordered before y, `Ordering.Equal` if x is equal
    to y, `Ordering.Greater` if x is ordered after y, and `Nothing` if x and y are
    not equal, and their relative ordering cannot be determined.

    The signature of the `compare` method is designed so that it enables comparators
    to provide a _partial ordering_ for a specific type.

    A hash code must be provided for all the objects, therefore, the type signature
    of `hash` method does not allow `Nothing` as a return value.

    The runtime expects the following semantics for all the comparators:
    - Hash consistency:
      - If x == y then hash(x) == hash(y)
      - If hash(x) != hash(y) then x != y
    - Consistency: if x == y then x == y for all the subsequent invocations.
    - Symmetry: if x == y then y == x
    - Reflexivity: x == x
    - Transitivity: if x < y and y < z then x < z
    - Antisymmetry: if x > y then y < x

    Users are responsible for the compliance to the aforementioned semantics.
    Should the semantics be violated, an unexpected behavior may be encountered, e.g.,
    `Array.sort` may return unexpected results or fail with `Incomparable_Values`.


    > Example
        Comparator for an unordered Pair `UPair`. In this example, we can see an
        implementation of the `hash` method that delegates to hash methods of
        fields.

        ```
        type UPair
            Value x y

        type UPair_Comparator
            compare pair1 pair2 =
                if pair1.x == pair2.x && pair1.y == pair2.y then Ordering.Equal else
                    if pair1.x == pair2.y && pair1.y == pair2.x then Ordering.Equal else
                        Nothing

            hash upair =
                x_comp = Comparable.from upair.x
                y_comp = Comparable.from upair.y
                (x_comp.hash upair.x) + (y_comp.hash upair.y)

        Comparable.from (_ : UPair) = UPair_Comparator
        ```

    > Example
        Representation of _rational numbers_ as a pair of integers needs a
        special equality. Here is a way to define it:

        ```
        type Rational
            Fraction (numerator:Integer) (denominator:Integer)

        Comparable.from (_:Rational) = Rational_Ordering

        type Rational_Ordering
            compare self r1 r2 =
                v1 = r1.numerator * r2.denominator
                v2 = r2.numerator * r1.denominator
                if v1 < v2 then Ordering.Less else
                    if v1 > v2 then Ordering.Greater else
                        Ordering.Equal
            hash self r1 = 42 # or something better
        ```

        By defining the `Rational_Ordering` and making it available via
        `Comparable.from (_:Rational)` method, all parts of the Enso system will use
        the custom comparator whenever equality or hash code is needed.
@Builtin_Type
type Comparable
    ## PRIVATE
       Called as a callback from internal engine code for an atom with a custom
       comparator. It is assumed that the given atom has a custom comparator, that is
       a comparator different than `Default_Comparator`.
    hash_callback : Atom -> Integer
    hash_callback atom = (Comparable.from atom).hash atom

    ## PRIVATE
       A callback allowing to compare two atoms with a custom comparator.
    compare_callback : Atom -> Atom -> Integer | Nothing
    compare_callback atom that =
        ordering = Ordering.compare atom that
        if ordering.is_error then Nothing else ordering.to_sign

    ## PRIVATE
       A custom comparator is any comparator that is different than the
       default ones.
    has_custom_comparator : Atom -> Boolean
    has_custom_comparator atom =
        comp = Comparable.from atom
        (comp.is_a Default_Comparator).not

## Default implementation of a _comparator_.
@Builtin_Type
type Default_Comparator
    compare : Any -> Any -> (Ordering|Nothing)
    compare x y =
        case Comparable.equals_builtin x y of
            True -> Ordering.Equal
            False ->
                case Comparable.less_than_builtin x y of
                    Nothing -> Nothing
                    True -> Ordering.Less
                    False ->
                        case Comparable.less_than_builtin y x of
                            Nothing -> Nothing
                            True -> Ordering.Greater
                            False -> Nothing

    hash : Number -> Integer
    hash x = Comparable.hash_builtin x


Comparable.from (_:Any) = Default_Comparator


## Types representing the ordering of values.
@Builtin_Type
type Ordering
    ## A representation that the first value orders as less than the second.
    Less

    ## A representation that the first value orders as equal to the second.
    Equal

    ## A representation that the first value orders as greater than the second.
    Greater

    ## Compares to values and returns an Ordering
    compare : Any -> Any -> Ordering ! (Incomparable_Values | Type_Error)
    compare x y =
        if x < y then Ordering.Less else
            if x == y then Ordering.Equal else
                if x > y then Ordering.Greater else
                    Error.throw (Incomparable_Values.Error x y)

    ## Converts the ordering to the signed notion of ordering based on integers.

       > Example
         Converting equal ordering to a signed number.

             import Standard.Base.Data.Ordering

             example_to_sign = Ordering.Equal.to_sign
    to_sign : Integer
    to_sign self = case self of
        Ordering.Less -> -1
        Ordering.Greater -> 1
        Ordering.Equal -> 0

    ## A lexicographical comparison.

    and_then : Ordering -> Ordering
    and_then self ~other = case self of
        Ordering.Less -> Ordering.Less
        Ordering.Equal -> other
        Ordering.Greater -> Ordering.Greater

    ## Converts a sign-based representation of ordering to Enso's native ordering.

       Arguments:
       - sign: The number representing the relative ordering of two entities.

       > Example
         Convert the number 0 to an Enso ordering.

             Ordering.from_sign 0
    from_sign : Integer -> Ordering
    from_sign sign = if sign == 0 then Ordering.Equal else
        if sign > 0 then Ordering.Greater else Ordering.Less

type Ordering_Comparator
    compare x y = (Comparable.from x.to_sign).compare x.to_sign y.to_sign
    hash x = x.to_sign

Comparable.from (_:Ordering) = Ordering_Comparator

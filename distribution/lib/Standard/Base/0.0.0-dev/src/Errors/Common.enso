import project.Data.Text.Text
import project.Error.Error
import project.Meta
import project.Nothing.Nothing
import project.Panic.Panic
from project.Data.Boolean import True, False

polyglot java import java.lang.ClassCastException
polyglot java import org.enso.base.CompareException

## An error indicating that no value was found.
type Not_Found

@Builtin_Type
type Index_Out_Of_Bounds
    ## PRIVATE
       UNSTABLE

       An error indicating that a requested index was out of bounds of a collection.

       Arguments:
       - index: The requested index.
       - length: The length of the collection.
    Error index length

    ## PRIVATE
       Pretty prints an index out of bounds error.
    to_display_text : Text
    to_display_text self =
        "The index " + self.index.to_text + " is out of bounds in a collection of length " + self.length.to_text + "."

@Builtin_Type
type Syntax_Error
    ## PRIVATE
       The runtime representation of a syntax error.

       Arguments:
       - message: A description of the erroneous syntax.
    Error message

@Builtin_Type
type Type_Error
    ## PRIVATE
       The runtime representation of a type error.

       Arguments:
       - expected: The expected type at the error location.
       - actual: The actual type at the error location.
       - name: The name of the argument whose type is mismatched.
    Error expected actual name

@Builtin_Type
type Compile_Error
    ## PRIVATE
       The runtime representation of a compilation error.

       Arguments:
       - message: A description of the erroneous state.
    Error message

@Builtin_Type
type Inexhaustive_Pattern_Match
    ## PRIVATE
       The error thrown when a there is no pattern to match on the scrutinee.

       Arguments:
       - scrutinee: The scrutinee that failed to match.
    Error scrutinee

@Builtin_Type
type Arity_Error
    ## PRIVATE
       The error thrown when the number of arguments provided to an operation
       does not match the expected number of arguments.

       Arguments:
       - expected_min: the minimum expected number of arguments.
       - expected_max: the maximum expected number of arguments.
       - actual: the actual number of arguments passed.
    Error expected_min expected_max actual

@Builtin_Type
type Uninitialized_State
    ## PRIVATE
       The error thrown when the program attempts to read from a state slot that has
       not yet been initialized.

       Arguments:
       - key: The key for the state slot that was not initialized.
    Error key

@Builtin_Type
type No_Such_Method
    ## PRIVATE
       The error thrown when the specified symbol does not exist as a method on
       the target.

       Arguments:
       - target: The target on which the attempted method call was performed.
       - symbol: The symbol that was attempted to be called on target.
    Error target symbol

    ## ADVANCED
       UNSTABLE

       Returns the method name of the method that could not be found.

       > Example
         Getting the method name from a no such method error.

             import Standard.Examples

             example_method_name =
                 error = Examples.no_such_method
                 error.method_name
    method_name : Text
    method_name self =
        Meta.meta self.symbol . name

@Builtin_Type
type No_Such_Field
    ## PRIVATE
       The error thrown when the specified symbol does not exist as a field on
       the value.

       Arguments:
       - target: The target on which the field was accessed.
       - field_name: The name of the field that was being accessed.
    Error value field_name

## An error that occurs when the enso_project function is called in a file
   that is not part of a project.
@Builtin_Type
type Module_Not_In_Package_Error

@Builtin_Type
type Arithmetic_Error
    ## PRIVATE
       An error for when an erroneous arithmetic computation takes place.

       Arguments:
       - message: A description of the error condition.
    Error message

@Builtin_Type
type Incomparable_Values
    ## An error that indicates that the two values are not comparable

       Arguments:
       - left: The left value (first operand)
       - right: The right value (second operand)
    Error left right

    ## PRIVATE
       Convert the Incomparable_Values error to a human-readable format.
    to_display_text : Text
    to_display_text self =
        case self.left.is_nothing && self.right.is_nothing of
            True -> "Incomparable_Values.Error"
            False -> "Cannot compare `" + self.left.to_text + "` with `" + self.right.to_text + "`"

    ## PRIVATE
       ADVANCED
       Catches possible errors from comparing values and throws an
       `Incomparable_Values` if any occur.
    handle_errors ~function =
        handle t = Panic.catch t handler=(_-> Error.throw (Incomparable_Values.Error Nothing Nothing))
        handle_cmp_exc = Panic.catch CompareException handler=(exc-> Error.throw (Incomparable_Values.Error exc.getLeftOperand exc.getRightOperand))

        handle ClassCastException <| handle No_Such_Method <| handle Type_Error <| handle Unsupported_Argument_Types <| handle_cmp_exc <|
            function

@Builtin_Type
type Invalid_Array_Index
    ## PRIVATE
       An error that occurs when a program requests a read from an array index
       that is out of bounds in the array.

       Arguments:
       - array: The array in which the index was requested.
       - index: The index that was out of bounds.
    Error array index

@Builtin_Type
type Not_Invokable
    ## PRIVATE
       An error that occurs when an object is used as a function in a function
       call, but it cannot be called.

       Arguments:
       - target: The called object.
    Error target

@Builtin_Type
type Unsupported_Argument_Types
    ## PRIVATE
       An error that occurs when arguments used in a function call are invalid
       types for the function.

       Arguments:
       - arguments: The passed arguments.
       - message: A detailed message.
    Error arguments message

@Builtin_Type
type Module_Does_Not_Exist
    ## PRIVATE
       An error that occurs when the specified module cannot be found.

       Arguments:
       - name: The module searched for.
    Error name

@Builtin_Type
type Invalid_Conversion_Target
    ## PRIVATE
       An error that occurs when the specified value cannot be converted to a given type

       Arguments:
       - target: the type trying to be converted to.
    Error target

@Builtin_Type
type No_Such_Conversion
    ## PRIVATE
       An error that occurs when the conversion from one type to another does not exist

       Arguments:
       - target: the type trying to be converted to.
       - that: the value to be converted.
       - conversion: the conversion that was attempted.
    Error target that conversion

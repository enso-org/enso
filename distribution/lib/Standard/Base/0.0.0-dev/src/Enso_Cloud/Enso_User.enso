import project.Data.Json.JS_Object
import project.Data.Text.Text
import project.Data.Vector.Vector
import project.Enso_Cloud.Enso_File.Enso_Asset_Type
import project.Enso_Cloud.Enso_File.Enso_File
import project.Enso_Cloud.Internal.Utils
import project.Error.Error
import project.Errors.Illegal_Argument.Illegal_Argument
import project.Network.HTTP.HTTP
import project.Network.HTTP.HTTP_Method.HTTP_Method
import project.Nothing.Nothing
from project.Data.Boolean import Boolean, False, True
from project.Enso_Cloud.Public_Utils import get_optional_field, get_required_field

type Enso_User
    ## PRIVATE
       Represents a user within Enso Cloud.

       Fields:
       - name: The user's name.
       - email: The user's email address.
       - id: The user's unique ID.
       - is_enabled: Whether the user is enabled.
       - root_directory_id: The ID of the user's root directory.
    User name:Text email:Text id:Text is_enabled:Boolean root_directory_id:Text

    ## ICON people
       Fetch the current user.
    current : Enso_User
    current =
        json = Utils.http_request_as_json HTTP_Method.Get (Utils.cloud_root_uri + "users/me")
        Enso_User.from json

    ## ICON people
       Lists all known users.
    list : Vector Enso_User
    list =
        json = Utils.http_request_as_json HTTP_Method.Get (Utils.cloud_root_uri + "users")
        users = get_required_field "users" json
        users.map (user-> Enso_User.from user)

    ## Flushes all cloud caches.
       For example, it will flush cached secrets, ensuring that new values will
       be fetched the next time the secrets are accessed.
    flush_caches : Nothing
    flush_caches = Utils.flush_caches

    ## PRIVATE
       The user's organization name. Paths to Cloud assets start with this name.
    organization_name self -> Text =
        # TODO use https://github.com/enso-org/cloud-v2/pull/1140 to get the org name
        org_name = Nothing
        org_name.if_nothing self.name

## PRIVATE
Enso_User.from (that:JS_Object) =
    root_directory_id = get_required_field "rootDirectoryId" that expected_type=Text
    is_enabled = get_optional_field "isEnabled" that if_missing=True expected_type=Boolean
    name = get_required_field "name" that expected_type=Text
    email = get_required_field "email" that expected_type=Text
    id = get_required_field "id" that expected_type=Text
    Enso_User.User name email id root_folder is_enabled root_directory_id

private

import project.Data.Json.JS_Object
import project.Data.Text.Text
import project.Data.Text.Text_Sub_Range.Text_Sub_Range
import project.Data.Time.Date_Time.Date_Time
import project.Data.Time.Date_Time_Formatter.Date_Time_Formatter
import project.Data.Vector.Vector
import project.Enso_Cloud.Enso_User.Enso_User
import project.Enso_Cloud.Enso_File.Enso_Asset_Type
import project.Enso_Cloud.Enso_File.Enso_File
import project.Enso_Cloud.Internal.Utils
import project.Error.Error
import project.Errors.Common.Not_Found
import project.Errors.File_Error.File_Error
import project.Errors.Illegal_Argument.Illegal_Argument
import project.Errors.Unimplemented.Unimplemented
import project.Network.HTTP.HTTP_Method.HTTP_Method
from project.Data.Boolean import Boolean, False, True
from project.Data.Text.Extensions import all
from project.Enso_Cloud.Public_Utils import get_required_field

## PRIVATE
   Currently, most of the Cloud API relies on asset IDs and not paths.
   This object is a proxy for an existing asset within the cloud - whenever we
   perform an operation that operates on IDs, we can fetch this helper and then
   use it to perform the operation.
type Existing_Enso_Asset
    ## PRIVATE
       Represents an existing asset within the Enso cloud.
    Value title:Text id:Text asset_type:Enso_Asset_Type

    ## PRIVATE
       Target URI for the api
    internal_uri self -> Text = case self.id of
        "" -> if self.asset_type == Enso_Asset_Type.Directory then Utils.directory_api else
            Error.throw (Illegal_Argument.Error "Invalid ID for a "+self.asset_type.to_text+".")
        _ -> case self.asset_type of
            Enso_Asset_Type.Directory -> Utils.directory_api + "/" + self.id
            Enso_Asset_Type.File -> Utils.files_api + "/" + self.id
            Enso_Asset_Type.Project -> Utils.projects_api + "/" + self.id
            Enso_Asset_Type.Data_Link -> Utils.datalinks_api + "/" + self.id
            Enso_Asset_Type.Secret -> Error.throw (Illegal_Argument.Error "Secrets cannot be accessed directly.")

    ## PRIVATE
    asset_uri self -> Text = Utils.assets_api + "/" + self.id

    ## PRIVATE
       Fetches the basic information about a file from the Cloud endpoint.
    get_file_description self -> JS_Object =
        Utils.http_request_as_json HTTP_Method.Get self.internal_uri

    ## PRIVATE
       The returned URL may be valid only for a very short time, so it should not be
       stored anywhere.
    get_download_url self -> Text =
        presigned_url = self.get_file_description |> get_required_field "url" expected_type=Text
        presigned_url

    ## PRIVATE
       Fetches the basic information about an existing file from the Cloud.
       It will fail if the file does not exist.
    get_asset_reference_for (file : Enso_File) -> Existing_Enso_Asset ! File_Error =
        # TODO we will get this from the `resolve_path` endpoint, for now though we use manual resolution
        current_user = Enso_User.current
        if file.path.organization_name != current_user.organization_name then Unimplemented.throw "Currently only resolving paths for the current user is supported." else
            root = Existing_Enso_Asset.Value "" current_user.root_directory_id Enso_Asset_Type.Directory
            file.path.path_segments.fold root current_dir-> segment->
                children = current_dir.list_directory . catch Illegal_Argument _->
                    Error.throw (Illegal_Argument.Error current_dir.title+" is not a directory.")
                children.find c-> c.title == segment . catch Not_Found _->
                    Error.throw (File_Error.Not_Found file)

    ## PRIVATE
    is_directory self = self.asset_type == Enso_Asset_Type.Directory

    ## PRIVATE
    is_regular_file self = self.asset_type == Enso_Asset_Type.File

    ## PRIVATE
    list_directory self =
        if self.asset_type != Enso_Asset_Type.Directory then Error.throw (Illegal_Argument.Error "Only directories can be listed.") else
             response = Utils.http_request_as_json HTTP_Method.Get self.internal_uri
             assets = get_required_field "assets" response expected_type=Vector
             assets.map Existing_Enso_Asset.from_json

    ## PRIVATE
    from_json json =
        title = get_required_field "title" json expected_type=Text
        id = get_required_field "id" json expected_type=Text
        #org = json.get "organizationId" ""
        asset_type = (id.take (Text_Sub_Range.Before "-")):Enso_Asset_Type
        Existing_Enso_Asset.Value title id asset_type

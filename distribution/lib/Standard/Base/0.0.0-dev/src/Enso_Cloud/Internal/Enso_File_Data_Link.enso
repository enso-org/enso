private

import project.Any.Any
import project.Data.Text.Text
import project.Data.Vector.Vector
import project.Enso_Cloud.Enso_File.Enso_File
import project.Errors.Problem_Behavior.Problem_Behavior
import project.Nothing.Nothing
import project.System.File.Generic.Writable_File.Writable_File
import project.System.File_Format.Auto_Detect
import project.System.Input_Stream.Input_Stream
import project.System.Output_Stream.Output_Stream
from project.Enso_Cloud.Data_Link_Capabilities import Data_Link_With_Input_Stream, Data_Link_With_Output_Stream, Writable_Data_Link
from project.Enso_Cloud.Data_Link_Helpers import parse_format
from project.Enso_Cloud.Public_Utils import get_optional_field, get_required_field


## PRIVATE
type Enso_File_Data_Link
    ## PRIVATE
    private Value (path : Text) format_json

    ## PRIVATE
    parse json source -> Enso_File_Data_Link =
        _ = source
        path = get_required_field "path" json expected_type=Text
        format_json = get_optional_field "format" json
        Enso_File_Data_Link.Value path format_json

    ## PRIVATE
    read self (format = Auto_Detect) (on_problems : Problem_Behavior) =
        effective_format = if format != Auto_Detect then format else parse_format self.format_json
        self.as_file.read effective_format on_problems

    ## PRIVATE
    as_file self -> Enso_File =
        Enso_File.new self.path

    ## PRIVATE
    with_input_stream self (open_options : Vector) (action : Input_Stream -> Any) -> Any =
        self.as_file.with_input_stream open_options action

    ## PRIVATE
    with_output_stream self (open_options : Vector) (action : Output_Stream -> Any) -> Any =
        self.as_file.with_output_stream open_options action

    ## PRIVATE
    as_writable_file self -> Writable_File = Writable_File.from self.as_file

## PRIVATE
Data_Link_With_Input_Stream.from (that:Enso_File_Data_Link) = Data_Link_With_Input_Stream.Value that

## PRIVATE
Data_Link_With_Output_Stream.from (that:Enso_File_Data_Link) = Data_Link_With_Output_Stream.Value that

## PRIVATE
Writable_Data_Link.from (that:Enso_File_Data_Link) = Writable_Data_Link.Value that

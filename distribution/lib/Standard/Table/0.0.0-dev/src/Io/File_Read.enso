from Standard.Base import all
from Standard.Base.Error.Problem_Behavior as Problem_Behavior_Module import Problem_Behavior, Report_Warning

import Standard.Table.Io.File_Format

## ALIAS Read Text File, Read File

   Read a file using the specified file format

   Arguments:
   - path: The path of the file to open and read the contents of. It will
     accept a textual path or a file.
   - format: The text encoding to decode the file with. Defaults to UTF-8.
   - on_problems: Specifies the behavior when a problem occurs during the
     function.
     By default, a warning is issued, but the operation proceeds.
     If set to `Report_Error`, the operation fails with a dataflow error.
     If set to `Ignore`, the operation proceeds without errors or warnings.

   ? Module or Instance?
     If you have a variable `file` of type `File`, we recommend calling the
     `.read` method on it directly, rather than using `File.read file`. The
     later, however, will still work.
File.read : (Text | File) -> File_Format -> Problem_Behavior -> Any ! File_Error
File.read path (format=File_Format.Auto) (on_problems=Report_Warning) =
    file = File.new path
    file.read format on_problems

## Read a file using the specified file format

   Arguments:
   - format: The text encoding to decode the file with. Defaults to UTF-8.
   - on_problems: Specifies the behavior when a problem occurs during the
     function.
     By default, a warning is issued, but the operation proceeds.
     If set to `Report_Error`, the operation fails with a dataflow error.
     If set to `Ignore`, the operation proceeds without errors or warnings.

   > Example
     Read the first sheet of an XLSX from disk and convert it into a table.

         import Standard.Table
         import Standard.Table.Io.File_Read
         import Standard.Examples

         example_xlsx_to_table = Examples.xlsx.read

   > Example
     Read the sheet named `Dates` from an XLS and convert it to a table.

         import Standard.Table
         import Standard.Table.Io.File_Read
         from Standard.Table.Io.File_Format import Excel
         from Standard.Table.Io.Excel import Excel_Section
         import Standard.Examples

         example_xls_to_table = Examples.xls.read (Excel (Sheet 'Dates'))
File.File.read : File_Format -> Problem_Behavior -> Any ! File_Error
File.File.read (format=File_Format.Auto) (on_problems=Report_Warning) =
    format.read self on_problems


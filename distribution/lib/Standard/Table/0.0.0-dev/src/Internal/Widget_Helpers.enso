from Standard.Base import all
from Standard.Base.System.File_Format import format_types

import Standard.Base.Metadata.Widget
from Standard.Base.Metadata.Widget import Single_Choice, Vector_Editor
from Standard.Base.Metadata.Choice import Option
import Standard.Base.Metadata.Display

import project.Data.Table.Table
import project.Data.Aggregate_Column.Aggregate_Column
import project.Internal.Parse_Values_Helper
import project.Data.Table_Conversions

## PRIVATE
   Make an aggregate column selector.
make_aggregate_column_selector : Table -> Display -> Boolean -> Widget
make_aggregate_column_selector table display=Display.Always include_group_by=True =
    col_names_selector = make_column_name_selector table display=Display.Always
    column_widget = Pair.new "column" col_names_selector

    col_list_selector = make_column_name_vector_selector table display=Display.Always

    group_by = if include_group_by then [Option "Group By" "(Aggregate_Column.Group_By)" [column_widget]] else []
    count = Option "Count" "Aggregate_Column.Count"
    count_distinct = Option "Count Distinct" "(Aggregate_Column.Count_Distinct)" [Pair.new "columns" (col_list_selector)]
    first = Option "First" "(Aggregate_Column.First)" [column_widget, Pair.new "order_by" (col_list_selector)]
    last = Option "Last" "(Aggregate_Column.Last)" [column_widget, Pair.new "order_by" (col_list_selector)]

    count_not_nothing = Option "Count Not Nothing" "(Aggregate_Column.Count_Not_Nothing)" [column_widget]
    count_nothing = Option "Count Nothing" "(Aggregate_Column.Count_Nothing)" [column_widget]

    ## Should be a list of Text columns only
    count_not_empty = Option "Count Not Empty" "(Aggregate_Column.Count_Not_Empty)" [column_widget]
    count_empty = Option "Count Empty" "(Aggregate_Column.Count_Empty)" [column_widget]
    concatenate = Option "Concatenate" "(Aggregate_Column.Concatenate)" [column_widget]
    shortest = Option "Shortest" "(Aggregate_Column.Shortest)" [column_widget]
    longest = Option "Longest" "(Aggregate_Column.Longest)" [column_widget]

    ## Should be a list of Numeric columns only
    sum = Option "Sum" "(Aggregate_Column.Sum)" [column_widget]
    average = Option "Average" "(Aggregate_Column.Average)" [column_widget]
    median = Option "Median" "(Aggregate_Column.Median)" [column_widget]
    percentile = Option "Percentile" "(Aggregate_Column.Percentile)" [column_widget]
    mode = Option "Mode" "(Aggregate_Column.Mode)" [column_widget]
    standard_deviation = Option "Standard Deviation" "(Aggregate_Column.Standard_Deviation)" [column_widget]

    # Should be a list of comparable columns only
    maximum = Option "Maximum" "(Aggregate_Column.Maximum)" [column_widget]
    minimum = Option "Minimum" "(Aggregate_Column.Minimum)" [column_widget]

    Single_Choice display=display values=(group_by+[count, count_distinct, first, last, count_not_nothing, count_nothing, count_not_empty, count_empty, concatenate, shortest, longest, sum, average, median, percentile, mode, standard_deviation, maximum, minimum])

## PRIVATE
   Make a column name selector.
make_column_name_selector : Table -> Display -> Widget
make_column_name_selector table display=Display.Always =
    col_names = table.column_names
    names = col_names.map n-> Option n n.pretty
    Single_Choice display=display values=names

## PRIVATE
   Make a multiple column name selector.
make_column_name_vector_selector : Table -> Display -> Widget
make_column_name_vector_selector table display=Display.Always =
    item_editor = make_column_name_selector table display=Display.Always
    Vector_Editor item_editor=item_editor item_default=item_editor.values.first.value display=display

## PRIVATE
   Make a column name selector.
make_order_by_selector : Table -> Display -> Widget
make_order_by_selector table display=Display.Always =
    col_names = table.column_names
    names = col_names.fold [] c-> n-> c + [Option n+" (Asc)" n.pretty, Option n+" (Desc)" "(Sort_Column.Name "+n.pretty+" Sort_Direction.Descending)"]
    item_editor = Single_Choice display=display values=names
    Vector_Editor item_editor=item_editor item_default=names.first.value display=display

## PRIVATE
   Make a column rename name selector.
make_rename_name_vector_selector : Table -> Display -> Vector_Editor
make_rename_name_vector_selector table display=Display.Always =
    col_names = table.column_names
    names = col_names.map n-> Option n "["+n.pretty+", "+n.pretty+"]"
    item_editor = Single_Choice display=Display.Always values=names
    default_item = "["+col_names.first.pretty+", "+col_names.first.pretty+"]"
    Vector_Editor item_editor=item_editor item_default=default_item display=display

## PRIVATE
   Selector for type argument on `Column.parse`.
parse_type_selector : Boolean -> Widget
parse_type_selector include_auto=True =
    valid_parse_targets = Parse_Values_Helper.valid_parse_targets

    prefix = if include_auto then ['Auto'] else []
    choice = prefix + (valid_parse_targets.map t-> 'Value_Type.'+t)
    names = prefix + valid_parse_targets
    options = names.zip choice . map pair-> Option pair.first pair.second
    Single_Choice display=Display.Always values=options

## PRIVATE
   Selector for writing a table to a file.
write_table_selector : Widget
write_table_selector =
    can_write type = if type == JSON_Format then True else
        Meta.meta type . methods . contains "write_table"
    all_types = [Auto_Detect] + (format_types.filter can_write)
    make_name type_obj = type_obj.to_text.replace "_Format" "" . replace "_" " "
    Single_Choice display=Display.Always values=(all_types.map n->(Option (make_name n) (File_Format.constructor_code n)))

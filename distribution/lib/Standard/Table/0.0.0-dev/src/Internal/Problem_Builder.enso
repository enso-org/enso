from Standard.Base import all
import Standard.Base.Runtime.Ref.Ref

import project.Internal.Vector_Builder.Vector_Builder
from project.Errors import Duplicate_Output_Column_Names, Invalid_Aggregate_Column, Invalid_Column_Names, Missing_Input_Columns, Truncated_Column_Names, No_Output_Columns

## PRIVATE
type Problem_Builder
    ## PRIVATE
    Value types_to_always_throw oob_indices missing_input_columns missing_input_columns_location other

    ## PRIVATE
    report_oob_indices self indices =
        append_to_ref self.oob_indices indices

    ## PRIVATE
    report_missing_input_columns self columns =
        append_to_ref self.missing_input_columns columns

    ## PRIVATE
    report_unique_name_strategy self unique_name_strategy =
        if unique_name_strategy.invalid_names.not_empty then
            self.report_other_warning (Invalid_Column_Names.Error unique_name_strategy.invalid_names)
        if unique_name_strategy.renames.not_empty then
            self.report_other_warning (Duplicate_Output_Column_Names.Error unique_name_strategy.renames)
        truncated_map = unique_name_strategy.truncated_names
        if truncated_map.not_empty then
            self.report_other_warning (Truncated_Column_Names.Warning truncated_map)

    ## PRIVATE
    report_other_warning self warning =
        self.other.append warning

    ## PRIVATE
       Returns a vector containing all reported problems, aggregated.
    build_problemset : Vector
    build_problemset self =
        problems = Vector.new_builder

        missing_criteria = self.missing_input_columns.get.build + self.oob_indices.get.build
        if missing_criteria.not_empty then
            problems.append (Missing_Input_Columns.Error missing_criteria where=self.missing_input_columns_location)
        self.other.to_vector.each problems.append

        problems.to_vector

    ## PRIVATE
       Attaches gathered warnings to the result.
       Any errors from the `result` take precedence over the ones owned by this
       builder.
    attach_problems_after : Problem_Behavior -> Any -> Any
    attach_problems_after self problem_behavior result =
        case result of
            _ -> case self.get_problemset_throwing_distinguished_errors of
                problems ->
                    problem_behavior.attach_problems_after result problems

    ## PRIVATE
       Attaches gathered warnings to the result of the provided computation.
       If in `Report_Error` mode and there are any problems gathered, the first
       one will be returned as error without even running the computation.
    attach_problems_before : Problem_Behavior -> Any -> Any
    attach_problems_before self problem_behavior ~computation =
        case self.get_problemset_throwing_distinguished_errors of
            problems ->
                problem_behavior.attach_problems_before problems computation

    ## PRIVATE
       Returns the generated problem set if no errors force throwing or throws
       the first error that is meant to be thrown regardless of problem behavior.
    get_problemset_throwing_distinguished_errors : Vector
    get_problemset_throwing_distinguished_errors self =
        problems = self.build_problemset
        distinguished_problem = problems.find if_missing=Nothing problem->
            self.types_to_always_throw.any tpe->
                problem.is_a tpe
        case distinguished_problem of
            Nothing -> problems
            problem -> Error.throw problem

    ## PRIVATE
       Raises `No_Output_Columns` error, with cause set to the first problem
       from this builder (if any were reported).
    raise_no_output_columns_with_cause : Any ! No_Output_Columns
    raise_no_output_columns_with_cause self =
        first_problem = self.build_problemset.get 0 if_missing=Nothing
        Error.throw (No_Output_Columns.Error first_problem)

    ## PRIVATE
       Creates a new helper object for aggregating problems to report.

       Arguments:
       - types_to_always_throw: Problems matching these types will be raised as
         an error by the `attach_` methods regardless of the `Problem_Behavior`
         used. By default, an empty vector.
       - error_on_missing_columns: If set to `True`, `Missing_Input_Columns` and
         `Invalid_Aggregate_Column` will be raised as errors by the `attach_`
         methods regardless of the `Problem_Behavior` used. Defaults to `False`.
         Setting this to `True` is essentially a shorthand for adding these
         problem types to `types_to_always_throw`.
       - missing_input_columns_location: The location to add to the missing
         input column error to make it more informative. Defaults to `Nothing`.
    new : Vector -> Boolean -> Text | Nothing -> Problem_Builder
    new types_to_always_throw=[] error_on_missing_columns=False missing_input_columns_location=Nothing =
        additional_types_to_throw = if error_on_missing_columns then [Missing_Input_Columns, Invalid_Aggregate_Column] else []
        Problem_Builder.Value types_to_always_throw+additional_types_to_throw (Ref.new Vector_Builder.empty) (Ref.new Vector_Builder.empty) missing_input_columns_location other=Vector.new_builder

## PRIVATE
   Appends a `Vector` to a `Vector_Builder` stored in a `Ref`.
append_to_ref : Ref Vector_Builder -> Vector -> Nothing
append_to_ref ref vector =
    appended = (ref.get) ++ vector
    ref . put appended

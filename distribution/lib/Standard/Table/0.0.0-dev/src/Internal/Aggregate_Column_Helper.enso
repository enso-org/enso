from Standard.Base import all
import Standard.Base.Data.Vector.No_Wrap
import Standard.Base.Errors.Illegal_Argument.Illegal_Argument
from Standard.Base.Runtime import assert

import project.Aggregate_Column.Aggregate_Column
import project.Column.Column
import project.Internal.Column_Naming_Helper.Column_Naming_Helper
import project.Internal.Problem_Builder.Problem_Builder
import project.Internal.Table_Helpers
import project.Sort_Column.Sort_Column
import project.Table.Table
import project.Value_Type.Value_Type
from project.Aggregate_Column.Aggregate_Column import all
from project.Errors import Duplicate_Output_Column_Names, Invalid_Aggregation, Invalid_Column_Names, No_Output_Columns

polyglot java import org.enso.table.aggregations.Aggregator
polyglot java import org.enso.table.aggregations.Concatenate as ConcatenateAggregator
polyglot java import org.enso.table.aggregations.Count as CountAggregator
polyglot java import org.enso.table.aggregations.CountDistinct as CountDistinctAggregator
polyglot java import org.enso.table.aggregations.CountEmpty as CountEmptyAggregator
polyglot java import org.enso.table.aggregations.CountNothing as CountNothingAggregator
polyglot java import org.enso.table.aggregations.First as FirstAggregator
polyglot java import org.enso.table.aggregations.GroupBy as GroupByAggregator
polyglot java import org.enso.table.aggregations.Last as LastAggregator
polyglot java import org.enso.table.aggregations.Mean as MeanAggregator
polyglot java import org.enso.table.aggregations.MinOrMax as MinOrMaxAggregator
polyglot java import org.enso.table.aggregations.Mode as ModeAggregator
polyglot java import org.enso.table.aggregations.Percentile as PercentileAggregator
polyglot java import org.enso.table.aggregations.ShortestOrLongest as ShortestOrLongestAggregator
polyglot java import org.enso.table.aggregations.StandardDeviation as StandardDeviationAggregator
polyglot java import org.enso.table.aggregations.Sum as SumAggregator

## Result type for aggregate_columns validation
   - key_columns: Vector of Columns from the table to group by.
   - valid_columns: Table structure to build as pairs of unique column name and Aggregate_Column.
   - problems: Set of any problems when validating the input.
   - old_style: Boolean indicating if the input was in the old style.
type Validated_Aggregate_Columns
    ## PRIVATE
    Value (key_columns:(Vector Column)) (valid_columns:(Vector (Pair Text Aggregate_Column))) (problems:(Vector Any)) (old_style:Boolean)

## PRIVATE
   Prepares an aggregation input for further processing:
   - resolves the column descriptors, reporting any issues,
   - ensures that the output names are unique,
   - finds the key columns.
prepare_aggregate_columns : Column_Naming_Helper -> Vector (Integer | Text | Regex | Aggregate_Column) | Text | Integer | Regex -> Vector Aggregate_Column -> Table -> Boolean -> Validated_Aggregate_Columns
prepare_aggregate_columns naming_helper group_by aggregates table error_on_missing_columns =
    is_a_key c = case c of
        Aggregate_Column.Group_By _ _ -> True
        _ -> False

    ## Resolve old style aggregate into new style
    old_style = aggregates.is_empty && group_by.any (g-> g.is_a Aggregate_Column)
    if old_style.not && group_by.any is_a_key then Error.throw (Invalid_Aggregation.Error "`columns` should not contain a `Group_By`.") else
        keys = if old_style then group_by.filter is_a_key else group_by.map (Aggregate_Column.Group_By _ "")

        # Key resolution always errors on missing, regardless of any settings.
        keys_problem_builder = Problem_Builder.new error_on_missing_columns=True
        resolved_keys = keys.map (resolve_aggregate table keys_problem_builder)

        ## Since `keys_problem_builder` has `error_on_missing_columns` set to `True`,
           any missing columns will be reported as errors. Therefore, we can assume
           that all the columns were present.
        keys_problem_builder.attach_problems_before Problem_Behavior.Report_Error <|
            assert (resolved_keys.contains Nothing . not)
            problem_builder = Problem_Builder.new error_on_missing_columns=error_on_missing_columns
            columns = if old_style then group_by else keys+aggregates
            valid_resolved_aggregate_columns = columns.map on_problems=No_Wrap (resolve_aggregate table problem_builder) . filter x-> x.is_nothing.not

            # Grouping Key
            key_columns = resolved_keys.map .column
            unique_key_columns = key_columns.distinct (on = .name)

            # Resolve Names
            unique = naming_helper.create_unique_name_strategy

            ## First pass ensures the custom names specified by the user are unique.
               The second pass resolves the default names, ensuring that they do not
               clash with the user-specified names (ensuring that user-specified names
               take precedence).
            pass_1 = valid_resolved_aggregate_columns.map on_problems=No_Wrap c-> if c.as == "" then "" else
                # Verify if the user-provided name is valid and if not, throw an error.
                naming_helper.ensure_name_is_valid c.as <|
                    unique.make_unique c.as
            renamed_columns = pass_1.map_with_index i->name->
                agg = valid_resolved_aggregate_columns.at i
                as = if name != "" then name else unique.make_unique (default_aggregate_column_name agg)
                Pair.new as agg

            # Build Problems Output
            case renamed_columns.is_empty of
                True ->
                    ## First, we try to raise any warnings that may have caused the
                       lack of columns, promoted to errors.
                    problem_builder.attach_problems_before Problem_Behavior.Report_Error <|
                        ## If none were found, we raise a generic error (this may
                           happen primarily when an empty list is provided to the
                           aggregate method).
                        Error.throw No_Output_Columns.Error
                False ->
                    problem_builder.report_unique_name_strategy unique
                    Validated_Aggregate_Columns.Value unique_key_columns renamed_columns problem_builder.get_problemset_throwing_distinguished_errors old_style

## PRIVATE
   Defines the default name of an `Aggregate_Column`.

   To be used when `as` is `Nothing`.

   Assumes that the `Aggregate_Column` is resolved. You may need to transform it
   with `resolve_aggregate` first.
default_aggregate_column_name aggregate_column include_column=True =
    case aggregate_column of
        Group_By c _ -> c.name
        Count _ -> "Count"
        Count_Distinct columns _ _ ->
            "Count Distinct " + (columns.map .name . join " ")
        Percentile p c _ -> ((p*100).floor.to_text + "%-ile ") + (if include_column then c.name else "")
        _ ->
            atom = Meta.meta aggregate_column : Meta.Atom
            prefix = atom.constructor.name . replace "_" " "
            c = aggregate_column.column
            prefix + (if include_column then " " + c.name else "")

## PRIVATE
   Utility function to check if all aggregates are operating on the same source column.
all_same_column : Vector Aggregate_Column -> Boolean
all_same_column aggregates =
    is_not_count c = case c of
        Count _ -> False
        Count_Distinct _ _ _ -> False
        _ -> True
    without_count = aggregates.filter is_not_count

    if without_count.length < 2 then True else
        column = without_count.first.column.name
        without_count.all c->(c.column.name == column)

## PRIVATE
   Returns a copy of this aggregate where all column descriptors (names,
   indices or column references potentially from a different table) are
   replaced with column references from the provided table.

   It also verifies that the columns have the right types.

   `Sort_Column`s are replaced with column references of matched columns coming
   from the provided table.

   This preprocess step is required by some helper function, to avoid having
   to pass the table reference and resolve the column descriptors all the
   time.

   If some columns cannot be resolved, `Nothing` is returned. Any issues are
   reported to the `Problem_Builder`.
resolve_aggregate : Table -> Problem_Builder -> Aggregate_Column -> Aggregate_Column | Nothing
resolve_aggregate table problem_builder aggregate_column:Aggregate_Column =
    table_columns = table.columns
    columns_helper = table.columns_helper

    resolve : (Integer|Text) -> Column ! Internal_Missing_Column_Error
    resolve c =
        res = columns_helper.resolve_column_or_expression c problem_builder
        res.if_nothing (Error.throw Internal_Missing_Column_Error)

    resolve_selector_to_vector : Text | Integer | Vector (Integer | Text) -> Vector Column ! Internal_Missing_Column_Error
    resolve_selector_to_vector selector =
        resolved = columns_helper.select_columns_helper selector Case_Sensitivity.Default True problem_builder
        if resolved.is_empty then Error.throw Internal_Missing_Column_Error else resolved

    resolve_order_by selector -> Nothing | Vector Internal_Order_By_Column_Reference = case selector of
        Nothing -> Nothing
        _ -> if selector == [] then Nothing else
            columns_for_ordering = Table_Helpers.prepare_order_by table_columns selector problem_builder
            sort_columns = columns_for_ordering.map c->
                Internal_Order_By_Column_Reference.Value c.column c.associated_selector.direction
            sort_columns

    resolve_numeric c =
        internal_column = resolve c
        col = columns_helper.make_column internal_column
        Value_Type.expect_numeric col <|
            internal_column

    resolve_text c =
        internal_column = resolve c
        col = columns_helper.make_column internal_column
        Value_Type.expect_text col <|
            internal_column

    result = case aggregate_column:Aggregate_Column of
        Group_By c as -> Group_By (resolve c) as
        Count as -> Count as
        Count_Distinct c as ignore_nothing -> if c == [] then Count as else
            new_c = resolve_selector_to_vector c
            Count_Distinct new_c as ignore_nothing
        Count_Not_Nothing c as -> Count_Not_Nothing (resolve c) as
        Count_Nothing c as -> Count_Nothing (resolve c) as
        Count_Not_Empty c as -> Count_Not_Empty (resolve_text c) as
        Count_Empty c as ->  Count_Empty (resolve_text c) as
        Sum c as -> Sum (resolve_numeric c) as
        Average c as -> Average (resolve_numeric c) as
        Median c as -> Median (resolve_numeric c) as
        Percentile p c as -> Percentile p (resolve_numeric c) as
        Mode c as -> Mode (resolve c) as
        Standard_Deviation c as population -> Standard_Deviation (resolve_numeric c) as population
        Concatenate c as separator prefix suffix quote_char -> Concatenate (resolve_text c) as separator prefix suffix quote_char
        First c as ignore_nothing order_by -> First (resolve c) as ignore_nothing (resolve_order_by order_by)
        Last c as ignore_nothing order_by -> Last (resolve c) as ignore_nothing (resolve_order_by order_by)
        Maximum c as -> Maximum (resolve c) as
        Minimum c as -> Minimum (resolve c) as
        Shortest c as -> Shortest (resolve_text c) as
        Longest c as -> Longest (resolve_text c) as

    ## Downgrade the `Internal_Missing_Column_Error` error into a `Nothing`
       value, keeping any other dataflow errors intact.
    result.catch Internal_Missing_Column_Error (_->Nothing)

## PRIVATE
type Internal_Order_By_Column_Reference
    ## PRIVATE
    Value column direction

## PRIVATE
   A marker for missing columns during resolution.
type Internal_Missing_Column_Error

## PRIVATE
   Creates a Java Aggregator for the Aggregate_Column
java_aggregator : Text -> Aggregate_Column -> Aggregator
java_aggregator name column =
    case column of
        Group_By c _ -> GroupByAggregator.new name c.java_column
        Count _ -> CountAggregator.new name
        Count_Distinct columns _ ignore_nothing ->
            resolved = columns.map c->c.java_column
            CountDistinctAggregator.new name resolved ignore_nothing
        Count_Not_Nothing c _ -> CountNothingAggregator.new name c.java_column False
        Count_Nothing c _ -> CountNothingAggregator.new name c.java_column True
        Count_Not_Empty c _ -> CountEmptyAggregator.new name c.java_column False
        Count_Empty c _ -> CountEmptyAggregator.new name c.java_column True
        Sum c _ -> SumAggregator.new name c.java_column
        Average c _ -> MeanAggregator.new name c.java_column
        Standard_Deviation c _ p -> StandardDeviationAggregator.new name c.java_column p
        Median c _ -> PercentileAggregator.new name c.java_column 0.5
        Percentile p c _ -> PercentileAggregator.new name c.java_column p
        Mode c _ -> ModeAggregator.new name c.java_column
        First c _ ignore_nothing ordering ->
            if ordering.is_nothing then FirstAggregator.new name c.java_column ignore_nothing else
                order_columns = ordering.map c->c.column.java_column
                order_directions = ordering.map c->c.direction.to_sign
                FirstAggregator.new name c.java_column ignore_nothing order_columns order_directions
        Last c _ ignore_nothing ordering ->
            if ordering.is_nothing then LastAggregator.new name c.java_column ignore_nothing else
                order_columns = ordering.map c->c.column.java_column
                order_direction = ordering.map c->c.direction.to_sign
                LastAggregator.new name c.java_column ignore_nothing order_columns order_direction
        Maximum c _ -> MinOrMaxAggregator.new name c.java_column MinOrMaxAggregator.MAX
        Minimum c _ -> MinOrMaxAggregator.new name c.java_column MinOrMaxAggregator.MIN
        Shortest c _ -> ShortestOrLongestAggregator.new name c.java_column ShortestOrLongestAggregator.SHORTEST
        Longest c _ -> ShortestOrLongestAggregator.new name c.java_column ShortestOrLongestAggregator.LONGEST
        Concatenate c _ join prefix suffix quote -> ConcatenateAggregator.new name c.java_column join prefix suffix quote
        _ -> Error.throw (Invalid_Aggregation.Error name -1 "Unsupported aggregation")

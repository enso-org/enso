from Standard.Base import all
import Standard.Base.Errors.Illegal_Argument.Illegal_Argument
import Standard.Base.System.File_Format.File_For_Read
import Standard.Base.System.File_Format.File_Format_Metadata
import Standard.Base.System.Input_Stream.Input_Stream
from Standard.Base.System.File import find_extension_from_name

import project.Data.Match_Columns.Match_Columns
import project.Data.Table.Table
import project.Excel.Excel_Section.Excel_Section
import project.Excel.Excel_Workbook.Excel_Workbook
import project.Internal.Excel_Reader
import project.Internal.Excel_Writer

## PRIVATE
   Resolve the xls_format setting to a boolean.
should_treat_as_xls_format : (Boolean|Infer) -> File -> Boolean ! Illegal_Argument
should_treat_as_xls_format xls_format file =
    if xls_format != Infer then xls_format else
        inferred_xls_format = xls_format_from_file_extension file.extension
        inferred_xls_format.if_nothing <|
            Error.throw (Illegal_Argument.Error ("Unknown file extension for Excel file (" + file.extension + ")"))

## Read the file to a `Table` from an Excel file
type Excel_Format
    ## Read Excels files into a Table or Vector.

       Arguments:
       - section: The `Excel_Section` to read from the workbook.
         This can be one of:
         - `Workbook` - open the workbook as a connection.
         - `Sheet_Names` - outputs a `Vector` of sheet names.
         - `Range_Names` - outputs a `Vector` of range names.
         - `Worksheet` - outputs a `Table` containing the specified sheet.
         - `Cell_Range` - outputs a `Table` containing the specified range.
       - headers: If set to `True`, the first row is used as column names. If
         set to `False`, the column names are Excel column names. If set to
         `Infer`, the process tries to infer if headers are present on the first
          row. If the column names are not unique, numeric suffixes will be
          appended to disambiguate them.
       - xls_format:
         If set to `True`, the file is read as an Excel 95-2003 format.
         If set to `False`, the file is read as an Excel 2007+ format.
         `Infer` will attempt to deduce this from the extension of the filename.
       - default_sheet: The default sheet to use if `section` is set to
         `Excel_Section.Workbook`.
    Excel (section:Excel_Section=Excel_Section.Workbook) (headers:(Boolean|Infer)=Infer) (xls_format:(Boolean|Infer)=Infer) (default_sheet:Text="EnsoSheet")

    ## PRIVATE
       ADVANCED
       If the File_Format supports reading from the file, return a configured instance.
    for_file_read : File_For_Read -> Excel_Format | Nothing
    for_file_read file:File_For_Read =
        is_xls = should_treat_as_xls_format Infer file
        if is_xls.is_error then Nothing else
            Excel_Format.Excel xls_format=is_xls

    ## PRIVATE
       If this File_Format should be used for writing to that file, return a configured instance.
    for_file_write : File -> Excel_Format | Nothing
    for_file_write file = Excel_Format.for_file_read file

    ## PRIVATE
       ADVANCED
       If the File_Format supports reading from the web response, return a configured instance.
    for_web : Text -> URI -> Excel_Format | Nothing
    for_web content_type uri =
        _ = [uri]
        inferred_xls_format = xls_format_from_content_type content_type
        inferred_xls_format.if_not_nothing <|
            Excel_Format.Excel xls_format=inferred_xls_format

    ## PRIVATE
       ADVANCED
       Implements the `File.read` for this `File_Format`
    read : File -> Problem_Behavior -> Any
    read self file on_problems =
        format = should_treat_as_xls_format self.xls_format file
        case self.section of
            Excel_Section.Workbook -> Excel_Workbook.new file format self.headers
            _ -> Excel_Reader.read_file file self.section self.headers on_problems format

    ## PRIVATE
       Implements decoding the format from a stream.
    read_stream : Input_Stream -> File_Format_Metadata -> Any
    read_stream self stream:Input_Stream (metadata : File_Format_Metadata = File_Format_Metadata.no_information) =
        xls_format = if self.xls_format != Infer then self.xls_format else
            inferred_xls_format = xls_format_from_metadata metadata
            # If still unable to infer it, we default to False
            inferred_xls_format.if_nothing False

        bad_format _ =
            message = case self.xls_format of
                Infer -> "Cannot infer xls_format from a stream (tried xlsx)."
                True -> "Stream was not in the expected format (xls)."
                False -> "Stream was not in the expected format (xlsx)."
            Error.throw (Illegal_Argument.Error message)

        Excel_Reader.handle_bad_format_with_handler bad_format <|
            workbook = Excel_Workbook.from_stream stream xls_format self.headers
            workbook.read_section self.section

    ## PRIVATE
       ADVANCED
       Implements the `Table.write` for this `File_Format`.

       Depending on the `section` will control where to write.
       - If `Excel_Section.Workbook` (the default), the `table` will be written
         to a new sheet in the workbook named `Sheet<n>` where n is the first
         integer >1 that is not already used as a sheet name. If too many sheets
         are present an `Illegal_Argument` error will be thrown.
       - If `Excel_Section.Worksheet`, the `table` will be written to the
         specified sheet (either adding or replacing).
       - If `Excel_Section.Cell_Range`, the `table` will be written to the
         specified range.

        Arguments:
        - file: The file to write to.
        - table: The table to write.
        - on_existing_file: What to do if the file already exists.
        - match_columns: How to match columns between the table and the file.
        - on_problems: What to do if there are problems reading the file.
    write_table : File -> Table -> Existing_File_Behavior -> Match_Columns -> Problem_Behavior -> File
    write_table self file table on_existing_file match_columns on_problems =
        format = should_treat_as_xls_format self.xls_format file

        case self.section of
            Excel_Section.Sheet_Names -> Error.throw (Illegal_Argument.Error "Sheet_Names cannot be used for `write`.")
            Excel_Section.Range_Names -> Error.throw (Illegal_Argument.Error "Range_Names cannot be used for `write`.")
            Excel_Section.Workbook ->
                Excel_Writer.write_file file table on_existing_file (Excel_Section.Worksheet self.default_sheet) True match_columns on_problems format
            _ -> Excel_Writer.write_file file table on_existing_file self.section self.headers match_columns on_problems format

## PRIVATE
   Infers the xls format from the Content-Type.
   Returns Nothing if the content type is not a known Excel format.
xls_format_from_content_type : Text -> Boolean | Nothing
xls_format_from_content_type content_type =
    parts = content_type.split ";" . map .trim
    case parts.first of
        "application/vnd.ms-excel" -> True
        "application/vnd.openxmlformats-officedocument.spreadsheetml.sheet" -> False
        _ -> Nothing

## PRIVATE
    Infers the xls format from the file extension.
    Returns Nothing if the content type is not a known Excel format.
xls_format_from_file_extension : Text -> Boolean | Nothing
xls_format_from_file_extension extension =
    case extension of
        ".xlsx" -> False
        ".xlsm" -> False
        ".xls" -> True
        ".xlt" -> True
        _ -> Nothing

## PRIVATE
xls_format_from_metadata : File_Format_Metadata -> Boolean | Nothing
xls_format_from_metadata metadata =
    from_content_type = metadata.content_type.if_not_nothing (xls_format_from_content_type metadata.content_type)
    from_content_type.if_nothing <|
        metadata.file_name.if_not_nothing <|
            extension = find_extension_from_name metadata.file_name
            xls_format_from_file_extension extension

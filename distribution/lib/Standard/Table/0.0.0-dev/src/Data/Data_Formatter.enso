from Standard.Base import all
import Standard.Base.Errors.Illegal_Argument.Illegal_Argument

import project.Internal.Parse_Values_Helper
from project.Data.Type.Value_Type import Value_Type, Auto, Bits

polyglot java import org.enso.table.parsing.BooleanParser
polyglot java import org.enso.table.parsing.DateParser
polyglot java import org.enso.table.parsing.TimeOfDayParser
polyglot java import org.enso.table.parsing.DateTimeParser
polyglot java import org.enso.table.parsing.WhitespaceStrippingParser
polyglot java import org.enso.table.parsing.IdentityParser
polyglot java import org.enso.table.parsing.TypeInferringParser
polyglot java import org.enso.table.parsing.NumberParser

polyglot java import org.enso.table.formatting.AnyObjectFormatter
polyglot java import org.enso.table.formatting.BooleanFormatter
polyglot java import org.enso.table.formatting.DecimalFormatter
polyglot java import org.enso.table.formatting.IntegerFormatter
polyglot java import org.enso.table.formatting.DateFormatter
polyglot java import org.enso.table.formatting.DateTimeFormatter
polyglot java import org.enso.table.formatting.TimeFormatter
polyglot java import org.enso.table.formatting.TextFormatter

polyglot java import java.lang.IllegalArgumentException

type Data_Formatter
    ## Specifies options for reading text data in a table to more specific types and
       serializing them back.

       For date and time formats, a Java format string can be used or one of
       `ENSO_ZONED_DATE_TIME`, `ISO_ZONED_DATE_TIME`, `ISO_LOCAL_DATE_TIME`,
       `ISO_OFFSET_DATE_TIME`, `ISO_LOCAL_DATE`, `ISO_LOCAL_TIME` to use a
       predefined format.

       Arguments:
       - trim_values: Trim whitespace before parsing.
       - allow_leading_zeros: Specifies how to treat numeric values starting
         with leading zeroes when detecting the type of a column automatically.
         If set to `False` (the default), then if found values will be left as
         text. If the type is a number then leading zeroes are accepted
         regardless of this setting.
       - decimal_point: The character used to separate the integer part from the
         fractional part of a number. If `Auto` then the format for numbers will
         be guessed automatically. Can be changed for example to ',' to allow
         for European format.
       - thousand_separator: A separator that can be used to separate groups of
         digits in numbers (must be in groups of 3 digits).
       - allow_exponential_notation: Specifies if exponential notation is
         allowed.
       - datetime_formats: Expected datetime formats.
       - date_formats: Expected date formats.
       - time_formats: Expected time formats.
       - datetime_locale: The locale to use when parsing dates and times.
       - true_values: Values representing True.
       - false_values: Values representing False.
    Value trim_values:Boolean=True allow_leading_zeros:Boolean=False decimal_point:Text|Auto=Auto thousand_separator:Text='' allow_exponential_notation:Boolean=False datetime_formats:(Vector Text)=["ENSO_ZONED_DATE_TIME"] date_formats:(Vector Text)=["ISO_LOCAL_DATE"] time_formats:(Vector Text)=["ISO_LOCAL_TIME"] datetime_locale:Locale=Locale.default true_values:(Vector Text)=["True","true","TRUE"] false_values:(Vector Text)=["False","false","FALSE"]

    ## PRIVATE
       ADVANCED
       Parse a Text into a value.

       Arguments:
       - text: Text value to parse.
       - datatype: The expected Enso type to parse the value into. If set to
         `Auto`, the type will be inferred automatically.
       - on_problems: Specifies the behavior when a problem occurs.
         By default, a warning is issued, but the operation proceeds.
         If set to `Report_Error`, the operation fails with a dataflow error.
         If set to `Ignore`, the operation proceeds without errors or warnings.
    parse : Text -> (Auto|Integer|Number|Date|Date_Time|Time_Of_Day|Boolean) -> Problem_Behavior -> Any
    parse self text datatype=Auto on_problems=Problem_Behavior.Report_Warning =
        parser = self.make_datatype_parser datatype
        result = parser.parseIndependentValue text
        problems = Vector.from_polyglot_array result.problems . map (Parse_Values_Helper.translate_parsing_problem datatype)
        on_problems.attach_problems_after result.value problems

    ## PRIVATE
       ADVANCED
       Format a value into a Text.

       Arguments:
       - value: Value to format.
    format : Any -> Text
    format self value =
        formatter = self.make_auto_formatter
        formatter.format value

    ## Create a clone of self with specified number formatting options.

       Arguments:
       - decimal_point: The character used to separate the integer part from the fractional part of a number.
       - thousand_separator: A separator that can be used to separate groups of digits in numbers.
       - allow_leading_zeros: Specifies if values starting with leading zeroes should be treated as numbers.
       - allow_exponential_notation: Allow parsing of exponential notation format.
    with_number_formatting : Text -> Text -> Boolean -> Boolean -> Data_Formatter
    with_number_formatting self (decimal_point=self.decimal_point) (thousand_separator=self.thousand_separator) (allow_leading_zeros=self.allow_leading_zeros) (allow_exponential_notation=self.allow_exponential_notation) =
        self.clone decimal_point=decimal_point thousand_separator=thousand_separator allow_leading_zeros=allow_leading_zeros allow_exponential_notation=allow_exponential_notation

    ## Specify values for Date/Time parsing.

       A Java format string can be used or one of `ENSO_ZONED_DATE_TIME`,
       `ISO_ZONED_DATE_TIME`, `ISO_LOCAL_DATE_TIME`, `ISO_OFFSET_DATE_TIME`,
       `ISO_LOCAL_DATE`, `ISO_LOCAL_TIME` to use a predefined format.

       Arguments:
       - datetime_formats: Expected datetime formats.
       - date_formats: Expected date formats.
       - time_formats: Expected time formats.
    with_datetime_formats : Text|(Vector Text) -> Text|(Vector Text) -> Text|(Vector Text) -> Data_Formatter
    with_datetime_formats self datetime_formats=self.datetime_formats date_formats=self.date_formats time_formats=self.time_formats =
        datetime_vector = wrap_text_in_vector datetime_formats
        date_vector = wrap_text_in_vector date_formats
        time_vector = wrap_text_in_vector time_formats
        self.clone datetime_formats=datetime_vector date_formats=date_vector time_formats=time_vector

    ## Specify values for Boolean parsing.

       Arguments:
       - true_values: Values representing True.
       - false_values: Values representing False.
    with_boolean_values : Text|(Vector Text) -> Text|(Vector Text) -> Data_Formatter
    with_boolean_values self true_values false_values =
        true_vector = wrap_text_in_vector true_values
        false_vector = wrap_text_in_vector false_values
        self.clone true_values=true_vector false_values=false_vector

    ## Create a clone of self with a specified Locale.

       Arguments:
       - locale: The locale to use when parsing dates and times.
    @datetime_locale Locale.default_widget
    with_locale : Locale -> Data_Formatter
    with_locale self datetime_locale = self.clone datetime_locale=datetime_locale

    ## Create a clone of self with a changed format string for a particular
       datatype.

       It is mostly a convenience function to easily specify a datatype format.

       Arguments:
       - type: The value type for which to change the format. The format can be
         changed only for `Date_Time`, `Date`, `Time` and `Boolean` value types.
       - format: The new format string to set. For dates, it is the usual date
         format notation, and for booleans it should be two values that
         represent true and false, separated by a `|`.
    with_format : Value_Type | Auto -> Text -> Data_Formatter
    with_format self type format = case type of
        Value_Type.Date -> self.with_datetime_formats date_formats=[format]
        Value_Type.Time -> self.with_datetime_formats time_formats=[format]
        Value_Type.Date_Time _ ->
            self.with_datetime_formats datetime_formats=[format]
        Value_Type.Boolean ->
            formats = format.split "|"
            if formats.length != 2 then Error.throw (Illegal_Argument.Error "The `format` for Booleans must be a string with two values separated by `|`, for example: 'Yes|No'.") else
                self.with_boolean_values true_values=[formats.at 0] false_values=[formats.at 1]
        Auto ->
            Error.throw (Illegal_Argument.Error "Cannot specify a `format` with type `Auto`.")
        _ : Value_Type ->
            Error.throw (Illegal_Argument.Error "Cannot specify a `format` for type `"+type.to_text+"`.")

    ## PRIVATE
       Clone the instance with some properties overridden.
    clone : Boolean -> Boolean -> Text -> Text -> Boolean -> Vector Text -> Vector Text -> Vector Text -> Locale -> Vector Text -> Vector Text -> Data_Formatter
    clone self (trim_values=self.trim_values) (allow_leading_zeros=self.allow_leading_zeros) (decimal_point=self.decimal_point) (thousand_separator=self.thousand_separator) (allow_exponential_notation=self.allow_exponential_notation) (datetime_formats=self.datetime_formats) (date_formats=self.date_formats) (time_formats=self.time_formats) (datetime_locale=self.datetime_locale) (true_values=self.true_values) (false_values=self.false_values) =
        Data_Formatter.Value trim_values=trim_values allow_leading_zeros=allow_leading_zeros decimal_point=decimal_point thousand_separator=thousand_separator allow_exponential_notation=allow_exponential_notation datetime_formats=datetime_formats date_formats=date_formats time_formats=time_formats datetime_locale=datetime_locale true_values=true_values false_values=false_values

    ## PRIVATE
    get_thousand_separator self =
        if self.thousand_separator.is_empty then Nothing else self.thousand_separator

    ## PRIVATE
    wrap_base_parser self base_parser =
        if self.trim_values.not then base_parser else
            WhitespaceStrippingParser.new base_parser

    ## PRIVATE
    make_integer_parser self auto_mode=False =
        separator = if self.thousand_separator.is_empty then Nothing else self.thousand_separator
        NumberParser.createIntegerParser auto_mode.not (auto_mode.not || self.allow_leading_zeros) self.trim_values separator

    ## PRIVATE
    make_decimal_parser self auto_mode=False =
        if self.decimal_point == Auto then NumberParser.createAutoDecimalParser auto_mode.not (auto_mode.not || self.allow_leading_zeros) self.trim_values self.allow_exponential_notation else
            NumberParser.createFixedDecimalParser auto_mode.not (auto_mode.not || self.allow_leading_zeros) self.trim_values self.allow_exponential_notation self.thousand_separator self.decimal_point

    ## PRIVATE
    make_boolean_parser self = self.wrap_base_parser <|
        BooleanParser.new self.true_values.to_array self.false_values.to_array

    ## PRIVATE
    make_date_parser self = self.wrap_base_parser <|
        Panic.catch Any handler=(caught_panic-> Error.throw (Illegal_Argument.Error caught_panic.payload.getMessage)) <|
            DateParser.new self.date_formats.to_array self.datetime_locale.java_locale

    ## PRIVATE
    make_date_time_parser self = self.wrap_base_parser <|
        Panic.catch Any handler=(caught_panic-> Error.throw (Illegal_Argument.Error caught_panic.payload.getMessage)) <|
            DateTimeParser.new self.datetime_formats.to_array self.datetime_locale.java_locale

    ## PRIVATE
    make_time_of_day_parser self = self.wrap_base_parser <|
        Panic.catch Any handler=(caught_panic-> Error.throw (Illegal_Argument.Error caught_panic.payload.getMessage)) <|
            TimeOfDayParser.new self.time_formats.to_array self.datetime_locale.java_locale

    ## PRIVATE
    make_identity_parser self = self.wrap_base_parser IdentityParser.new

    ## PRIVATE
    make_datatype_parser self datatype = case datatype of
        Integer     -> self.make_integer_parser
        Decimal     -> self.make_decimal_parser
        Boolean     -> self.make_boolean_parser
        Date        -> self.make_date_parser
        Date_Time   -> self.make_date_time_parser
        Time_Of_Day -> self.make_time_of_day_parser
        Auto        -> self.make_auto_parser
        _ ->
            type_name = case datatype.to_text of
                text : Text -> text
                _ -> Meta.meta datatype . to_text
            Error.throw (Illegal_Argument.Error "Unsupported datatype: "+type_name)

    ## PRIVATE
    make_value_type_parser self value_type = case value_type of
        # TODO once we implement #5159 we will need to add checks for bounds here and support 16/32-bit ints
        Value_Type.Integer Bits.Bits_64 -> self.make_integer_parser
        # TODO once we implement #6109 we can support 32-bit floats
        Value_Type.Float Bits.Bits_64   -> self.make_decimal_parser
        Value_Type.Boolean              -> self.make_boolean_parser
        Value_Type.Date                 -> self.make_date_parser
        Value_Type.Date_Time True       -> self.make_date_time_parser
        Value_Type.Time                 -> self.make_time_of_day_parser
        Auto -> self.make_auto_parser
        _ ->
            Error.throw (Illegal_Argument.Error "Unsupported value type: "+value_type.to_display_text)

    ## PRIVATE
    get_specific_type_parsers self =
        [self.make_integer_parser True, self.make_decimal_parser True, self.make_date_time_parser, self.make_date_parser, self.make_time_of_day_parser, self.make_boolean_parser]

    ## PRIVATE
    make_auto_parser self =
        fallback_parser = self.make_identity_parser
        TypeInferringParser.new self.get_specific_type_parsers.to_array fallback_parser

    ## PRIVATE
    make_integer_formatter self =
        IntegerFormatter.new self.get_thousand_separator

    ## PRIVATE
    make_decimal_formatter self =
        decimal_point = if self.decimal_point == Auto then '.'  else self.decimal_point
        DecimalFormatter.new self.get_thousand_separator decimal_point

    ## PRIVATE
    make_date_formatter self =
        if self.date_formats.is_empty then Error.throw (Illegal_Argument.Error "Formatting dates requires at least one entry in the `date_formats` parameter") else
            Panic.catch Any handler=(caught_panic-> Error.throw (Illegal_Argument.Error caught_panic.payload.getMessage)) <|
                DateFormatter.new self.date_formats.first self.datetime_locale.java_locale

    ## PRIVATE
    make_time_of_day_formatter self =
        if self.time_formats.is_empty then Error.throw (Illegal_Argument.Error "Formatting times requires at least one entry in the `time_formats` parameter") else
            Panic.catch Any handler=(caught_panic-> Error.throw (Illegal_Argument.Error caught_panic.payload.getMessage)) <|
                TimeFormatter.new self.time_formats.first self.datetime_locale.java_locale

    ## PRIVATE
    make_date_time_formatter self =
        if self.datetime_formats.is_empty then Error.throw (Illegal_Argument.Error "Formatting date-times requires at least one entry in the `datetime_formats` parameter") else
            Panic.catch Any handler=(caught_panic-> Error.throw (Illegal_Argument.Error caught_panic.payload.getMessage)) <|
                DateTimeFormatter.new self.datetime_formats.first self.datetime_locale.java_locale

    ## PRIVATE
    make_boolean_formatter self =
        if self.true_values.is_empty then Error.throw (Illegal_Argument.Error "Formatting booleans requires at least one entry in the `true_values` parameter") else
            if self.false_values.is_empty then Error.throw (Illegal_Argument.Error "Formatting booleans requires at least one entry in the `false_values` parameter") else
                BooleanFormatter.new self.true_values.first self.false_values.first

    ## PRIVATE
    make_text_formatter self =
        TextFormatter.new

    ## PRIVATE
    get_specific_type_formatters self =
        [self.make_integer_formatter, self.make_decimal_formatter, self.make_boolean_formatter, self.make_date_time_formatter, self.make_date_formatter, self.make_time_of_day_formatter, self.make_text_formatter]

    ## PRIVATE
    make_auto_formatter self =
        # TODO The panic rethrow+recover is a workaround for the vector error propagation bug.
        formatters = Panic.recover Illegal_Argument (self.get_specific_type_formatters.map Panic.rethrow)
        AnyObjectFormatter.new formatters.to_array

    ## PRIVATE
    make_formatter_for_column_type self (column_type : Value_Type) = case column_type of
        Value_Type.Char _ _ -> self.make_text_formatter
        Value_Type.Integer _ -> self.make_integer_formatter
        Value_Type.Float _ -> self.make_decimal_formatter
        Value_Type.Boolean -> self.make_boolean_formatter
        Value_Type.Date -> self.make_date_formatter
        Value_Type.Time -> self.make_time_of_day_formatter
        Value_Type.Date_Time _ -> self.make_date_time_formatter
        _ -> self.make_auto_formatter

## PRIVATE
   Utility function to convert single text value to a vector
wrap_text_in_vector v = case v of
    _ : Text -> [v]
    _ -> v

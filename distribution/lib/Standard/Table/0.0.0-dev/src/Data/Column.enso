from Standard.Base import all
import Standard.Base.Data.Array_Proxy.Array_Proxy
import Standard.Base.Data.Ordering.Comparator
import Standard.Base.Error.Common.Index_Out_Of_Bounds
import Standard.Base.Error.Illegal_Argument.Illegal_Argument
import Standard.Base.Error.Illegal_State.Illegal_State

import Standard.Base.Data.Index_Sub_Range as Index_Sub_Range_Module

import project.Data.Data_Formatter.Data_Formatter
import project.Data.Storage.Storage
import project.Data.Table.Table
import project.Data.Value_Type.Value_Type
import project.Internal.Java_Problems
import project.Internal.Parse_Values_Helper
import project.Internal.Widget_Helpers

from project.Data.Table import print_table
from project.Data.Value_Type import Auto, ensure_valid_parse_target
from project.Errors import No_Index_Set_Error, Floating_Point_Equality

polyglot java import org.enso.table.data.column.operation.map.MapOperationProblemBuilder
polyglot java import org.enso.table.data.column.storage.Storage as Java_Storage
polyglot java import org.enso.table.data.table.Column as Java_Column
polyglot java import org.enso.table.operations.OrderBuilder

type Column
    ## Creates a new column given a name and a vector of elements.

       Arguments:
       - name: The name of the column to create.
       - items: The elements to contain in the column.

       > Example
         Create a new column called "My Column" from a vector of numbers.

             from Standard.Table import Column

             example_from_vector =
                 Column.from_vector "My Column" [1, 2, 3, 4, 5]
    from_vector : Text -> Vector -> Column
    from_vector name items = Column.Value (Java_Column.fromItems name items.to_array)

    ## PRIVATE
       Creates a new column given a name and an internal Java storage.
    from_storage : Text -> Java_Storage -> Column
    from_storage name storage = Column.Value (Java_Column.new name storage)

    ## Creates a new column given a name and a vector of elements repeated over and over.

       Arguments:
       - name: The name of the column to create.
       - items: The elements to contain in the column.
       - repeats: The number of times to repeat the vector.
    from_vector_repeated : Text -> Vector -> Integer -> Column
    from_vector_repeated name items repeats = Column.Value (Java_Column.fromRepeatedItems name items.to_array repeats)

    ## PRIVATE

       A representation of a column in a Table.

       Arguments:
       - java_column: The internal representation of the column.
    Value java_column

    ## Returns a text containing an ASCII-art table displaying this data.

       Arguments:
       - show_rows: the number of initial rows that should be displayed.
       - format_terminal: whether ANSI-terminal formatting should be used

       > Example
         Convert a column to a pretty-printed text representation.

             import Standard.Examples

             example_display = Examples.integer_column.display
    display : Integer -> Boolean -> Text
    display self show_rows=10 format_terminal=False =
        java_col = self.java_column
        index = java_col.getIndex
        col_name = java_col.getName . map normalize_string_for_display
        storage = java_col.getStorage
        num_rows = java_col.getSize
        display_rows = Math.min num_rows show_rows
        items = Vector.new display_rows num->
            row = if storage.isNa num then "Nothing" else
                get_item_string storage num
            [index.ilocString num, row]
        table = print_table [index.getName, col_name] items 1 format_terminal
        if num_rows - display_rows <= 0 then table else
            missing = '\n\u2026 and ' + (num_rows - display_rows).to_text + ' hidden rows.'
            table + missing

    ## TEXT_ONLY

       Prints an ASCII-art table with this data to the standard output.

       Arguments:
       - show_rows: the number of initial rows that should be displayed.

       > Example
         Convert a column to a pretty-printed text representation and print it
         to the console.

             import Standard.Examples

             example_display = Examples.integer_column.print
    print : Integer -> Nothing
    print self show_rows=10 =
        IO.println (self.display show_rows format_terminal=True)
        IO.println ''

    ## Element-wise equality comparison.

       Arguments:
       - other: The value to compare `self` against. If `other` is a column, the
         comparison is performed pairwise between corresponding elements of
         `self` and `other`.

       Returns a column with results of comparing this column's elements against
       `other`.

       ! Warnings

         - If this operation results in comparing floating-point values for
           equality which is not recommended, a `Floating_Point_Equality`
           warning is attached to the result.

       > Example
         Compare two columns for pairwise equality.

             import Standard.Examples

             example_eq = Examples.integer_column == Examples.decimal_column

       > Example
         Compare a column with a number.

             import Standard.Examples

             example_eq = Examples.integer_column == 1
    == : Column | Any -> Column
    == self other =
        other_name = case other of
            other_column : Column -> "["+other_column.name+"]"
            _ -> other.to_text
        location = "["+self.name+"] == "+other_name
        fallback problem_builder a b = case b of
            _ : Decimal ->
                problem_builder.reportFloatingPointEquality -1
                a == b
            _ -> a == b
        run_vectorized_binary_op_with_problem_handling self '==' fallback other location

    ## Element-wise case-insensitive text equality comparison.

       Arguments:
       - other: The value to compare `self` against. If `other` is a column, the
         comparison is performed pairwise between corresponding elements of
         `self` and `other`.

       Returns a column with results of comparing this column's elements against
       `other`.
    equals_ignore_case : Column | Any -> Locale -> Column
    equals_ignore_case self other locale=Locale.default =
        ## TODO currently this always runs the fallback which is slow due to the
           cost of Java-to-Enso calls. We want to have a vectorized
           implementation, but we need to extend the architecture to allow
           passing the locale to it.
           See: https://www.pivotaltracker.com/n/projects/2539304/stories/184093260
        run_vectorized_binary_op self "equals_ignore_case" (x-> y-> x.equals_ignore_case y locale=locale) other

    ## Element-wise non-equality comparison.

       Arguments:
       - other: The value to compare `self` against. If `other` is a column, the
         comparison is performed pairwise between corresponding elements of
         `self` and `other`.

       Returns a column with results of comparing this column's elements against
       `other`.

       ! Warnings

         - If this operation results in comparing floating-point values for
           equality which is not recommended, a `Floating_Point_Equality`
           warning is attached to the result.

       > Example
         Compare two columns for pairwise inequality.

             import Standard.Examples

             example_neq = Examples.integer_column != Examples.decimal_column

       > Example
         Compare a column with a number.

             import Standard.Examples

             example_neq = Examples.integer_column != 1
    != : Column | Any -> Column
    != self other =
        (self == other).not

    ## Element-wise order comparison.

       Arguments:
       - other: The value to compare `self` against. If `other` is a column, the
         comparison is performed pairwise between corresponding elements of
         `self` and `other`.

       Returns a column with results of comparing this column's elements against
       `other`.

       > Example
         Compare two columns for pairwise greater-than-or-equal.

             import Standard.Examples

             example_geq = Examples.integer_column >= Examples.decimal_column

       > Example
         Compare a column with a number.

             import Standard.Examples

             example_geq = Examples.integer_column >= 1
    >= : Column | Any -> Column
    >= self other =
        run_vectorized_binary_op self ">=" (>=) other

    ## Element-wise order comparison.

       Arguments:
       - other: The value to compare `self` against. If `other` is a column, the
         comparison is performed pairwise between corresponding elements of
         `self` and `other`.

       Returns a column with results of comparing this column's elements against
       `other`.

       > Example
         Compare two columns for pairwise less-than-or-equal.

             import Standard.Examples

             example_leq = Examples.integer_column <= Examples.decimal_column

       > Example
         Compare a column with a number.

             import Standard.Examples

             example_leq = Examples.integer_column <= 1
    <= : Column | Any -> Column
    <= self other =
        run_vectorized_binary_op self "<=" (<=) other

    ## Element-wise order comparison.

       Arguments:
       - other: The value to compare `self` against. If `other` is a column, the
         comparison is performed pairwise between corresponding elements of
         `self` and `other`.

       Returns a column with results of comparing this column's elements against
       `other`.

       > Example
         Compare two columns for pairwise greater-than.

             import Standard.Examples

             example_gt = Examples.integer_column > Examples.decimal_column

       > Example
         Compare a column with a number.

             import Standard.Examples

             example_gt = Examples.integer_column > 1
    > : Column | Any -> Column
    > self other =
        run_vectorized_binary_op self ">" (>) other

    ## Element-wise order comparison.

       Arguments:
       - other: The value to compare `self` against. If `other` is a column, the
         comparison is performed pairwise between corresponding elements of
         `self` and `other`.

       Returns a column with results of comparing this column's elements against
       `other`.

       > Example
         Compare two columns for pairwise less-than.

             import Standard.Examples

             example_lt = Examples.integer_column < Examples.decimal_column

       > Example
         Compare a column with a number.

             import Standard.Examples

             example_lt = Examples.integer_column < 1
    < : Column | Any -> Column
    < self other = run_vectorized_binary_op self "<" (<) other

    ## Element-wise inclusive bounds check.

       Arguments:
       - lower: The lower bound to compare elements of `self` against. If
         `lower` is a column, the comparison is performed pairwise between
         corresponding elements of `self` and `lower`.
       - upper: The upper bound to compare elements of `self` against. If
         `upper` is a column, the comparison is performed pairwise between
         corresponding elements of `self` and `upper`.

       Returns a column with boolean values indicating whether values of this
       column fit between the lower and upper bounds (both ends inclusive).
    between : (Column | Any) -> (Column | Any) -> Column
    between self lower upper = (self >= lower) && (self <= upper)

    ## ALIAS Add Columns

       Element-wise addition.

       Arguments:
       - other: The value to add to `self`. If `other` is a column, the addition
         is performed pairwise between corresponding elements of `self` and
         `other`.

       Returns a column with results of adding `other` from each element of
       `self`.

       > Example
         Add two columns to each other.

             import Standard.Examples

             example_plus = Examples.decimal_column + Examples.integer_column

       > Example
         Add a single value to each item in a column.

             import Standard.Examples

             example_plus = Examples.integer_column + 10
    + : Column | Any -> Column
    + self other = run_vectorized_binary_op self '+' (+) other

    ## ALIAS Subtract Columns

       Element-wise subtraction.

       Arguments:
       - other: The value to subtract from `self`. If `other` is a column, the
         subtraction is performed pairwise between corresponding elements of
         `self` and `other`.

       Returns a column with results of subtracting `other` from each element of
       `self`.

       > Example
         Subtract one column from another.

             import Standard.Examples

             example_minus = Examples.decimal_column - Examples.integer_column

       > Example
         Subtract a single value from each item in a column.

             import Standard.Examples

             example_minus = Examples.integer_column - 10
    - : Column | Any -> Column
    - self other = run_vectorized_binary_op self '-' (-) other

    ## ALIAS Multiply Columns

       Element-wise multiplication.

       Arguments:
       - other: The value to multiply `self` by. If `other` is a column, the
         multiplication is performed pairwise between corresponding elements of
         `self` and `other`.

       Returns a column containing the result of multiplying each element of
       `self` by `other`.

       > Example
         Multiply the elements of two columns together.

             import Standard.Examples

             example_mul = Examples.decimal_column * Examples.integer_column

       > Example
         Multiply each value in a column by a single value.

             import Standard.Examples

             example_mul = Examples.integer_column * 10
    * : Column | Any -> Column
    * self other = run_vectorized_binary_op self '*' (*) other

    ## ALIAS Divide Columns

       Element-wise division.

       Arguments:
       - other: The value to divide `self` by. If `other` is a column, the
         division is performed pairwise between corresponding elements of `self`
         and `other`.

       Returns a column containing the result of dividing each element of `self`
       by `other`.

       ! Warnings

         - If division by zero occurs, an `Arithmetic_Error` warning is attached
           to the result.

       > Example
         Divide the elements of one column by the elements of another.

             import Standard.Examples

             example_div = Examples.decimal_column / Examples.integer_column

       > Example
         Multiply each value in a column by a single value.

             import Standard.Examples

             example_div = Examples.integer_column / 10
    / : Column | Any -> Column
    / self other =
        other_name = case other of
            other_column : Column -> "["+other_column.name+"]"
            _ -> other.to_text
        location = "["+self.name+"] / "+other_name
        fallback problem_builder a b =
            if b != 0 then a / b else
                # TODO indices for fallback
                problem_builder.reportDivisionByZero Nothing
                Nothing
        run_vectorized_binary_op_with_problem_handling self '/' fallback other location

    ## Element-wise modulus.

       Arguments:
       - other: The value to modulo `self` against. If `other` is a column, the
         modulus is performed pairwise between corresponding elements of `self`
         and `other`.

       Returns a column with results of modulus this column's elements against
       `other`.

       ! Warnings

         - If division by zero occurs, an `Arithmetic_Error` warning is attached
           to the result.

       > Example
         Modulus of two columns against each other.

             import Standard.Examples

             example_mod = Examples.integer_column % Examples.decimal_column

       > Example
         Modulus of a column with a number.

             import Standard.Examples

             example_mod = Examples.integer_column % 3
    % : Column | Any -> Column
    % self other =
        other_name = case other of
            other_column : Column -> "["+other_column.name+"]"
            _ -> other.to_text
        location = "["+self.name+"] % "+other_name
        fallback problem_builder a b =
            if b != 0 then a % b else
                # TODO indices for fallback
                problem_builder.reportDivisionByZero Nothing
        run_vectorized_binary_op_with_problem_handling self '%' fallback other location

    ## ALIAS Power Columns

       Element-wise raising to the power.

       Arguments:
       - other: The exponent to raise `self` by. If `other` is a column, the
         power operation is performed pairwise between corresponding elements
         of `self` and `other`.

       Returns a column containing the result of raising each element of `self`
       by `other`.

       > Example
         Squares the elements of one column.

             import Standard.Examples

             example_div = Examples.decimal_column ^ 2

       > Example
         Raises each value in a column by the value in another column.

             import Standard.Examples

             example_div = Examples.decimal_column ^ Examples.integer_column
    ^ : Column | Any -> Column
    ^ self other = run_vectorized_binary_op self '^' (^) other

    ## ALIAS AND Columns

       Element-wise boolean conjunction.

       Arguments:
       - other: The value to compute the conjunction of `self` with. If `other`
         is a column, the conjunction is performed pairwise between
         corresponding elements of `self` and `other`.

       Returns a column containing the result of performing boolean `and` on
       each element of `self` and `other`.

       > Example
         Compute the pairwise logical conjunction to two columns.

             import Standard.Examples

             example_and = Examples.bool_column_1 && Examples.bool_column_2

       > Example
         Compute the logical conjunction of each value in a column with a single
         value.

             import Standard.Examples

             example_and = Examples.bool_column_1 && True
    && : Column | Any -> Column
    && self other =
        run_vectorized_binary_op self "&&" (&&) other

    ## ALIAS OR Columns

       Element-wise boolean disjunction.

       Arguments:
       - other: The value to compute the disjunction of `self` with. If `other`
         is a column, the disjunction is performed pairwise between
         corresponding elements of `self` and `other`.

       Returns a column containing the result of performing the boolean `or` on
       each element of `self` and `other`.

       > Example
         Compute the pairwise logical disjunction to two columns.

             import Standard.Examples

             example_or = Examples.bool_column_1 || Examples.bool_column_2

       > Example
         Compute the logical disjunction of each value in a column with a single
         value.

             import Standard.Examples

             example_or = Examples.bool_column_1 || True
    || : Column | Any -> Column
    || self other =
        run_vectorized_binary_op self "||" (||) other

    ## ALIAS NOT

       Boolean negation of each element in self column.

       > Example
         Negate the elements of a column.

             import Standard.Examples

             example_not = Examples.bool_column_1.not
    not : Column
    not self = run_vectorized_unary_op self "not" .not

    ## ALIAS IF

       Replaces `True` values with `when_true` and `False` with `when_false`.
       Only meant for use with boolean columns.

       Arguments:
       - when_true: value or column when `self` is `True`.
       - when_false: value or column when `self` is `False`.

       > Example
         If the value in a column is `True`, replace it with `1`, otherwise `0`.

             import Standard.Examples

             example_if = Examples.bool_column_1.iif 1 0
    iif : Any -> Any -> Column
    iif self when_true when_false = case self.storage_type of
        Storage.Boolean ->
            s = self.java_column.getStorage

            true_val = case when_true of
                _ : Column -> when_true.java_column.getStorage
                _ -> when_true

            false_val = case when_false of
                _ : Column -> when_false.java_column.getStorage
                _ -> when_false

            rs = s.iif true_val false_val
            Column.Value (Java_Column.new "Result" rs)
        _ -> Error.throw (Illegal_Argument.Error "`iif` can only be used with boolean columns.")

    ## Returns a column of first non-`Nothing` value on each row of `self` and
       `values` list.

       Arguments:
       - values: list of columns or values to coalesce with `self`.

       > Example
         Get the first non-`Nothing` value in two columns.

             import Standard.Examples

             example_coalesce = Examples.decimal_column.coalesce Examples.integer_column
    coalesce : (Any | Vector Any) -> Column
    coalesce self values =
        fallback a b = a.if_nothing b
        run_vectorized_many_op self "coalesce" fallback values

    ## Returns a column of minimum on each row of `self` and `values` list.

       Arguments:
       - values: list of columns or values to minimum with `self`.

       > Example
         Get the minimum value in two columns.

             import Standard.Examples

             example_min = Examples.decimal_column.min Examples.integer_column
    min : (Any | Vector Any) -> Column
    min self values =
        fallback a b = if a.is_nothing then b else
            if b.is_nothing then a else
                if b < a then b else a
        run_vectorized_many_op self "minimum" fallback values

    ## Returns a column of maximum on each row of `self` and `values` list.

       Arguments:
       - values: list of columns or values to maximum with `self`.

       > Example
         Get the maximum value in two columns.

             import Standard.Examples

             example_max = Examples.decimal_column.max Examples.integer_column
    max : (Any | Vector Any) -> Column
    max self values =
        fallback a b = if a.is_nothing then b else
            if b.is_nothing then a else
                if b > a then b else a
        run_vectorized_many_op self "maximum" fallback values

    ## Returns a column of booleans, with `True` items at the positions where
       this column contains a `Nothing`.

       > Example
         Check a column for missing values.

             import Standard.Examples

             example_is_missing = Examples.decimal_column.is_missing
    is_missing : Column
    is_missing self = run_vectorized_unary_op self "is_missing" (== Nothing) on_missing=True

    ## UNSTABLE
       Returns a column of booleans, with `True` items at the positions where
       this column contains a NaN. This is only applicable to double columns.
    is_nan : Column
    is_nan self =
        is_object_nan x = case x of
            _ : Decimal -> x.is_nan
            _ -> False
        run_vectorized_unary_op self "is_nan" is_object_nan on_missing=False

    ## PRIVATE
       Returns a column of booleans, with `True` items at the positions where
       this column contains an empty string or `Nothing`.
    is_empty : Column
    is_empty self = run_vectorized_unary_op self "is_empty" Filter_Condition.Is_Empty.to_predicate on_missing=True

    ## Returns a column of booleans, with `True` items at the positions where
       this column does not contain a `Nothing`.

       > Example
         Check a column for present values.

             import Standard.Examples

             example_is_present = Examples.decimal_column.is_present
    is_present : Column
    is_present self = self.is_missing.not

    ## PRIVATE
       Returns a column of booleans with `True` at the positions where this
       column contains a blank value.

       Arguments:
       - treat_nans_as_blank: If `True`, then `Number.nan` is considered as
         blank.

       ? Blank values
         Blank values are `Nothing`, `""` and depending on setting `Number.nan`.
    is_blank : Boolean -> Boolean -> Column
    is_blank self treat_nans_as_blank=False =
        case self.storage_type of
            Storage.Text -> self.is_empty
            Storage.Decimal -> if treat_nans_as_blank then self.is_missing || self.is_nan else self.is_missing
            Storage.Any -> if treat_nans_as_blank then self.is_empty || self.is_nan else self.is_empty
            _ -> self.is_missing

    ## ALIAS Fill Missing

       Returns a new column where missing values have been replaced with the
       provided default.

       Arguments:
       - default: The value to replace missing values with. If this argument
         is a column, the value from `default` at the corresponding position
         will be used.

       > Example
         Fill missing values in a column with the value 20.5.

             import Standard.Examples

             example_fill_missing = Examples.decimal_column.fill_missing 20.5
    fill_missing : Column | Any -> Column
    fill_missing self default =
        storage = self.java_column.getStorage
        name = self.java_column.getName
        new_st = case default of
            Column.Value java_col ->
                other_storage = java_col.getStorage
                storage.fillMissingFrom other_storage
            _ ->
                storage.fillMissing default
        col = Java_Column.new name new_st
        Column.Value col

    ## Checks for each element of the column if it starts with `other`.

       Arguments:
       - other: The value to compare `self` with. If `other` is a column, the
         operation is performed pairwise between corresponding elements of
         `self` and `other`.
       - case_sensitivity: Specifies if the text values should be compared case
         sensitively.

       > Example
         Check the elements of a column for starting with the elements of
         another column.

             import Standard.Examples

             example_starts_with =
                Examples.text_column_1.starts_with Examples.text_column_2

       > Example
         Check the elements of a column for starting with a value.

             import Standard.Examples

             example_starts_with = Examples.text_column_1.starts_with "hell"

       > Example
         Check the elements of a column for starting with a value comparing case insensitively.

             import Standard.Examples

             example_starts_with = Examples.text_column_1.starts_with "hell" Case_Sensitivity.Insensitive
    starts_with : Column | Text -> Case_Sensitivity -> Column
    starts_with self other case_sensitivity=Case_Sensitivity.Default =
        run_vectorized_binary_case_text_op self Java_Storage.Maps.STARTS_WITH other case_sensitivity (a -> b -> a.starts_with b case_sensitivity)

    ## Checks for each element of the column if it ends with `other`.

       Arguments:
       - other: The value to compare `self` with. If `other` is a column, the
         operation is performed pairwise between corresponding elements of
         `self` and `other`.
       - case_sensitivity: Specifies if the text values should be compared case
         sensitively.

       > Example
         Check the elements of a column for ending with the elements of another
         column.

             import Standard.Examples

             example_ends_with =
                Examples.text_column_1.ends_with Examples.text_column_2

       > Example
         Check the elements of a column for ending with a value.

             import Standard.Examples

             example_ends_with = Examples.text_column_1.ends_with "hell"
    ends_with : Column | Text -> Case_Sensitivity -> Column
    ends_with self other case_sensitivity=Case_Sensitivity.Default =
        run_vectorized_binary_case_text_op self Java_Storage.Maps.ENDS_WITH other case_sensitivity (a -> b -> a.ends_with b case_sensitivity)

    ## Checks for each element of the column if it contains `other`.

       Arguments:
       - other: The value to compare `self` with. If `other` is a column, the
         operation is performed pairwise between corresponding elements of
         `self` and `other`.
       - case_sensitivity: Specifies if the text values should be compared case
         sensitively.

       > Example
         Check the elements of a column for containing the elements of another
         column.

             import Standard.Examples

             example_contains =
                 Examples.text_column_1.contains Examples.text_column_2

       > Example
         Check the elements of a column for containing a value.

             import Standard.Examples

             example_contains = Examples.text_column_1.contains "hell"
    contains : Column | Text -> Column
    contains self other case_sensitivity=Case_Sensitivity.Default =
        run_vectorized_binary_case_text_op self Java_Storage.Maps.CONTAINS other case_sensitivity (a -> b -> a.contains b case_sensitivity)

    ## Checks for each element of the column if it matches an SQL-like pattern.

       Arguments:
       - pattern: The pattern to match `self` against. If it is a column, the
         operation is performed pairwise between corresponding elements of
         `self` and that column. The pattern is an SQL-like pattern, where
         `%` matches any sequence of characters and `_` matches any single
         character.

       > Example
         Check if elements of a column start with 'F' and end with a dot.

             import Standard.Examples

             example_contains = Examples.text_column_1.like "F%."
    like : Column | Text -> Column
    like self pattern =
        run_vectorized_binary_op self "like" (_ -> _ -> Error.throw (Illegal_State.Error "The `Like` operation should only be used on Text columns.")) pattern

    ## Checks for each element of the column if it is contained within the
       provided vector or column.

       Arguments:
       - vector: A vector of elements or another column. The resulting column
         will contain true at the positions where the corresponding element of
         `self` is contained in `vector`.

       > Example
         Check if elements of a column are contained in a provided vector.

             import Standard.Examples

             example_contains = Examples.text_column_1.is_in [1, 2, 5]
    is_in : Column | Vector -> Column
    is_in self vector =
        op_name = "is_in"
        result_name = "Result"
        case self.java_column.getStorage.isOpVectorized op_name of
            True ->
                fallback_fn _ _ =
                    Panic.throw (Illegal_State.Error "Impossible: This is a bug in the Standard.Table library.")
                true_vector = case vector of
                    _ : Vector -> vector
                    _ : Array -> Vector.from_polyglot_array vector
                    column : Column -> column.to_vector
                run_vectorized_binary_op self op_name fallback_fn true_vector skip_nulls=False new_name=result_name
            False ->
                ## We have custom code for the non-vectorized case, because
                   usually a vectorized binary op will apply the fallback
                   function element-wise if the other argument is a Column. But
                   for `Is_In` we want each element of `self` to be checked
                   against the whole other column, instead of just the
                   corresponding row - so we need to go around a bit.
                true_vector = case vector of
                    _ : Vector -> vector
                    _ : Array -> Vector.from_polyglot_array vector
                    ## This does no copying, as `Column.to_vector` just returns
                       a view of the storage.
                    column : Column -> column.to_vector
                new_vector = self.to_vector.map (Filter_Condition.Is_In true_vector).to_predicate
                Column.from_vector result_name new_vector

    ## Parses a text column into values

       Arguments:
       - type: The type to parse the column to. Defaults to `Auto` meaning that
         the type will be inferred from the data.
       - format: The formatting settings to use when parsing the column.
         For `Date`, `Time_Of_Day` and `Date_Time`, a Java date time style
         can be used. For `Boolean`, it should be two values that represent true
         and false, separated by a `|`. Alternatively, a `Data_Formatter` can be
         passed to provide complete customisation of the formatting.
       - on_problems: Specifies how to handle if a problem occurs, raising as a
         warning by default.

       ! Error Conditions

         - If the column is not a text column, an `Invalid_Value_Type` error is
           raised.
         - If some values in the column did not match the expected datatype
           format, an `Invalid_Format` problem is reported. The problematic
           cells are replaced with `Nothing`.
         - If parsing a numeric column and the selected format does not allow
           leading zeros (the default) and such cells are found, a
           `Leading_Zeros` problem is reported. These cells are replaced with
           `Nothing`.

       > Example
         Parse dates in a column in the format `yyyy-MM-dd` (the default format).

             import Standard.Examples

             example_contains = Examples.text_column_1.parse Date

       > Example
         Parse dates in a column in the format `dd/MM/yyyy`

             import Standard.Examples

             example_contains = Examples.text_column_1.parse Date 'dd/MM/yyyy'

       > Example
         Parse a Yes/No column into a boolean column.

             import Standard.Examples

             example_contains = Examples.text_column_1.parse Boolean 'Yes|No'
    @type Widget_Helpers.parse_type_selector
    parse : (Auto|Integer|Decimal|Date|Date_Time|Time_Of_Day|Boolean) -> Text | Data_Formatter -> Problem_Behavior -> Column
    parse self type=Auto format=Data_Formatter.Value on_problems=Report_Warning =
        Value_Type.expect_text self.value_type related_column=self.name <| ensure_valid_parse_target type <|
            formatter = case format of
                _ : Text ->
                    Data_Formatter.Value.with_format type format
                _ -> format

            parser = if type == Auto then formatter.make_auto_parser else formatter.make_datatype_parser type
            storage = self.java_column.getStorage
            new_storage_and_problems = parser.parseColumn self.name storage

            new_storage = new_storage_and_problems.value
            problems = Vector.from_polyglot_array new_storage_and_problems.problems . map (Parse_Values_Helper.translate_parsing_problem type)

            output = Column.Value (Java_Column.new self.name new_storage)
            on_problems.attach_problems_after output problems

    ## ALIAS Transform Column

       Applies `function` to each item in this column and returns the column
       of results.

       Arguments:
       - function: The function to apply to each element of `self` column.
       - on_missing: The value to return for missing cells. Ideally it should be
         replaced with a `skip_nulls` parameter like elsewhere, but currently
         that is not possible due to a bug:
         https://github.com/oracle/graal/issues/4741

       > Example
         Multiply each element of the column by itself.

             import Standard.Examples

             example_map = Examples.integer_column.map (x -> x * x)
    map : (Any -> Any) -> Column
    map self function on_missing=Nothing =
        storage = self.java_column.getStorage
        new_st = storage.map Nothing function on_missing Nothing
        col = Java_Column.new "Result" new_st
        Column.Value col

    ## ALIAS Transform Columns

       Applies `function` to consecutive pairs of elements of `self` and `that`
       and returns a column of results.

       Arguments:
       - that: The column to zip with `self`.
       - function: A binary function that is applied to corresponding pairs of
         elements of `self` and `that` to produce a value.
       - skip_missing: controls whether missing values should be passed to the
         `function`. The default value of `True` will skip the rows for which
         the value in either column is missing and automatically append
         `Nothing` to the result table. If set to `False`, every pair of values
         is passed to `function`.

       > Example
         Zip two columns together as pairs.

             import Standard.Examples

             example_zip =
                Examples.integer_column.zip Examples.text_column_1 [_, _]
    zip : Column -> (Any -> Any -> Any) -> Boolean -> Column
    zip self that function skip_missing=True =
        s1 = self.java_column.getStorage
        s2 = that.java_column.getStorage
        rs = s1.zip Nothing function s2 skip_missing Nothing
        Column.Value (Java_Column.new "Result" rs)

    ## ALIAS Rename Column

       Returns a new column, containing the same elements as `self`, but with
       the given name.

       Arguments:
       - name: The new name for the column.

       > Example
         Rename a column.

             import Standard.Examples

             example_rename = Examples.integer_column.rename "My Numbers"
    rename : Text -> Column
    rename self name = Column.Value (self.java_column.rename name)

    ## Returns the name of this column.

       > Example
         Get the name of a column.

             import Standard.Examples

             example_name = Examples.text_column_2.name
    name : Text
    name self = self.java_column.getName

    ## Returns the length of this column.

       > Example
         Get the length of a column.

             import Standard.Examples

             example_length = Examples.text_column_2.length
    length : Integer
    length self = self.java_column . getSize

    ## Returns the number of missing items in this column.

       > Example
         Count the number of missing values in a column.

             import Standard.Examples

             example_count_missing = Examples.text_column_2.count_missing
    count_missing : Integer
    count_missing self = self.java_column.getStorage.countMissing

    ## Returns the number of non-null items in this column.

       > Example
         Count the number of not missing values in a column.

             import Standard.Examples

             example_count = Examples.text_column_2.count
    count : Integer
    count self = self.length - self.count_missing

    ## Returns the value contained in this column at the given index.

       Arguments:
       - index: The index in the column from which to get the value.

       If the value is an NA then this method returns nothing. If the index is
       not an index in the column it returns an `Index_Out_Of_Bounds`.

       > Example
         Get the first element from a column.

             import Standard.Examples

             example_at = Examples.integer_column.at 0
    at : Integer -> (Any | Nothing) ! Index_Out_Of_Bounds
    at self index =
        valid_index = (index >= 0) && (index < self.length)
        if valid_index.not then Error.throw (Index_Out_Of_Bounds.Error index self.length) else
            storage = self.java_column.getStorage
            if storage.isNa index then Nothing else
                storage.getItem index

    ## UNSTABLE

       Returns a column containing rows of this column.

       Arguments:
       - max_rows: specifies a maximum amount of rows to fetch; if not set, all
         available rows are fetched.
    read : (Nothing | Integer) -> Column
    read self max_rows=Nothing =
        if max_rows.is_nothing then self else self.slice 0 max_rows

    ## Returns a vector containing all the elements in this column.

       > Example
         Get the elements of a column as a vector.

             import Standard.Examples

             example_to_vector = Examples.integer_column.to_vector
    to_vector : Vector
    to_vector self = Vector.from_polyglot_array self.java_column.getStorage.toList

    ## Returns the underlying storage type of this column.

       > Example
         Get the storage type of a column.

             import Standard.Examples

             example_storage_type = Examples.integer_column.storage_type
    storage_type : Storage
    storage_type self =
        tp = self.java_column.getStorage.getType
        Storage.from_java tp

    ## UNSTABLE TODO this is a prototype that will be revisited later on
    value_type : Value_Type
    value_type self = self.storage_type.to_approximate_value_type

    ## UNSTABLE

       Converts this column to JS_Object representation.

       > Example
         Get a JavaScript representation of the column.

             import Standard.Examples

             example_to_json = Examples.integer_column.to_js_object
    to_js_object : JS_Object
    to_js_object self =
        name = self.java_column.getName
        storage = self.java_column.getStorage
        storage_proxy = Array_Proxy.new storage.size i-> storage.getItem i
        storage_json = Vector.from_polyglot_array storage_proxy
        JS_Object.from_pairs [["name", name], ["data", storage_json]]

    ## Converts this column into a single-column table.

       > Example
         Convert a column to a table.

             import Standard.Examples

             example_to_table = Examples.integer_column.to_table
    to_table : Table
    to_table self = Table.Value self.java_column.toTable

    ## UNSTABLE

       Sorts the column according to the specified rules.

       Arguments:
       - order: specifies the default sort order for this operation.
       - missing_last: specifies the default placement of missing values when
         compared to non-missing ones. Note that this argument is independent
         from `order`, i.e. missing values will always be sorted according to
         this rule, ignoring the ascending / descending setting.
       - by: function taking two items in this column and returning an
         ordering. If specified, it is used instead of the natural
         ordering of the values.

       > Example
         Sorting a column in ascending order.

             import Standard.Examples

             example_sort = Examples.integer_column.sort

       > Example
         Sorting a column in descending order, placing missing values at the
         top of the resulting column.

             import Standard.Examples

             example_sort =
                 Examples.integer_column.sort Sort_Direction.Descending missing_last=False

       > Example
         Sorting `column` in ascending order, using a custom comparison
         function.

             import Standard.Examples

             example_sort =
                 my_compare a b = Ordering.compare a.abs b.abs
                 Examples.decimal_column.sort by=my_compare
    sort : Sort_Direction -> Boolean -> (Any -> Any -> Ordering) | Nothing -> Column
    sort self order=Sort_Direction.Ascending missing_last=True by=Nothing =
        order_bool = case order of
            Sort_Direction.Ascending -> True
            Sort_Direction.Descending -> False
        java_cmp = Comparator.new by
        rule = OrderBuilder.OrderRule.new self.java_column java_cmp order_bool missing_last
        mask = OrderBuilder.buildOrderMask [rule].to_array
        new_col = self.java_column.applyMask mask
        Column.Value new_col

    ## Creates a new Column with the specified range of rows from the input
       Column.

       Arguments:
       - range: The selection of rows from the table to return.
    take : (Index_Sub_Range | Range | Integer) -> Column
    take self range=(First 1) =
        Index_Sub_Range_Module.take_helper self.length self.at self.slice (slice_ranges self) range

    ## Creates a new Column from the input with the specified range of rows
       removed.

       Arguments:
       - range: The selection of rows from the table to remove.
    drop : (Index_Sub_Range | Range | Integer) -> Column
    drop self range=(First 1) =
        Index_Sub_Range_Module.drop_helper self.length self.at self.slice (slice_ranges self) range

    ## PRIVATE
       Returns a column with a continuous sub-range of rows taken.
    slice : Integer -> Integer -> Column
    slice self start end =
        length = self.length
        offset = Math.max (Math.min start length) 0
        limit = Math.max (Math.min (end - offset) (length - offset)) 0
        Column.Value (self.java_column.slice offset limit)

    ## UNSTABLE

       Returns the first element in the column, if it exists.

       If the column is empty, this method will return a dataflow error
       containing an `Index_Out_Of_Bounds`.

       > Example
         Get the first element of a column.

             import Standard.Examples

             example_first = Examples.integer_column.first
    first : Any ! Index_Out_Of_Bounds
    first self = self.at 0

    ## UNSTABLE

       Returns the last element in the column, if it exists.

       If the column is empty, this method will return a dataflow error
       containing an `Index_Out_Of_Bounds`.

       > Example
         Get the last element of a column.

             import Standard.Examples

             example_last = Examples.integer_column.last
    last : Any ! Index_Out_Of_Bounds
    last self = self.at (self.length - 1)

    ## UNSTABLE

       Returns a column containing the values of `self` column with their order
       reversed.

       > Example
         Reverse the values of a column to produce a new column.

             import Standard.Examples

             example_reverse = Examples.integer_column.reverse
    reverse : Column
    reverse self =
        mask = OrderBuilder.buildReversedMask self.length
        Column.Value (self.java_column.applyMask mask)

    ## UNSTABLE

       Returns a column of numbers, in which every entry denotes how many times
       the value at the given position occured before.

       > Example
         Count duplicate occurences of values in a column.

             import Standard.Examples

             example_duplicate_count = Examples.integer_column.duplicate_count
    duplicate_count : Column
    duplicate_count self = Column.Value self.java_column.duplicateCount

    ## PRIVATE
       Helper for the expression to tell it which functions needs a Vector.
    var_args_functions : Vector
    var_args_functions = ['is_in', 'coalesce', 'min', 'max']

## PRIVATE

   Folds the vectorized operation over the provided column and values. When more
   than one value to is provided, the result is folded with subsequent values.

   Arguments:
   - column: The column to execute the operation over.
   - name: The name of the vectorized operation.
   - fallback_fn: A function used if the vectorized operation isn't available.
   - operands: The vector of operands to apply to the function after `column`.
   - skip_nulls: Specifies if nulls should be skipped. If set to `True`, a null
     value results in null without passing it to the function. If set to
     `False`, the null values are passed as any other value and can have custom
     handling logic.
   - new_name: The name of the column created as the result of this operation.
run_vectorized_many_op : Column -> Text -> (Any -> Any -> Any) -> Vector -> Boolean -> Text -> Column
run_vectorized_many_op column name fallback_fn operands skip_nulls=False new_name=(name + "_" + column.name) =
    problem_builder = MapOperationProblemBuilder.new column.name
    result = case operands of
        _ : Vector ->
            folded = operands.fold column.java_column.getStorage current-> operand->
                case operand of
                    _ : Column ->
                        current.zip name fallback_fn operand.java_column.getStorage skip_nulls problem_builder
                    _ ->
                        current.bimap name fallback_fn operand skip_nulls problem_builder
            Column.Value (Java_Column.new new_name folded)
        _ : Array -> run_vectorized_many_op column name fallback_fn (Vector.from_polyglot_array operands) skip_nulls new_name
        _ -> run_vectorized_many_op column name fallback_fn [operands] skip_nulls new_name
    Problem_Behavior.Report_Warning.attach_problems_after result <|
        Java_Problems.parse_aggregated_problems problem_builder.getProblems

## PRIVATE

   Executes a vectorized binary operation over the provided column.

   Arguments:
   - column: The column to execute the operation over.
   - name: The name of the vectorized operation.
   - fallback_fn: A function used if the vectorized operation isn't available.
   - operand: The operand to apply to the function after `column`.
   - skip_nulls: Specifies if nulls should be skipped. If set to `True`, a null
     value results in null without passing it to the function. If set to
     `False`, the null values are passed as any other value and can have custom
     handling logic.
   - new_name: The name of the column created as the result of this operation.
run_vectorized_binary_op : Column -> Text -> (Any -> Any -> Any) -> Any -> Boolean -> Text -> Column
run_vectorized_binary_op column name fallback_fn operand skip_nulls=True new_name="Result" =
    problem_builder = MapOperationProblemBuilder.new column.name
    result = case operand of
        Column.Value col2 ->
            s1 = column.java_column.getStorage
            s2 = col2.getStorage
            rs = s1.zip name fallback_fn s2 skip_nulls problem_builder
            Column.Value (Java_Column.new new_name rs)
        _ ->
            s1 = column.java_column.getStorage
            rs = s1.bimap name fallback_fn operand skip_nulls problem_builder
            Column.Value (Java_Column.new new_name rs)
    Problem_Behavior.Report_Warning.attach_problems_after result <|
        Java_Problems.parse_aggregated_problems problem_builder.getProblems

## PRIVATE

   Executes a vectorized binary operation over the provided column.

   Arguments:
   - column: The column to execute the operation over.
   - name: The name of the vectorized operation.
   - fallback_fn: A function used if the vectorized operation isn't available.
     It takes a `MapOperationProblemBuilder` as its first argument.
   - operand: The operand to apply to the function after `column`.
   - location: The location to use in the reported problem details.
   - on_problems: Specifies how the problems should be handled.
   - skip_nulls: Specifies if nulls should be skipped. If set to `True`, a null
     value results in null without passing it to the function. If set to
     `False`, the null values are passed as any other value and can have custom
     handling logic.
   - new_name: The name of the column created as the result of this operation.
run_vectorized_binary_op_with_problem_handling column name fallback_fn operand location skip_nulls=True new_name="Result" =
    problem_builder = MapOperationProblemBuilder.new location
    applied_fn = fallback_fn problem_builder
    result = case operand of
        Column.Value col2 ->
            s1 = column.java_column.getStorage
            s2 = col2.getStorage
            rs = s1.zip name applied_fn s2 skip_nulls problem_builder
            Column.Value (Java_Column.new new_name rs)
        _ ->
            s1 = column.java_column.getStorage
            rs = s1.bimap name applied_fn operand skip_nulls problem_builder
            Column.Value (Java_Column.new new_name rs)
    Problem_Behavior.Report_Warning.attach_problems_after result <|
        Java_Problems.parse_aggregated_problems problem_builder.getProblems

## PRIVATE

   Executes a vectorized unary operation over the provided column.

   Arguments:
   - column: The column to execute the operation over.
   - name: The name of the vectorized operation.
   - fallback_fn: A function used if the vectorized operation isn't available.
   - on_missing: The value to return for missing cells. Ideally it should be
     replaced with a `skip_nulls` parameter like elsewhere, but currently that
     is not possible due to a bug: https://github.com/oracle/graal/issues/4741
run_vectorized_unary_op : Column -> Text -> (Any -> Any) -> Any -> Column
run_vectorized_unary_op column name fallback_fn on_missing=Nothing =
    problem_builder = MapOperationProblemBuilder.new column.name
    s = column.java_column.getStorage
    rs = s.map name fallback_fn on_missing problem_builder
    result = Column.Value (Java_Column.new "Result" rs)
    Problem_Behavior.Report_Warning.attach_problems_after result <|
        Java_Problems.parse_aggregated_problems problem_builder.getProblems

## PRIVATE

   Gets a textual representation of the item at position `ix` in `column`.

   Arguments:
   - column: The column to get the item from.
   - ix: The index in the column from which to get the item.
get_item_string : Column -> Integer -> Text
get_item_string column ix =
    item = column.getItem ix
    ## TODO This special handling of `Text` is because `"a".to_text` evaluates
       to "'a'" and not just "a". The code can be simplified once the following
       task is implemented:
       https://www.pivotaltracker.com/story/show/181499256
    case item of
        _ : Text -> normalize_string_for_display item
        _ -> item.pretty

## PRIVATE
   Ensures that the string can be safely displayed in a terminal.

   If the string contains special characters, it will be wrapped in quotes and
   the characters escaped. Otherwise, the string is returned as-is.
normalize_string_for_display string =
    prettified = string.pretty
    just_quoted = "'" + string + "'"
    if prettified == just_quoted then string else prettified

## PRIVATE
   A helper to create a new table consisting of slices of the original table.
slice_ranges column ranges =
    normalized = Index_Sub_Range_Module.normalize_ranges ranges
    Column.Value (column.java_column.slice normalized.to_array)

## PRIVATE
   Creates a storage builder suitable for building a column for the provided
   column type.

   This relies on a rudimentary mapping between `Value_Type` and `Storage`. It
   does not ensure validity checks for the particular type, like checking string
   length or number size.

   It may be tempting to return an `InferredBuilder` for the `Mixed` type - as
   this will use a more compact storage if a mixed type column contains only
   numbers. However, since currently `Column.value_type` is derived directly
   from its storage type, that would result in a changed `value_type` in the
   result. Whereas we want to ensure that if the requested type is `Mixed`, the
   resulting column should also report `Mixed` value type. Once the types work
   decouples `value_type` from `storage_type`, this logic could be adjusted.

   Due to the coupling of value types and storage, `value_type` of the created
   column may not be exactly the same as the one requested here, it will be the
   closest one currently supported by our storage (i.e. any constraints like
   integer size or constant text width will be dropped). This will need to be
   revisited as part of the types work:
   https://www.pivotaltracker.com/story/show/183854180
make_storage_builder_for_type value_type initial_size=128 =
    closest_storage_type = case value_type of
        Value_Type.Boolean -> Storage.Boolean
        Value_Type.Byte -> Storage.Integer
        Value_Type.Integer _ -> Storage.Integer
        Value_Type.Float _ -> Storage.Decimal
        ## Arbitrary precision numbers are not currently representable by our
           specialized in-memory storage, so falling back to object storage.
        Value_Type.Decimal _ _ -> Storage.Any
        Value_Type.Char _ _ -> Storage.Text
        Value_Type.Date -> Storage.Date
        Value_Type.Date_Time with_timezone ->
            ## Our specialized storage is only capable of storing date time with timezone. If we want to store a different kind of date-time, we will
            if with_timezone then Storage.Date_Time else Storage.Any
        Value_Type.Time -> Storage.Time_Of_Day
        Value_Type.Mixed -> Storage.Any
        _ -> Storage.Any
    Storage.make_builder closest_storage_type initial_size

## PRIVATE
   Helper for case case_sensitivity based text operations
run_vectorized_binary_case_text_op : Column -> Text -> (Text | Column) -> Case_Sensitivity -> (Text -> Text -> Any) -> Column
run_vectorized_binary_case_text_op left op other case_sensitivity fallback =
    Value_Type.expect_text left.value_type <| case case_sensitivity of
        Case_Sensitivity.Default -> run_vectorized_binary_op left op fallback other
        Case_Sensitivity.Sensitive -> run_vectorized_binary_op left op fallback other
        Case_Sensitivity.Insensitive _ ->
            ## TODO currently this always runs the fallback which is slow due to the
               cost of Java-to-Enso calls. We want to have a vectorized
               implementation, but we need to extend the architecture to allow
               passing the locale to it.
               See: https://www.pivotaltracker.com/n/projects/2539304/stories/184093260
            run_vectorized_binary_op left Nothing fallback other

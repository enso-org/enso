from Standard.Base import all
import Standard.Base.Data.Array_Proxy.Array_Proxy

import project.Data.Table.Table

## Represents a single row of some in-memory Table.
type Row
    ## PRIVATE
    Value (table:Table) (index:Integer)

    ## Gets the number of columns in the table.
    length : Integer
    length self = self.table.columns.length

    ## Gets the value of the specified column.

       Arguments:
       - column: The name or index of the column being looked up.
    at : (Integer | Text) -> Any
    at self column = self.table.at column . at self.index

    ## Gets the value of the specified column.

       Arguments:
       - column: The name or index of the column being looked up.
       - if_missing: The value to use if the column isn't present.
    get : (Integer | Text) -> Any -> Any
    get self column ~if_missing=Nothing =
        table_column = self.table.get column
        case table_column of
            Nothing -> if_missing
            _ -> table_column.at self.index

    ## Gets the row as a Vector.
    to_vector : Vector
    to_vector self = Vector.from_polyglot_array (Array_Proxy.from_proxy_object self)

    ## PRIVATE
       Converts this row into a JS_Object.
    to_js_object : Vector
    to_js_object self = self.to_vector.to_js_object

from Standard.Base import all

import Standard.Table.Data.Expression.Expression_Error

polyglot java import org.enso.table.error.ColumnCountMismatchException
polyglot java import org.enso.table.error.ColumnNameMismatchException
polyglot java import org.enso.table.error.EmptyFileException
polyglot java import org.enso.table.error.EmptySheetException


type Missing_Input_Columns
    ## PRIVATE
       One or more columns not found in the input table.
    Error (criteria : [Text])

    ## PRIVATE

       Convert a missing input error to a human-readable form.
    to_display_text : Text
    to_display_text self =
        "The criteria "+self.criteria.to_text+" did not match any columns."

type Column_Indexes_Out_Of_Range
    ## PRIVATE
       One or more column indexes were invalid on the input table.
       Can occur when using By_Index.
    Error (indexes : [Integer])

    ## PRIVATE

       Convert a column indexes out of bounds error to a human-readable form.
    to_display_text : Text
    to_display_text self = case self.indexes.length == 1 of
        True -> "The index " + (self.indexes.at 0).to_text + " is out of range."
        False -> "The indexes "+self.indexes.short_display_text+" are out of range."

type Too_Many_Column_Names_Provided
    ## PRIVATE
       More names than the column count provided to the function.
       Can occur when using By_Position.
    Error (column_names : [Text])

    ## PRIVATE

       Convert a too many columns error to a human-readable form.
    to_display_text : Text
    to_display_text self =
        "Too many column names provided. " + (self.column_names.at 0).to_text + " are unused."

## One or more column names were invalid during a rename operation.
type Invalid_Output_Column_Names
    Error (column_names : [Text])

    ## PRIVATE

       Pretty prints the invalid output columns error.
    to_display_text : Text
    to_display_text self = case self.column_names.length == 1 of
        True -> "The name " + (self.column_names.at 0).to_text + " is invalid."
        False -> "The names "+self.column_names.short_display_text+" are invalid."

## One or more column names clashed during a rename operation.
type Duplicate_Output_Column_Names
    Error (column_names : [Text])

    ## PRIVATE

       Pretty prints the duplicate output column names error.
    to_display_text : Text
    to_display_text self = case self.column_names.length == 1 of
        True -> "The name " + (self.column_names.at 0).to_text + " was repeated in the output, so was renamed."
        False -> "The names "+self.column_names.short_display_text+" were repeated in the output, and were renamed."

## No columns in the output result.
type No_Output_Columns
    ## PRIVATE

       Pretty prints the no output columns error.
    to_display_text : Text
    to_display_text self =
        "The result contains no columns."

## Indicates that one column has been matched by multiple selectors, resulting
   in ambiguous new names.
type Ambiguous_Column_Rename
    Error (column_name : Text) (new_names : Vector Text)

    ## PRIVATE

       Pretty prints the error.
    to_display_text : Text
    to_display_text self =
        'The column "' + self.column_name + '" is matched by multiple selectors, and it is ambiguous which of the following names it should be assigned: ' + self.new_names.short_display_text + "."

## Indicates that no input columns were selected for the operation, so the
   operation will cause no effect.
type No_Input_Columns_Selected
    to_display_text : Text
    to_display_text self =
        "No input columns have been selected for the operation."

## Indicates that an aggregation calculation could not be completed.
type Invalid_Aggregation
    Error (column:Text) (rows:[Integer]) (message:Text)

    to_display_text : Text
    to_display_text self =
        "The "+self.column+" could not be calculated at "+self.rows.short_display_text+": "+self.message

## Indicates that some operation relies on equality on floating-point values,
   which is not recommended.
type Floating_Point_Equality
    Error (location:Text)

    to_display_text : Text
    to_display_text self =
        "Relying on equality of floating-point numbers is not recommended (within "+self.location+")."

## Indicates that a text value with a delimiter was included in a concatenation without any quote character
type Unquoted_Delimiter
    Error (column:Text) (rows:[Integer])

    to_display_text : Text
    to_display_text self =
        "The "+self.column+" at rows "+self.rows.short_display_text+" contains the delimiter and there is no specified quote character."

## Warning when additional warnings occurred.
type Additional_Warnings
    Error (count:Integer)

    to_display_text : Text
    to_display_text self =
        "There were "+self.count.to_text+" additional issues."

## Indicates that when loading a delimited file, a row was encountered which had
   too many or too few columns.

   Only the first 10 rows are reported, any additional ones are aggregated into
   a single instance of `Additional_Invalid_Rows`.
type Invalid_Row
    Error (source_file_line_number : Integer) (index : Integer | Nothing) (row : [Text]) (expected_columns : Integer)

    to_display_text : Text
    to_display_text self =
        table_loc = case self.index of
            Nothing -> ""
            _ -> ", table row "+self.index.to_text
        loc = "(line "+self.source_file_line_number.to_text+table_loc+")"
        got_columns = self.row.length
        count_msg = if got_columns > self.expected_columns then "too many" else "too few"
        "The row " + loc + " had " + count_msg + " columns (expected " + self.expected_columns.to_text + ", got " + got_columns.to_text + ")."


## Indicates how many additional `Invalid_Row` warnings have been suppressed.
type Additional_Invalid_Rows
    Error (count : Integer)

    to_display_text : Text
    to_display_text self =
        "There were "+self.count.to_text+" additional invalid rows."

## Indicates that a quote inside of a delimited file cell has been opened but
   never closed.
type Mismatched_Quote
   to_display_text : Text
   to_display_text self =
       "A quote has been opened but never closed inside of a Delimited file, so it cannot be interpreted properly."

## Indicates an unexpected parser error.
type Parser_Error
    Error cause

## Indicates that quoting was disabled for a `Delimited` file, but some cells
   contained characters that do need quoting which may cause the output file to
   be corrupted.
type Unquoted_Characters_In_Output
    Warning (column : Text) (rows : [Integer])

    to_display_text : Text
    to_display_text self =
        altered_rows = self.rows.map ix->
            if ix == -1 then "the header" else ix
        "The "+self.column+" at rows "+altered_rows.short_display_text+" contains characters that need quoting, but quoting is disabled. The generated file may be corrupted."

    to_text : Text
    to_text self = "Unquoted_Characters_In_Output.Warning "+self.column.pretty+" "+self.rows.to_text

## Indicates that a specified location was not valid.
type Invalid_Location
    Error (location:Text)

    to_display_text : Text
    to_display_text self =
        "The location '"+self.location+"' is not valid."

## Indicates that some values did not match the expected datatype format.

   Arguments:
   - column: the column in which the problematic cells appeared, if applicable.
     It may be empty if the value is parsed outside of a context of a column.
   - datatype: The expected datatype.
   - cells: Contents of the cells that did not match the expected datatype
     format.
type Invalid_Format
    Error column:(Text|Nothing) (datatype:(Integer|Number|Date|Time|Time_Of_Day|Boolean)) (cells:[Text])

    to_display_text : Text
    to_display_text self =
        self.cells.length+" cells in column "+self.column+" had invalid format for datatype "+self.datatype.to_text+"."

## Indicates that some values contained leading zeros even though these were not allowed.

   Arguments:
   - column: the column in which the problematic cells appeared, if applicable.
     It may be empty if the value is parsed outside of a context of a column.
   - datatype: The expected datatype.
   - cells: Contents of the cells that contained leading zeros.
type Leading_Zeros
    Error column:(Text|Nothing) (datatype:(Integer|Number|Date|Time|Time_Of_Day|Boolean)) (cells:[Text])

    to_display_text : Text
    to_display_text self = "Leading zeros in column "+self.column+" with datatype "+self.datatype.to_text+"."

## Indicates that an empty file was encountered, so no data could be loaded.
type Empty_File_Error

    to_display_text : Text
    to_display_text = "It is not allowed to create a Table with no columns, so an empty file could not have been loaded."

    ## PRIVATE
    handle_java_exception =
        Panic.catch EmptyFileException handler=(_ -> Error.throw Empty_File_Error)

## Indicates that an empty sheet was encountered, so no data could be loaded.
type Empty_Sheet_Error

    to_display_text : Text
    to_display_text = "It is not allowed to create a Table with no columns, so an empty sheet could not have been loaded."

    ## PRIVATE
    handle_java_exception =
        Panic.catch EmptySheetException handler=(_ -> Error.throw Empty_Sheet_Error)

## Indicates that multiple `Column_Type_Selector` match the same column.

   If all matching selectors indicate the same type, the warning is reported but
   a parse is attempted anyway. If mixed types are requested, the column is not
   parsed due to ambiguity.
type Duplicate_Type_Selector
    Error column:Text ambiguous:Boolean

    to_display_text : Text
    to_display_text self = "Duplicate type selector for column " + self.column + "."

## Indicates that the column was already present in the table.
type Existing_Column
    Error column_name

    to_display_text : Text
    to_display_text self = "The column '" + self.column_name + "' already exists, but `Set_Mode.Add` was selected."

## Indicates that the column was not present in the table.
type Missing_Column
    Error column_name

    to_display_text : Text
    to_display_text self = "The column '" + self.column_name + "' was not found, but `Set_Mode.Update` was selected."

## Indicates that the target range contains existing data and the user did not
   specify to overwrite.
type Existing_Data
    Error message

    to_display_text : Text
    to_display_text self = "Existing data found: " + self.message

## Indicates that the specified range is not large enough to fit the data.
type Range_Exceeded
    Error message

    to_display_text : Text
    to_display_text self = "Range exceeded: " + self.message

## Indicates that the existing table has a different number of columns to the
   new table.
type Column_Count_Mismatch
    Error expected actual

    to_display_text : Text
    to_display_text self =
        "Expected " + self.expected.to_text + " columns, got " + self.actual.to_text + "."

    ## PRIVATE
    handle_java_exception =
        throw_column_count_mismatch caught_panic =
            cause = caught_panic.payload
            Error.throw (Column_Count_Mismatch.Error cause.getExpected cause.getActual)
        Panic.catch ColumnCountMismatchException handler=throw_column_count_mismatch

## Indicates that the existing table has a different set of column names to the
   new table.
type Column_Name_Mismatch
    Error missing extras message

    to_display_text : Text
    to_display_text self = self.message

    ## PRIVATE
    handle_java_exception =
        throw_column_name_mismatch caught_panic =
            cause = caught_panic.payload
            Error.throw (Column_Name_Mismatch.Error (Vector.from_polyglot_array cause.getMissing) (Vector.from_polyglot_array cause.getExtras) cause.getMessage)
        Panic.catch ColumnNameMismatchException handler=throw_column_name_mismatch

## UNSTABLE

   An error returned when a non-existent column is being looked up.

   Arguments:
   - column_name: The name of the column that doesn't exist.
type No_Such_Column
    Error column_name

    ## PRIVATE

       Create a human-readable version of the no such column error.
    to_display_text : Text
    to_display_text self =
        "The column " + self.column_name + " does not exist."

## UNSTABLE

   An error returned when getting an index but no index is set for that table.
type No_Index_Set_Error
    ## PRIVATE

       Create a human-readable version of the no index set error.
    to_display_text : Text
    to_display_text self = "The table does not have an index set."

type Invalid_Value_Type
    Error expected actual related_column=Nothing

    ## PRIVATE

       Create a human-readable version of the error.
    to_display_text : Text
    to_display_text self =
        prefix = case self.related_column of
            Nothing -> "Expected "
            column_name -> "Expected " + column_name + " column to have "
        prefix + self.expected.to_text + " type, but got " + self.actual.to_text + "."

## UNSTABLE

   An error representing an invalid JSON format for conversion.
type Invalid_JSON_Format
    Error input message

    ## PRIVATE

       Provides a human-readable representation of the Invalid_JSON_Format.
    to_display_text : Text
    to_display_text self =
        "The input " + self.input.to_text + " had an invalid format due to: " + self.message.to_text + "."

type Column_Type_Mismatch
    ## UNSTABLE

       An error indicating a mismatch of column types of merged columns.
    Error (column_name : Text) (expected_type : Text) (got_type : Text)

    ## PRIVATE

       Create a human-readable version of the error.
    to_display_text : Text
    to_display_text self =
        "The column ["+self.column_name+"] expects type "+self.expected_type+" but one of the provided tables had type "+self.got_type+" which is not compatible with it."

type No_Common_Type
    ## UNSTABLE

       An error indicating that no common type could be found for the merged
       columns.
    Error (column_name : Text)

    ## PRIVATE

       Create a human-readable version of the error.
    to_display_text : Text
    to_display_text self =
        "No common type could have been found for the columns corresponding to ["+self.column_name+"]. If you want to allow mixed types, please retype the columns to the `Mixed` before the concatenation (note however that most Database backends do not support `Mixed` types, so it may work only for the in-memory backend)."

type Unmatched_Columns
    ## UNSTABLE

       An error indicating that some columns were not present in all of the
       merged tables.
    Error (column_names : Vector Text)

    to_display_text : Text
    to_display_text self =
        "The following columns were not present in some of the provided tables: " + (self.column_names.map (n -> "["+n+"]") . join ", ") + ". The missing values have been filled with `Nothing`."

type Cross_Join_Row_Limit_Exceeded
    ## Indicates that a `cross_join` has been attempted where the right table
       has more rows than allowed by the limit.
    Error (limit : Integer) (existing_rows : Integer)

    to_display_text : Text
    to_display_text self =
        "The cross join operation exceeded the maximum number of rows allowed. The limit is "+self.limit.to_text+" and the number of rows in the right table was "+self.existing_rows.to_text+". The limit may be turned off by setting the `right_row_limit` option to `Nothing`."

type Row_Count_Mismatch
    ## Indicates that the row counts of zipped tables do not match.
    Error (left_rows : Integer) (right_rows : Integer)

    to_display_text : Text
    to_display_text self =
        "The number of rows in the left table ("+self.left_rows.to_text+") does not match the number of rows in the right table ("+self.right_rows.to_text+")."

type Invalid_Aggregate_Column
    ## Indicates that a provided name is not found within available column not
       represents a valid expression.
    Error (name : Text) (expression_error : Expression_Error | No_Such_Column | Nothing)

    to_display_text : Text
    to_display_text self =
        "The name ["+self.name+"] is not a valid column name nor expression."

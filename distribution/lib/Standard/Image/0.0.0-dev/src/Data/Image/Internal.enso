from Standard.Base import all

import Standard.Image.Data.Image
import Standard.Image.Data.Matrix

polyglot java import org.enso.image.data.Image as Java_Image
polyglot java import org.opencv.core.Mat
polyglot java import org.opencv.core.Scalar

## PRIVATE

   Apply a core matrix operation.

   Arguments:
   - mat: The matrix to operate on.
   - value: The value to apply to the matrix.
   - function: The function with which to apply `value` to `mat`.
core_op : Mat -> Any -> (Mat -> Scalar -> Mat -> Nothing) -> Nothing
core_op mat value function =
    result = Mat.new
    scalar = case value of
        Vector.Vector_Data arr ->
            Scalar.new arr
        Matrix.Matrix_Data m ->
            if ((m.rows == mat.rows) && (m.cols == mat.cols) && (m.channels == mat.channels)) then m else Panic.throw Matrix.Dimensions_Not_Equal
        _ ->
            Scalar.all value
    function mat scalar result
    Image.Image_Data result

## PRIVATE

   Handles errors in `core_op`.

   Arguments:
   - error: The value to throw as an error.
core_op_handler : Any
core_op_handler error =
    case error of
        Matrix.Dimensions_Not_Equal -> Error.throw error
        _ -> Panic.throw error

